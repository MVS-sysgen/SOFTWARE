 PROC 0 DEBUGUID(DUMMY)        -
 LOADLIB('SYSGEN.KLINGON.LOAD') -
 FREEZEDS(&SYSUID..KLINGON.FREEZE)            -
 HELPFILE(SYSGEN.KLINGON.ASSETS(#KLHELP)) -
 GRAPHIC(SYSGEN.KLINGON.ASSETS(#GRAFICS))
 CONTROL NOMSG NOPROMPT NOCONLIST NOSYMLIST FLUSH NOLIST
 /*********************************************************************
 /*
 /*  COMMENTS ON PARMS PASSED TO THIS CLIST
 /*
 /*  DEBUGUID = HERC01 THAT THE TRACE DEBUGING FILE (10) IS TO
 /*             BE ALLOCATED FOR, SPECIFY AS DUMMY IF NOT DESIRED
 /*             (I.E. DEBUGUID(DUMMY))
 /*
 /*  LOADLIB  = NAME OF LOAD LIBRARY WHERE THE KL AND KLINIT PROGRAMS
 /*             ARE LINKED IN
 /*
 /*  FREEZEDS = THE NAME FOR THE FREEZE FILE, IN CASE
 /*             GAME FROZEN
 /*
 /*  HELPFILE = NAME OF DATASET WHERE KLINGON HELP DATA IS KEPT
 /*
 /*  GRAPHIC  = "GRAPHICS" IMAGES FOR 3270 TYPE TERMINALS
 /*
 /*********************************************************************
CONTROL NOFLUSH NOMSG END(OD)
 /*********************************************************************
 /*
 /* FT06F001 IS NOT USED, BUT SOME VERSIONS OF FORTRAN INSIST ON IT
 /* SO ALLOCATED HERE.
 /*
 /*********************************************************************
FREE  DD(FT06F001)
ALLOC DD(FT06F001) DA(*)
WRITE
WRITE
WRITE *** NOW ATTEMPTING TO ALLOCATE YOUR FREEZE FILE
WRITE *** IF NOT FOUND, IT WILL BE CREATED
FREE  DD(FT27F001)
ALLOC DA('&FREEZEDS') DD(FT27F001) OLD
IF &LASTCC = 0 THEN GOTO NOINIT
 WRITE INIT FREEZE FILE...
 FREE  DD(FT27F001)
 ALLOC DA('&FREEZEDS') SP(1 1) TRACKS DD(FT27F001) UNIT(SYSDA)
 CALL '&LOADLIB(INITKL)'
NOINIT: -
WRITE
WRITE *** FREEZE FILE ALLOCATION COMPLETE
WRITE
FREE ATTR(F)
ATTR F RECFM(F B) LRECL(80) BLKSIZE(3200) DSORG(PS)
WRITE
WRITE *** NOW CREATING HIGH SPEED WORK FILES
WRITE
FREE DD(FT99F001)
FREE DD(FT98F001)
FREE DD(FT97F001)
ALLOC DD(FT99F001) UNIT(SYSDA) USING(F) SP(1 1) CYL
ALLOC DD(FT98F001) UNIT(SYSDA) USING(F) SP(1 1) CYL
ALLOC DD(FT97F001) UNIT(SYSDA) USING(F) SP(1 1) CYL
 /********************************************************************
 /*
 /* UNIT=10 IS THE DEBUG FILE, WILL NOT ACTUALLY BE USED UNLESS
 /* DEBUG MODE IS ACTIVATED
 /* (ACTIVATED BY XDEBUG COMMAND WITH PASSWORD)
 /* (LIMITED HERE TO ALLOCATE ONLY IF THE DEBUGUID MATCHES
 /*  LOGONID BEING USED)
 /*
 /********************************************************************
 /* &SYSUID = &DEBUGUID THEN ALLOC DD(FT10F001) SYSOUT(X) HOLD REUSE
 /********************************************************************
 /*
 /*  UNIT 8  = SYSOUT TO SEND CERTIFICATES OF MERIT TO
 /*            (FOR WINNERS OF EXPERT AND EMERITUS GAMES)
 /*
 /*  UNIT 25 = FILES WHERE GRAPHICS FILES ARE KEPT, COPIED TO
 /*            HIGH SPEED WORK FILES
 /*
 /*  UNIT 26 = KLINGON HELP FILE (NOT IBM STANDARD HELP)
 /*
 /********************************************************************
  /*
 FREE DD(FT08F001,FT25F001,FT26F001)
 ALLOC DD(FT08F001) SYSOUT(A)  NOHOLD
 ALLOC DD(FT25F001) DA('&GRAPHIC') SHR
 ALLOC DD(FT26F001) DA('&HELPFILE') SHR
  /*
 CALL '&LOADLIB(KL)'
  /*
 FREE DA('&HELPFILE')
 /* E DA('&GRAP43')
 /* E DA('&GRAP24')
 FREE DA('&LOADLIB')
  /*
 FREE DD(FT27F001,FT08F001,FT26F001,FT99F001,FT98F001,FT06F001)
 FREE DD(FT25F001,FT97F001,FT10F001)
  /*
 WRITE
 WRITE ***********************************************************
 WRITE *** PLEASE ONLY KEEP THE FREEZE FILE IF YOU FROZE A GAME  *
 WRITE ***********************************************************
 WRITE
 WRITE
 WRITENR   DO YOU WISH TO KEEP THE FREEZE FILE (Y/N) ?
 READ &OPT
 IF &OPT EQ Y OR &OPT EQ YES THEN GOTO KEEP
 DELETE '&FREEZEDS'
 WRITE
 WRITE *** FREEZE FILE DELETED
 WRITE
 KEEP: END
