* note - not an exec; a cmsbatch fragment to create an exec...
*
FILEDEF  INMOVE  TERM ( RECFM F BLOCK 80 LRECL 80
FILEDEF  OUTMOVE DISK KGCCEG EXEC A  ( RECFM F BLOCK 80 LRECL 80
MOVEFILE
&CONTROL OFF NOMSG
*
*
* WHEN CALLED THE CMSUSER 'G' DISK WILL BE ACCESSED R/W AS 'N'
*
* KGCCEG KIKPPCOB
* -INCLUDE .../KIKPPCOB.C
* /*
*
*
* DEFAULT INPUT VARS
&FT       = C
&FM       = A
&ASMNAM   = ASSEMBLE
*
* ANALYSIS OF INPUT VARS
&IF &INDEX GT 1 &IF &2 NE * &FT = &2
&IF &INDEX GT 2 &IF &3 NE * &FM = &3
*
* MAKE SURE ENVIRONMENT LOOKS OK
-SANITY
&IF &INDEX GT 0 &GOTO -S2
&TYPE YOU DIDN'T SAY WHAT TO COMPILE!
&EXIT 999
-S2
EXEC KGCCGET
*
* PRESET RETURN-CODE VARS FOR 'REPORT CARD'
* -1 MEANS SKIPPED, -2 MEANS NOT USED
&COPYRET = -1
&COMPRET = -1
&ASMRET  = -1
&GENRET  = -1
&COP2RET = -1
*
* COPY SYSIN TO INPUT FILE
-COPY
FILEDEF  INMOVE  TERM ( RECFM F BLOCK 80 LRECL 80
FILEDEF  OUTMOVE DISK &1 &FT &FM ( RECFM F BLOCK 80 LRECL 80
MOVEFILE
&COPYRET = &RETCODE
&IF &COPYRET GT 0 &GOTO -RPTCARD
*
* SOURCE, FOLLOWED BY A '/*', TO FOLLOW EXEC...
*
* FINAL GCC PASS
-COMP
ERASE   &1 WORK &FM
FILEDEF SYSPRINT TERM               ( RECFM F  BLOCK 80  LRECL 80
FILEDEF SYSPARM  DISK GCC PARM *
FILEDEF OUT      DISK &1 WORK &FM   ( RECFM FB BLOCK 480  LRECL 80
FILEDEF SYSIN    DISK &1 &FT  &FM
&NAME = &CONCAT  &FN .C
* NOTE USE OF GCC370 ALLOWING USE ON NON-380 6PK
GCC370
&COMPRET = &RETCODE
&IF &COMPRET GT 4 &GOTO -RPTCARD
* ASSEMBLE THE COMPILER OUTPUT
ERASE  &1 ASSEMBLE &FM
ERASE  &1 LISTING  &FM
RENAME &1 WORK &FM = ASSEMBLE =
FILEDEF SYSPRINT CLEAR
FILEDEF SYSTERM  CLEAR
FILEDEF SYSPARM  CLEAR
FILEDEF OUT      CLEAR
FILEDEF SYSIN    CLEAR
GLOBAL MACLIB OSMACRO DMSGPI PDPCLIB
&ASMNAM &1 (NOTERM NOLIST
&ASMRET = &RETCODE
&IF &ASMRET GT 4 &GOTO -RPTCARD
-GEN
GLOBAL TXTLIB PDPCLIB
LOAD &1 (CLEAR
&GENRET = &RETCODE
&IF &GENRET GT 0 &GOTO -RPTCARD
GENMOD &1
&GENRET = &RETCODE
&IF &GENRET GT 0 &GOTO -RPTCARD
-COPY2
COPY &1 MODULE A = = N (REPLACE
&COP2RET = &RETCODE
*IF &COP2RET GT 0 &GOTO -RPTCARD
*
* GENERATE A 'REPORT CARD' FOR THIS RUN & CLEANUP...
-RPTCARD
FILEDEF * CLEAR
FILEDEF SYSPRINT TERM (RECFM F LRECL 80 BLOCK 80
FILEDEF SYSTERM  TERM (RECFM F LRECL 80 BLOCK 80
FILEDEF SYSIN   DUMMY (RECFM F LRECL 80 BLOCK 80
&WARN = 0
TYPE KBAR TYPE *
&TYPE &0 REPORT CARD FOR &1
&TYPE
KRPTRTN &COPYRET 0 4 40 COPY SRC  TO DISK
&IF &RETCODE GT &WARN &WARN = &RETCODE
KRPTRTN &COMPRET 4 4 40 GCC COMPILE
&IF &RETCODE GT &WARN &WARN = &RETCODE
KRPTRTN &ASMRET  4 4 40 ASSEMBLE
&IF &RETCODE GT &WARN &WARN = &RETCODE
KRPTRTN &GENRET  0 4 40 LOAD AND GENMOD
&IF &RETCODE GT &WARN &WARN = &RETCODE
KRPTRTN &COP2RET 0 4 40 COPY MODULE
&IF &RETCODE GT &WARN &WARN = &RETCODE
TYPE KBAR TYPE *
&IF &WARN EQ 0 &GOTO -RPT18
KBLOCK BAD
-RPT18
FILEDEF * CLEAR
&EXIT &WARN
/*
