* note - not an exec; a cmsbatch fragment to create an exec...
*
FILEDEF  INMOVE  TERM ( RECFM F BLOCK 80 LRECL 80
FILEDEF  OUTMOVE DISK KASM EXEC A  ( RECFM F BLOCK 80 LRECL 80
MOVEFILE
&CONTROL OFF NOMSG
*
* DEFAULT INPUT VARS
&FT       = ASSEMBLE
&FM       = A
&ASMNAM   = ASSEMBLE
*
* ANALYSIS OF INPUT VARS
&IF &INDEX GT 1 &IF &2 NE * &FT = &2
&IF &INDEX GT 2 &IF &3 NE * &FM = &3
*
* MAKE SURE ENVIRONMENT LOOKS OK
-SANITY
&IF &INDEX GT 0 &GOTO -S2
&TYPE YOU DIDN'T SAY WHAT TO COMPILE!
&EXIT 999
-S2
SET LDRTBLS 64
*
* PRESET RETURN-CODE VARS FOR 'REPORT CARD'
* -1 MEANS SKIPPED, -2 MEANS NOT USED
&COPYRET = -1
&ASMRET  = -1
&COP2RET = -1
*
* COPY SYSIN TO INPUT FILE
-COPY
FILEDEF  INMOVE  TERM ( RECFM F BLOCK 80 LRECL 80
FILEDEF  OUTMOVE DISK &1 &FT &FM ( RECFM F BLOCK 80 LRECL 80
MOVEFILE
&COPYRET = &RETCODE
&IF &COPYRET GT 0 &GOTO -RPTCARD
*
* SOURCE, FOLLOWED BY A '/*', TO FOLLOW EXEC...
*
* ASSEMBLE
-COMP
ERASE  &1 LISTING  &FM
FILEDEF *        CLEAR
GLOBAL MACLIB OSMACRO DMSGPI
&ASMNAM &1 (TERM
&ASMRET = &RETCODE
&IF &ASMRET GT 4 &GOTO -RPTCARD
-COPY2
COPY &1 TEXT A = = N (REPLACE
&COP2RET = &RETCODE
*IF &COP2RET GT 0 &GOTO -RPTCARD
*
* GENERATE A 'REPORT CARD' FOR THIS RUN & CLEANUP...
-RPTCARD
FILEDEF * CLEAR
FILEDEF SYSPRINT TERM (RECFM F LRECL 80 BLOCK 80
FILEDEF SYSTERM  TERM (RECFM F LRECL 80 BLOCK 80
FILEDEF SYSIN   DUMMY (RECFM F LRECL 80 BLOCK 80
&WARN = 0
TYPE KBAR TYPE *
&TYPE &0 REPORT CARD FOR &1
&TYPE
KRPTRTN &COPYRET 0 4 40 COPY SRC  TO DISK
&IF &RETCODE GT &WARN &WARN = &RETCODE
KRPTRTN &ASMRET  4 4 40 ASSEMBLE
&IF &RETCODE GT &WARN &WARN = &RETCODE
KRPTRTN &COP2RET 0 4 40 COPY TEXT
&IF &RETCODE GT &WARN &WARN = &RETCODE
TYPE KBAR TYPE *
&IF &WARN EQ 0 &GOTO -RPT18
KBLOCK BAD
-RPT18
FILEDEF * CLEAR
&EXIT &WARN
/*
