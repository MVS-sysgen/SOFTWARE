* note - not an exec; a cmsbatch fragment to create an exec...
*
FILEDEF  INMOVE  TERM ( RECFM F BLOCK 80 LRECL 80
FILEDEF  OUTMOVE DISK KGCCCSL EXEC A  ( RECFM F BLOCK 80 LRECL 80
MOVEFILE
&CONTROL OFF NOMSG
*
* ..KGCCCSL KIKFCP0$ * * * OSVSAM38
* ..-INCLUDE .../KIKFCP0$.C
* ../*
*    INCLUDE KIKENTRY
*    INCLUDE KIKFCP1$
* ../*
*
* DEFAULT INPUT VARS
&FT       = C
&FM       = A
&ASMNAM   = ASSEMBLE
&XLIB     =
*
* DEFAULT VARS (OTHER)
&N        = N
&TXTLIB   = KIKSLOD
*
* ANALYSIS OF INPUT VARS
&IF &INDEX GT 1 &IF &2 NE * &FT = &2
&IF &INDEX GT 2 &IF &3 NE * &FM = &3
&IF &INDEX GT 3 &IF &4 NE * &ASMNAM = &4
&IF &INDEX GT 4 &IF &5 NE * &XLIB = &5
*
* MAKE SURE ENVIRONMENT LOOKS OK
-SANITY
&IF &INDEX GT 0 &SKIP 2
&TYPE YOU DIDN'T SAY WHAT TO COMPILE!
&EXIT 999
&EN = &LENGTH &1
&IF &EN EQ 8 &SKIP 2
&TYPE &1 ISN'T 8 CHARS LONG!
&EXIT 999
* GET ENTRY POINT, MODULE, AND SUFFIX FROM NAME
&EN  = &SUBSTR &1 1 6
&MOD = &SUBSTR &1 4 3
&SUF = &SUBSTR &1 7 2
*
EXEC KGCCGET
*
* PRESET RETURN-CODE VARS FOR 'REPORT CARD'
* -1 MEANS SKIPPED, -2 MEANS NOT USED
&COP1RET = -1
&COP2RET = -1
&COMPRET = -1
&ASMRET  = -1
&LNKRET  = -1
&UNLKRET = -1
&CATRET  = -1
&ZAPRET  = -1
*
* ERASE FILES THIS EXEC WILL GENERATE
-ERASE
ERASE  &1 &FT         A
ERASE  &1 WORK        A
ERASE  &1 SYSLIN      A
ERASE  &1 ASSEMBLE    A
ERASE  &1 ASM2        A
ERASE  &1 TEXT        A
ERASE  &1 TEXT        &FM
ERASE  &1 CTXT        A
ERASE  &1 TXT         A
ERASE  &1 ZAP         A
ERASE  &1 LOADLIB     A
ERASE  &1 LISTING     A
ERASE  &1 LISTING     &FM
ERASE  &1 LKEDIT      A
ERASE  &1 LKEDIT      &FM
ERASE  CKSUM ASSEMBLE A
ERASE  CKSUM LISTING  A
ERASE  CKSUM TEXT     A
*
* COPY SYSIN TO COMPILER INPUT FILE
-COPY1
FILEDEF  INMOVE  TERM ( RECFM F BLOCK 80 LRECL 80
*ILEDEF  INMOVE  DISK &1 XC  &FM
FILEDEF  OUTMOVE DISK &1 &FT &FM ( RECFM F BLOCK 80 LRECL 80
MOVEFILE
&COP1RET = &RETCODE
&IF &COP1RET GT 0 &GOTO -RPTCARD
*
* COPY SYSIN TO LINKEDIT INPUT FILE
-COPY2
FILEDEF  INMOVE  TERM ( RECFM F BLOCK 80 LRECL 80
*ILEDEF  INMOVE  DISK &1 XL     &FM
FILEDEF  OUTMOVE DISK &1 SYSLIN &FM ( RECFM F BLOCK 80 LRECL 80
MOVEFILE
&COP2RET = &RETCODE
&IF &COP2RET GT 0 &GOTO -RPTCARD
*
* GCC
-COMP
FILEDEF SYSPRINT TERM               ( RECFM F  BLOCK 80  LRECL 80
FILEDEF SYSPARM  DISK GCC PARM *
FILEDEF OUT      DISK &1 WORK &FM   ( RECFM FB BLOCK 480  LRECL 80
FILEDEF SYSIN    DISK &1 &FT  &FM
&NAME = &CONCAT  &FN .C
* NOTE USE OF GCC370 ALLOWING USE ON NON-380 6PK
GCC370
&COMPRET = &RETCODE
&IF &COMPRET GT 4 &GOTO -RPTCARD
*
* ASSEMBLE THE COMPILER OUTPUT
-ASM
RENAME &1 WORK &FM = ASSEMBLE =
FILEDEF SYSPRINT CLEAR
FILEDEF SYSTERM  CLEAR
FILEDEF SYSPARM  CLEAR
FILEDEF OUT      CLEAR
FILEDEF SYSIN    CLEAR
*LOBAL MACLIB OSMACRO DMSGPI PDPCLIB OSVSAM38
GLOBAL MACLIB &XLIB OSMACRO DMSGPI PDPCLIB
&ASMNAM &1 (TERM
TYPE &1 LISTING  &FM
&ASMRET = &RETCODE
&IF &ASMRET GT 4 &GOTO -RPTCARD
*
* LINK EDIT THE PROGRAM
-LKED
FILEDEF * CLEAR
* FOR COMPILES
FILEDEF @@KSTRT  DISK @@KSTRT  TEXT &N
FILEDEF KIKBLLIN DISK KIKBLLIN TEXT &N
FILEDEF KIKCOBGL DISK KIKCOBGL TEXT &N
FILEDEF KIKCOBGX DISK KIKCOBGX TEXT &N
FILEDEF KIKENTRY DISK KIKENTRY TEXT &N
FILEDEF KIKGCCGL DISK KIKGCCGL TEXT &N
FILEDEF KIKGCCGX DISK KIKGCCGX TEXT &N
* FOR SIP
FILEDEF KIKASRB  DISK KIKASRB  TEXT &N
FILEDEF KIKLOAD  DISK KIKLOAD  TEXT &N
FILEDEF SVC99    DISK SVC99    TEXT &N
FILEDEF VCONSTB5 DISK VCONSTB5 TEXT &N
* FOR TCP
FILEDEF KIKCRLP  DISK KIKCRLP  TEXT &N
FILEDEF CHK3270  DISK CHK3270  TEXT *
FILEDEF CX58V107 DISK CX58V107 TEXT *
FILEDEF CX58V108 DISK CX58V108 TEXT *
FILEDEF GET3270  DISK GET3270  TEXT *
FILEDEF PGPL3270 DISK PGPL3270 TEXT *
FILEDEF PGT3270  DISK PGT3270  TEXT *
FILEDEF PUT3270  DISK PUT3270  TEXT *
FILEDEF WSFQRY   DISK WSFQRY   TEXT *
FILEDEF CM2FULL  DISK CM2FULL  TEXT *
FILEDEF ZCON     DISK ZCON     TEXT *
FILEDEF FSIO     DISK FSIO     TEXT *
FILEDEF WR3270   DISK WR3270   TEXT *
*
* I need my own (edited!) copy of DMSGIO, normally on maints 19D,
* because maints copy contains comments that the os lked hates!
*
FILEDEF DMSGIO   DISK DMSGIO   TEXT &N
FILEDEF CMSSYS   DISK CMSSYS   TEXT &N
FILEDEF CMSMIN   DISK CMSMIN   TEXT *
* FOR PCP
FILEDEF KIKACP   DISK KIKACP   TEXT *
* FOR PPCOB, PPGCC
FILEDEF KIKMACLB DISK KIKMACLB TEXT &N
* FOR THIS LINK...
COPY    &1 TEXT &FM  &1 CTXT &FM (REPLACE
FILEDEF &1       DISK &1       TEXT A
*LOBAL TXTLIB PDPCLIB
FILEDEF SYSLIB   DISK PDPCLIB TXTLIB *
*ILEDEF SYSPRINT DISK &1 LKEDIT &FM  <-- LKED IGNORES...
FILEDEF SYSLIN   DISK &1 SYSLIN &FM
LKED &1 ( LIST XREF MAP NOTERM
&LNKRET = &RETCODE
&IF &LNKRET LE 4 &GOTO -LINK1B
TYPE &1 LKEDIT
&GOTO -RPTCARD
-LINK1B
FILEDEF * CLEAR
*
* TURN THE LOADMOD BACK INTO A CMS TEXT DECK
-UNLKED
FILEDEF SYSPUNCH DISK &1 TXT &FM (RECFM F LRECL 80 BLOCK 80
KUNLKED &1 * &EN
&UNLKRET = &RETCODE
&IF &UNLKRET NE 0 &GOTO -RPTCARD
COPY &1 TXT &FM &1 TEXT  A (REPLACE
*
-CKSUM
-ASM2
-LKED2
-LINK2B
-UNLKED2
*
-CATLG
TXTLIB DEL &TXTLIB &1
&IF &RETCODE EQ 0 &GOTO -CATLG
TXTLIB ADD &TXTLIB &1
&CATRET = &RETCODE
&IF &CATRET NE 0 &GOTO -RPTCARD
ERASE &1 TEXT *
*
-CKSUM2
-ZAP
*
* GENERATE A 'REPORT CARD' FOR THIS RUN & CLEANUP...
-RPTCARD
FILEDEF * CLEAR
FILEDEF SYSPRINT TERM (RECFM F LRECL 80 BLOCK 80
FILEDEF SYSTERM  TERM (RECFM F LRECL 80 BLOCK 80
FILEDEF SYSIN   DUMMY (RECFM F LRECL 80 BLOCK 80
&WARN = 0
TYPE KBAR TYPE *
&TYPE &0 REPORT CARD FOR &1
&TYPE
KRPTRTN &COP1RET 0 4 40 COPY C SRC TO DISK
&IF &RETCODE GT &WARN &WARN = &RETCODE
KRPTRTN &COP2RET 0 4 40 COPY LNK CTL TO DISK
&IF &RETCODE GT &WARN &WARN = &RETCODE
KRPTRTN &COMPRET 4 4 40 GCC COMPILE
&IF &RETCODE GT &WARN &WARN = &RETCODE
KRPTRTN &ASMRET 4 4 40 ASSEMBLE
&IF &RETCODE GT &WARN &WARN = &RETCODE
KRPTRTN &LNKRET 4 4 40 LINK EDIT
&IF &RETCODE GT &WARN &WARN = &RETCODE
KRPTRTN &UNLKRET 0 4 40 UN LINK EDIT
&IF &RETCODE GT &WARN &WARN = &RETCODE
KRPTRTN &CATRET  0 4 40 CATALOG INTO &TXTLIB
&IF &RETCODE GT &WARN &WARN = &RETCODE
TYPE KBAR TYPE *
&IF &WARN EQ 0 &GOTO -RPT18
KBLOCK BAD
FILEDEF * CLEAR
&EXIT &WARN
-RPT18
FILEDEF * CLEAR
* ERASE FILES THIS EXEC GENERATED
-ERASE
ERASE  &1 &FT         A
ERASE  &1 WORK        A
ERASE  &1 SYSLIN      A
ERASE  &1 ASSEMBLE    A
ERASE  &1 ASM2        A
ERASE  &1 TEXT        A
ERASE  &1 TEXT        &FM
ERASE  &1 CTXT        A
ERASE  &1 TXT         A
ERASE  &1 ZAP         A
ERASE  &1 ZAPLOG      &N
ERASE  &1 LOADLIB     A
ERASE  &1 LISTING     A
ERASE  &1 LISTING     &FM
ERASE  &1 LKEDIT      A
ERASE  &1 LKEDIT      &FM
&EXIT  0
/*
