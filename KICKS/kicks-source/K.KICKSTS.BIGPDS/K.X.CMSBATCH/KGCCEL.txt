* note - not an exec; a cmsbatch fragment to create an exec...
*
FILEDEF  INMOVE  TERM ( RECFM F BLOCK 80 LRECL 80
FILEDEF  OUTMOVE DISK KGCCEL EXEC A  ( RECFM F BLOCK 80 LRECL 80
MOVEFILE
&CONTROL OFF NOMSG
*
*
* WHEN CALLED THE CMSUSER 'G' DISK WILL BE ACCESSED R/W AS 'N'
*
* ..KGCCE  KIKPCP1$
* ..-INCLUDE .../KIKPCP1$.C
* ../*
*   KGCCEL KIKPCP1$
*    INCLUDE KIKENTRY
*    INCLUDE KIKPCP1$
*    INCLUDE KIKACP
*    INCLUDE SVC99
*   /*
*
*
* DEFAULT INPUT VARS
&EN       = $1
&FM       = A
&ASMNAM   = ASSEMBLE
&TXTLIB   = KIKSLOD
*
* ANALYSIS OF INPUT VARS
&IF &INDEX GT 1 &IF &2 NE * &EN = &2
&IF &INDEX GT 2 &IF &3 NE * &FM = &3
*
* MAKE SURE ENVIRONMENT LOOKS OK
-SANITY
&IF &INDEX GT 0 &GOTO -S2
&TYPE YOU DIDN'T SAY WHAT TO LINK
&EXIT 999
-S2
EXEC KGCCGET
*
* PRESET RETURN-CODE VARS FOR 'REPORT CARD'
* -1 MEANS SKIPPED, -2 MEANS NOT USED
&COPYRET = -1
&LKEDRET = -1
&UNLKRET = -1
&CATLRET = -1
*
* COPY SYSIN TO INPUT FILE
-COPY
FILEDEF  INMOVE  TERM ( RECFM F BLOCK 80 LRECL 80
FILEDEF  OUTMOVE DISK &1 SYSLIN &FM ( RECFM F BLOCK 80 LRECL 80
MOVEFILE
&COPYRET = &RETCODE
&IF &COPYRET GT 0 &GOTO -RPTCARD
*
* LINK EDIT THE PROGRAM
-LKED
FILEDEF * CLEAR
* FOR COMPILES
FILEDEF @@KSTRT  DISK @@KSTRT  TEXT N
FILEDEF KIKBLLIN DISK KIKBLLIN TEXT N
FILEDEF KIKCOBGL DISK KIKCOBGL TEXT N
FILEDEF KIKCOBGX DISK KIKCOBGX TEXT N
FILEDEF KIKENTRY DISK KIKENTRY TEXT N
FILEDEF KIKGCCGL DISK KIKGCCGL TEXT N
FILEDEF KIKGCCGX DISK KIKGCCGX TEXT N
* FOR SIP
FILEDEF KIKASRB  DISK KIKASRB  TEXT N
FILEDEF KIKLOAD  DISK KIKLOAD  TEXT N
FILEDEF SVC99    DISK SVC99    TEXT N
FILEDEF VCONSTB5 DISK VCONSTB5 TEXT N
* FOR TCP
FILEDEF KIKCRLP  DISK KIKCRLP  TEXT N
FILEDEF CM2FULL  DISK CM2FULL  TEXT N
FILEDEF ZCON     DISK ZCON     TEXT N
FILEDEF FSIO     DISK FSIO     TEXT *
FILEDEF WR3270   DISK WR3270   TEXT *
FILEDEF DMSGIO   DISK DMSGIO   TEXT N
FILEDEF CMSSYS   DISK CMSSYS   TEXT N
* FOR PCP
FILEDEF KIKACP   DISK KIKACP   TEXT N
* FOR PPCOB, PPGCC
FILEDEF KIKMACLB DISK KIKMACLB TEXT N
FILEDEF KIKVALUE DISK KIKVALUE TEXT N
* FOR THIS LINK...
FILEDEF &1       DISK &1       TEXT A
GLOBAL TXTLIB PDPCLIB
FILEDEF SYSLIB   DISK PDPCLIB TXTLIB *
*ILEDEF SYSPRINT DISK &1 LKEDIT &FM  <-- LKED IGNORES...
FILEDEF SYSLIN   DISK &1 SYSLIN &FM
LKED &1 ( LIST XREF MAP NOTERM
&LKEDRET = &RETCODE
TYPE &1 LKEDIT
&IF &LKEDRET GT 4 &GOTO -RPTCARD
FILEDEF * CLEAR
COPY  &1 LKEDIT A = = N (REPLACE
*
* TURN THE LOADMOD BACK INTO A CMS TEXT DECK
-UNLKED
FILEDEF SYSPUNCH DISK &1 TXT &FM (RECFM F LRECL 80 BLOCK 80
KUNLKED &1 * &EN
&UNLKRET = &RETCODE
&IF &UNLKRET NE 0 &GOTO -RPTCARD
COPY &1 TXT &FM &1 TEXT N (REPLACE
COPY &1 TEXT N  &1 TEXT A (REPLACE
*
* PUT THE TEXT DECK INTO THE TXTLIB
-CATLG
TXTLIB DEL &TXTLIB &1
&IF &RETCODE EQ 0 &GOTO -CATLG
TXTLIB ADD &TXTLIB &1
&CATLRET = &RETCODE
&IF &CATLRET NE 0 &GOTO -RPTCARD
ERASE &1 TEXT *
*
* GENERATE A 'REPORT CARD' FOR THIS RUN & CLEANUP...
-RPTCARD
FILEDEF * CLEAR
FILEDEF SYSPRINT TERM (RECFM F LRECL 80 BLOCK 80
FILEDEF SYSTERM  TERM (RECFM F LRECL 80 BLOCK 80
FILEDEF SYSIN   DUMMY (RECFM F LRECL 80 BLOCK 80
&WARN = 0
TYPE KBAR TYPE *
&TYPE &0 REPORT CARD FOR &1
&TYPE
KRPTRTN &COPYRET 0 4 40 COPY SYSLIN
&IF &RETCODE GT &WARN &WARN = &RETCODE
KRPTRTN &LKEDRET 4 4 40 LINK EDIT
&IF &RETCODE GT &WARN &WARN = &RETCODE
KRPTRTN &UNLKRET 0 4 40 UN LINK EDIT
&IF &RETCODE GT &WARN &WARN = &RETCODE
KRPTRTN &CATLRET 0 4 40 CATALOG
&IF &RETCODE GT &WARN &WARN = &RETCODE
TYPE KBAR TYPE *
&IF &WARN EQ 0 &GOTO -RPT18
KBLOCK BAD
-RPT18
FILEDEF * CLEAR
&EXIT &WARN
/*
