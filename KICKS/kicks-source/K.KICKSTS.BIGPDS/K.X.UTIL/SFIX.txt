PROC 0 VER(V1R5M0) U(KICKS) S(KICKSSYS) UPDATE(UPDATE) DUMMY(.NU.)+
       X(KICKSTS)
 /* */
 /* CLIST TO MAKE JCL (ETAL) CHANGES FOR KICKS SOURCE */
 /* */
 /* TO RUN THIS TYPE                      */
 /* EXEC KICKSSYS.V1R5M0.CLIST(SFIX)      */
 /* */
 /* */
 /* FIRST GENERATE PDSUPDTE CONTROL CARDS */
 /*            AKA IPOUPDTE               */
 /* */
CONTROL NOMSG NOFLUSH
 /* */
SET &UU1 = &SYSUID
SET &UU2 = &SYSPREF
SET &UU3 = ????????  /* EDIT THIS LINE TO GET WHAT YOU WANT */
 /* */
SET &NM  = 1
SET &UU  = &UU1
 /* */
DOPROMPT: +
WRITE KICKS SOURCE CUSTOMIZATION
WRITE
WRITE 1.  USERID   IS &UU1
WRITE 2.  PREFIX   IS &UU2
WRITE 3. 'LITERAL' IS &UU3  (EDIT CLIST TO GET WHAT YOU WANT)
WRITE
WRITE
WRITE THE KICKS SOURCE HLQ (HIGH LEVEL QUALIFIER) WILL BE &NM  &UU
WRITE -- IF THIS IS *NOT* WHAT YOU WANT, PRESS ENTER TO
WRITE    CYCLE THRU THE LIST, OR TYPE NO TO QUIT.
WRITE -- IF IT *IS* WHAT YOU WANT THEN TYPE YES TO CONTINUE
READ &YN
IF &YN EQ Y THEN GOTO ITSYES
IF &YN EQ YES THEN GOTO ITSYES
IF &YN EQ N THEN EXIT
IF &YN EQ NO  THEN EXIT
IF &NM = 1 THEN DO
 SET &NM = 2
 SET &UU = &UU2
 GOTO DOPROMPT
 END
IF &NM = 2 THEN DO
 SET &NM = 3
 SET &UU = &UU3
 GOTO DOPROMPT
 END
SET &NM = 1
SET &UU = &UU1
GOTO DOPROMPT
ITSYES: SET &UU = &UU
 /* */
ERROR GOTO ERRDONE1
 SET &ZOS = 0
 SYSCALL TESTSUB ZOS
ERRDONE1: +
  ERROR OFF
 IF &ZOS > 0 THEN DO
  WRITE LOOKS LIKE ZOS
  END
 ELSE DO
  WRITE LOOKS LIKE TURNKEY MVS
  END
 /* */
DELETE ZFIX.SYSIN
FREE  FI(SYSIN)
CONTROL MSG   FLUSH
ALLOC FI(SYSIN) DA(ZFIX.SYSIN) NEW CATALOG +
 UNIT(SYSDA) SPACE(5) TRACKS
OPENFILE SYSIN OUTPUT
IF &ZOS > 0 THEN DO
 /* ZJOBCARD - USE JOB CLASS=A FOR Z/OS */
 WRITE -- WILL CHANGE JOB CLASS
 SET &SYSIN = &STR(CLASS=C<CLASS=A<&STR( )JOB&STR( )<)
 PUTFILE SYSIN
 /* ZJOBCARD - USE REGION=64M FOR Z/OS */
 WRITE -- WILL CHANGE REGION SIZES
 SET &SYSIN = &STR(REGION=5000K<REGION=64M<&STR( )JOB&STR( )<)
 PUTFILE SYSIN
 SET &SYSIN = &STR(REGION=6000K<REGION=64M<&STR( )JOB&STR( )<)
 PUTFILE SYSIN
 SET &SYSIN = &STR(REGION=7000K<REGION=64M<&STR( )JOB&STR( )<)
 PUTFILE SYSIN
 END
 /* ZUSRID - MATCH HLQ'S TO CURRENT USER ID */
SET &SYSIN = &STR(HERC01<&UU<
PUTFILE SYSIN
SET &SYSIN = &STR(K.S.<&UU&STR(.)S.<
PUTFILE SYSIN
SET &SYSIN = &STR(K.U.<&UU&STR(.)U.<
PUTFILE SYSIN
SET &SYSIN = &STR(K.X.<&UU&STR(.)X.<
PUTFILE SYSIN
SET &SYSIN = &STR(&STR(')K.<&STR(')&UU&STR(.)<
PUTFILE SYSIN
SET &SYSIN = &STR(&STR(=)K.<&STR(=)&UU&STR(.)<
PUTFILE SYSIN
SET &SYSIN = &STR(&STR((K.<&STR((&UU&STR(.)<
PUTFILE SYSIN
SET &SYSIN = &STR(.S.<.&S..<)
PUTFILE SYSIN
SET &SYSIN = &STR(.U.<.&U..<)
PUTFILE SYSIN
SET &SYSIN = &STR(.X.<.&X..<)
PUTFILE SYSIN
IF &ZOS = 0 THEN DO
 /* WRITE LOOKS LIKE TURNKEY */
 /* DEFAULT TCP 2$ IN CASE BAD (OLD) ZP60009 */
 WRITE -- WILL CHANGE TCP TO 2$
 SET &SYSIN = &STR(TCP&STR((1$&STR())<TCP&STR((2$&STR())<
 PUTFILE SYSIN
 END
IF &ZOS > 0 THEN DO
 /* ZASM - USE ASMA90 ASSEMBLER IN Z/OS */
 WRITE -- WILL CHANGE ASSEMBLER NAME
 SET &SYSIN = &STR(IFOX00<ASMA90<)
 PUTFILE SYSIN
 /* ZCOB - USE COBOL2 PROCS IN Z/OS */
 WRITE -- WILL CHANGE PROC NAMES TO USE COBOL2
 SET &SYSIN = &STR(KIKCOBCL<KIKCB2CL<)
 PUTFILE SYSIN
 SET &SYSIN = &STR(K2KCOBCL<KIKCB2CL<)
 PUTFILE SYSIN
 SET &SYSIN = &STR(KIKCOBCS<KIKCB2CS<)
 PUTFILE SYSIN
 SET &SYSIN = &STR(K2KCOBCS<KIKCB2CS<)
 PUTFILE SYSIN
 /* ZJCLLIB - USE Z/OS JCL PROCLIB IN Z/OS */
 WRITE -- WILL CHANGE JOBPROC TO JCLLIB
 SET &SYSIN = &STR(&STR( )DD&STR( )<&STR( )JCLLIB&STR( )<
 SET &SYSIN = &STR(&SYSIN.JOBPROC&STR( )<
 PUTFILE SYSIN
 SET &SYSIN = &STR(&STR( )DSN=<&STR( )ORDER=(<JOBPROC&STR( )<
 PUTFILE SYSIN
 SET &SYSIN = &STR(&STR(,)DISP=SHR<Z&STR()))<JOBPROC&STR( )<
 PUTFILE SYSIN
 END
CLOSFILE SYSIN
FREE FI(SYSIN)
 /* */
 /* THEN RUN PDSUPDTE WITH THOSE CONTROL CARDS */
 /* */
CONTROL NOMSG   FLUSH
 /* WOULD BE NICE TO DO THE FREE'S IN A SUBROUTINE, */
 /* BUT MVS38J CLISTS DO NOT HAVE 'SYSCALL'...      */
 /* */
 FREE  FI(SYSTERM)
 FREE  FI(SYSPRINT)
 FREE  FI(@CMSB)
 FREE  FI(@JCL)
 CONTROL MSG   FLUSH
 ALLOC FI(SYSIN) DA(ZFIX.SYSIN) OLD
 ALLOC FI(SYSTERM)  DA(*)
 ALLOC FI(SYSPRINT) DUMMY
  /* */
 ALLOC FI(@CMSB)    DA(&X..CMSBATCH)      SHR
 ALLOC FI(@JCL)     DA(&X..JCL)           SHR
  /* */
 CALL '&UU..&S..&VER..SKIKLOAD(PDSUPDTE)' '&UPDATE'
  /* */
  /* THEN GET RID OF THE SYSIN FILE */
  /*  AND FREE ALL THE OTHER ALLOCS */
  /* */
 CONTROL NOMSG NOFLUSH
 FREE  FI(SYSIN)
 FREE  FI(SYSTERM)
 FREE  FI(SYSPRINT)
 DELETE ZFIX.SYSIN
 FREE  FI(@CMSB)
 FREE  FI(@JCL)
 WRITE DONE!
 EXIT
  /* */
 TESTSUB: PROC 1 TEST
  SYSREF &TEST
  SET &TEST = &TEST + 1
  END
