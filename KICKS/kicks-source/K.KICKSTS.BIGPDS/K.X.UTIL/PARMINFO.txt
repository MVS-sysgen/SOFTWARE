       ID DIVISION.
       PROGRAM-ID.     PARMINFO.
       DATE-WRITTEN.   05/10/90.
       DATE-COMPILED.  TODAY.
       AUTHOR.         MIKE NOEL.
       REMARKS.        COPIES PARM INFO INTO DATASET.
      *
      * I (MIKE NOEL) WROTE THIS, BUT I'M NOT CERTAIN I 'OWN' IT
      * SINCE I ALMOST CERTAINLY WAS EITHER SOMEONES EMPLOYEE OR
      * SOMEONES CONSULTANT AT THE TIME. AND IT WAS PROBABLY BEFORE
      * THE 1990 'DATE-WRITTEN', THAT WAS JUST THE LAST TIME I
      * COPIED IT FROM WEREEVER I'D WRITTEN IT BEFORE...
      *
      * THIS IS SIMPLY MY 'QUIT CLAIM' DEED TO IT!
      *
      * CHARACTERS IN PARM ARE TRANSFERRED TO RECORD(S) IN
      * DD PARMCARD, EXCEPT THAT
      *  - ';' CHARACTER IS NOT TRANSFERRED, BUT CAUSES NEW RECORD
      *  - '\' CHARACTER IS TRANSLATED TO '/' CHARACTER
      *
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT PARMCARD   ASSIGN TO UT-S-PARMCARD.
       DATA DIVISION.
       FILE SECTION.
       FD  PARMCARD
           RECORDING MODE F
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORDS STANDARD.
       01  PARMCARD-RECORD.
           05  PR-TEXT OCCURS 80       PIC X.
       WORKING-STORAGE SECTION.
       77  WS-BEGIN PIC X(24) VALUE 'PARMINFO WORKING STORAGE'.
       77  I                           PIC S9(8) COMP SYNC.
       77  J                           PIC S9(8) COMP.
       LINKAGE SECTION.
       01  PARM-INFO.
           05  PARM-LENGTH             PIC S9(4) COMP.
           05  PARM-TEXT OCCURS 255    PIC X.
       PROCEDURE DIVISION USING PARM-INFO.
           OPEN OUTPUT PARMCARD.
           MOVE ALL SPACES TO PARMCARD-RECORD.
           MOVE 1 TO I, J.
       MOVE-LOOP.
           IF I > PARM-LENGTH THEN GO TO QUIT.
           IF PARM-TEXT(I) = ';' THEN
               WRITE PARMCARD-RECORD
               MOVE 1 TO J
               ADD  1 TO I
               MOVE ALL SPACES TO PARMCARD-RECORD
               GO TO MOVE-LOOP.
           IF PARM-TEXT(I) = '\' THEN
               MOVE '/' TO PARM-TEXT(I).
           MOVE PARM-TEXT(I) TO PR-TEXT(J).
           ADD 1 TO J.
           IF J > 80 THEN
               WRITE PARMCARD-RECORD
               MOVE 1 TO J
               MOVE ALL SPACES TO PARMCARD-RECORD.
           ADD  1 TO I
           GO TO MOVE-LOOP.
       QUIT.
           IF J > 1 THEN WRITE PARMCARD-RECORD.
           CLOSE PARMCARD.
           STOP RUN.

