         MACRO                                                          00010000
&NM      FDIN  &S,&EXLEN,&VALUES,&LEN=0,&MAX=,&EXIT=             89095  00020000
         GBLA  &FDCNTR,&FDOFFS                                   84146  00030000
         LCLA  &C,&I,&J,&K,&N,&U                                 84146  00040000
         LCLA  &T,&E,&O7,&O9,&DATA,&EXFG                         89095  00050000
         LCLC  &L,&FDCHAIN,&W,&UEX                               89095  00060000
         LCLB  &NOP,&IN,&SKPLEN,&INDAD                           81270  00070000
         LCLB  &NL,&DEBL,&DEBR,&DEBZ,&PADL,&PADR,&RADJ,&UP              00080000
         LCLB  &BLUE,&GREEN,&PINK,&RED,&TURQ,&WHITE,&YELLOW,&C1,&C2,&C3 00090000
         LCLB  &UNDER,&BLINK,&REVERSE,&MONO,&MDT                 87313  00100000
         LCLB  &INTENSE,&DETECT,&NONDISP,&NUMERIC,&SKIP,&PROTECT,&NULL  00110000
         LCLB  &DEFAULT,&PREV                                           00120000
&U       SETA  &FDOFFS                                           84146  00130000
&UEX     SETC  'AL2('.'&U'.')'                                   89095  00140000
&FDCNTR  SETA  &FDCNTR+1                                                00150000
&FDCHAIN SETC  'ZFD'.'&FDCNTR'                                          00160000
&C       SETA  &FDCNTR+1                                                00170000
         AIF   ('&S' EQ '*END' OR '&S' EQ 'END').NOOFF  END OF CHAIN    00180000
         AIF   ('&S' NE '*EXPAND').NOEXPND                       84214  00190000
         AIF   ('&NM' EQ '').NOXNAM                              84214  00200000
&NM      DC    (&FDOFFS)X'00'                                    84214  00210000
         AGO   .NOXCOM                                           84214  00220000
.NOXNAM  AIF   (&FDOFFS LT 1).NOXCOM                             84214  00230000
         DC    (&FDOFFS)X'00'                                    84214  00240000
.NOXCOM  AIF   ('&SYSLIST(2)' EQ 'NORESET').MEND                 84214  00250000
&FDOFFS  SETA  0                                                 84214  00260000
         AGO   .MEND                                             84214  00270000
.NOEXPND AIF   (T'&EXIT EQ 'O').NOUEX                            89095  00280000
         AIF   (N'&EXIT GE 2).CKUEXR                            GP07008 00290000
&UEX     SETC  '&UEX'.',SL2('.'&EXIT'.')'                        89095  00300000
&EXFG    SETA  1                                                 89095  00310000
         AGO   .NOUEX                                           GP07008 00320000
.CKUEXR  AIF   ('&EXIT(1)' NE 'R').BADUEX                       GP07008 00330000
&UEX     SETC  '&UEX'.',SL2('.'&EXIT(2)'.'-*)'                  GP07008 00340000
&EXFG    SETA  1                                                 89095  00350000
         AGO   .NOUEX                                           GP07008 00360000
.BADUEX  MNOTE 8,'FDIN: EXIT= ADDRESS MALFORMED'                GP07008 00370000
.NOUEX   AIF   (T'&MAX EQ 'O').NOMAXQ                            89095  00380000
         AIF   (T'&MAX EQ 'N').CKMAX                             89095  00390000
         MNOTE 4,'NON-NUMERIC MAX= NOT SUPPORTED'                89095  00400000
         AGO   .NOMAXQ                                           89095  00410000
.CKMAX   AIF   (&MAX LT 0).NOMAXQ                                89095  00420000
&I       SETA  &MAX                                              84146  00430000
         AGO   .HAVMAX                                           84146  00440000
.NOMAXQ  AIF   (T'&LEN NE 'N' OR '&LEN' EQ '0').NOLENQ           84146  00450000
         AIF   (&LEN LE 0).NOLENQ                                84146  00460000
&I       SETA  &LEN                                              84146  00470000
         AGO   .HAVMAX                                           84146  00480000
.NOLENQ  MNOTE 0,'LEN=/MAX= MISSING - WIDTH DEFAULTED TO 255'    84146  00490000
&I       SETA  255                                               84146  00500000
.HAVMAX  ANOP  ,                                                 84146  00510000
&FDOFFS  SETA  &FDOFFS+4+&I  SET OFFSET OF NEXT ENTRY            84146  00520000
         AIF   (K'&SYSLIST(0) LT 1 OR K'&SYSLIST(0) GT 4).NOOFF  84146  00530000
OFFS&SYSLIST(0) EQU &U,&I                                        84214  00540000
.NOOFF   AIF   ('&NM' EQ '').NONAME                              84146  00550000
&NM      EQU   *                                                        00560000
.NONAME  AIF   ('&S' NE 'END' AND '&S' NE '*END').PROCESS        84146  00570000
&FDCHAIN DC    AL1(0)        END OF FD LIST                             00580000
         MEXIT ,                                                        00590000
.PROCESS ANOP  ,                                                        00600000
&I       SETA  N'&SYSLIST                                               00610000
&J       SETA  1                                                        00620000
         AIF   ('&S'(1,1) NE '''').NOLIT                         84146  00630000
&SKPLEN  SETB  1                                                        00640000
.NOLIT   AIF   (&J GE &I).CHECK                                         00650000
&J       SETA  &J+1                                                     00660000
&L       SETC  '&SYSLIST(&J)'                                           00670000
         AIF   ('&L' EQ '').NOLIT                                       00680000
&NOP     SETB  (&NOP  OR '&L' EQ 'NOP')                                 00690000
&NL      SETB  (&NL  OR '&L' EQ 'NL' OR '&L' EQ 'NEWLINE')              00700000
&DEBL SETB (&DEBL OR '&L' EQ 'DEBL' OR '&L' EQ 'DEB' OR '&L' EQ 'DEBZ') 00710000
&DEBR    SETB  (&DEBR OR '&L' EQ 'DEBR' OR '&L' EQ 'DEB')               00720000
&DEBZ    SETB  (&DEBZ OR '&L' EQ 'DEBZ')                                00730000
&PADL    SETB  (&PADL OR '&L' EQ 'PADL' OR '&L' EQ 'PAD')               00740000
&PADR    SETB  (&PADR OR '&L' EQ 'PADR' OR '&L' EQ 'PAD')               00750000
&RADJ    SETB  (&RADJ OR '&L' EQ 'RADJ')                                00760000
&UP      SETB  (&UP OR '&L' EQ 'UP')                                    00770000
&UP      SETB  (&UP OR '&L' EQ 'UPPER')                                 00780000
&BLUE    SETB  (&BLUE OR '&L' EQ 'BLUE')                                00790000
&GREEN   SETB  (&GREEN OR '&L' EQ 'GREEN')                              00800000
&PINK    SETB  (&PINK OR '&L' EQ 'PINK')                                00810000
&RED     SETB  (&RED  OR '&L' EQ 'RED')                                 00820000
&TURQ    SETB  (&TURQ  OR '&L' EQ 'TURQ' OR '&L' EQ 'CYAN')      90326  00830000
&WHITE   SETB  (&WHITE OR '&L' EQ 'WHITE')                              00840000
&YELLOW  SETB  (&YELLOW OR '&L' EQ 'YELLOW')                            00850000
&BLUE    SETB  (&BLUE OR '&L' EQ 'BL')                          GP10240 00860000
&GREEN   SETB  (&GREEN OR '&L' EQ 'GR')                         GP10240 00870000
&PINK    SETB  (&PINK OR '&L' EQ 'PI')                          GP10240 00880000
&RED     SETB  (&RED  OR '&L' EQ 'RE')                          GP10240 00890000
&TURQ    SETB  (&TURQ  OR '&L' EQ 'TU' OR '&L' EQ 'CY')         GP10240 00900000
&WHITE   SETB  (&WHITE OR '&L' EQ 'WH')                         GP10240 00910000
&YELLOW  SETB  (&YELLOW OR '&L' EQ 'YE')                        GP10240 00920000
&MONO    SETB  (&MONO OR '&L' EQ 'MONO')                         87313  00930000
&UNDER   SETB  (&UNDER OR '&L' EQ 'UL' OR '&L' EQ 'UNDER')              00940000
&BLINK   SETB  (&BLINK OR '&L' EQ 'BLINK')                              00950000
&REVERSE SETB  (&REVERSE OR '&L' EQ 'REVERSE')                          00960000
&INTENSE SETB  (&INTENSE OR '&L' EQ 'INTENSE' OR '&L' EQ 'INT')         00970000
&DETECT  SETB  (&DETECT OR '&L' EQ 'DETECT' OR '&L' EQ 'LP')            00980000
&NONDISP SETB  (&NONDISP OR '&L' EQ 'NONDISP' OR '&L' EQ 'NDISP')       00990000
&NUMERIC SETB  (&NUMERIC OR '&L' EQ 'NUMERIC' OR '&L' EQ 'NUM')         01000000
&SKIP    SETB  (&SKIP OR '&L' EQ 'SKIP')                                01010000
&PROTECT SETB  (&PROTECT OR '&L' EQ 'PROTECT')                          01020000
&MDT     SETB  (&MDT OR '&L' EQ 'MDT')                           87313  01030000
&NULL    SETB  (&NULL OR '&L' EQ 'NULL')                         84146  01040000
&DEFAULT SETB  (&DEFAULT OR '&L' EQ 'DEFAULT' OR '&L' EQ 'DFLT')        01050000
&PREV    SETB  (&PREV OR '&L' EQ 'PREVIOUS' OR '&L' EQ 'PREV')          01060000
&INDAD   SETB  (&INDAD OR '&L' EQ '*')                           81270  01070000
         AIF   (&DATA NE 0).NOLIT                                       01080000
         AIF   (K'&L GT 4).OMLEN                                        01090000
&K       SETA  0                                                        01100000
&L       SETC  '&L'.'    '                                              01110000
&L       SETC  '&L'(1,4)                                                01120000
         AIF   ('&L' NE 'X   ').DT                              GP10240 01130000
&L       SETC  'HEX '                                           GP10240 01140000
.DT      AIF   (&K GE 35).OMLEN                                 GP07004 01150000
&K       SETA  &K+1                                                     01160000
&N       SETA  (&K-1)*4+1                                               01170000
&W      SETC  'CHARCON ASISADDRHEX SHEXBIT I   $I  D   $D  F   TIMETIMD*01180000
               DATEDATJWDAYMTH DAY MD  DMY MDY CHEXICM ICN IZ  IA  DCM *01190000
               DCN DZ  DA  EDATDATDCCHHTTR '(&N,4)              GP07004 01200000
         AIF   ('&L' NE '&W').DT                                        01210000
&DATA    SETA  &K                                                       01220000
         AIF   (&J EQ 2).OMSET                                          01230000
         AGO   .NOLIT                                                   01240000
.OMLEN   AIF   (&J NE 2 OR &SKPLEN).NOLIT                               01250000
 AIF (&NOP OR &IN OR &NL OR &DEBL OR &DEBR OR &DEBZ OR &PADL).OMSET     01260000
 AIF (&UP OR &PADR OR &RADJ OR &BLUE OR &GREEN OR &PINK OR &RED).OMSET  01270000
 AIF (&TURQ OR &WHITE OR &YELLOW OR &UNDER OR &BLINK OR &NULL).OMSET    01280000
 AIF (&REVERSE OR &INTENSE OR &DETECT OR &NONDISP OR &INDAD).OMSET      01290000
 AIF (&NUMERIC OR &SKIP OR &PROTECT OR &DEFAULT OR &PREV).OMSET         01300000
         AIF   (&MDT OR &MONO).OMSET                             87313  01310000
         AGO   .NOLIT                                                   01320000
.OMSET   ANOP  ,             EXPLICIT LENGTH OMITTED                    01330000
&SKPLEN  SETB  1             USE L'                                     01340000
         AGO   .NOLIT                                                   01350000
.CHECK   ANOP  ,                                                        01360000
&L       SETC  'L'''                                                    01370000
&IN      SETB  1                                                 84146  01380000
&T       SETA  128*&IN+64*&NOP+4*&EXFG+&INDAD                    89095  01390000
&E SETA 128*&NL+64*&DEBL+32*&DEBR+16*&DEBZ+8*&PADL+4*&PADR+2*&RADJ+&UP  01400000
&O7      SETA  128*&DEFAULT+64*&PREV+&MDT                        87313  01410000
&PROTECT SETB  (&PROTECT OR &SKIP)                                      01420000
&NUMERIC SETB  (&NUMERIC OR &SKIP)                                      01430000
&DETECT  SETB  (&DETECT  OR &NONDISP)                                   01440000
&INTENSE SETB  (&INTENSE OR &NONDISP)                                   01450000
&O7      SETA  &O7+32*&PROTECT+16*&NUMERIC+8*&INTENSE+4*&DETECT+2*&NULL 01460000
&C1      SETB  (&GREEN OR &TURQ OR &WHITE OR &YELLOW)                   01470000
&C2      SETB  (&RED OR &PINK OR &WHITE OR &YELLOW)                     01480000
&C3      SETB  (&BLUE OR &PINK OR &TURQ OR &WHITE)                      01490000
&O9      SETA  64*&C1+32*&C2+16*&C3+8*&MONO+4*&UNDER+2*&REVERSE+&BLINK  01500000
         AIF   ((&O7 NE 0 AND &O7 NE 128) OR &O9 NE 0).LONG      86244  01510000
&T       SETA  &T+48         USE FDPRT SHORT FORM                86244  01520000
         AIF   ('&S'(1,1) EQ '''').CSTRPRT                       86244  01530000
         AIF   ('&EXLEN' NE '' AND NOT &SKPLEN).EXLPRT           86244  01540000
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,&DATA,&LEN,&L&S),SL2(&S),&UEX      89095  01550000
         MEXIT ,                                                 86244  01560000
.EXLPRT  AIF   (K'&EXLEN LT 2).NORLPRT                           86244  01570000
         AIF   ('&EXLEN'(1,1) NE '(' OR '&EXLEN'(2,1) EQ '(').NORLPRT   01580000
&T       SETA  &T+2          ILEN IS REGISTER FORM               86244  01590000
.NORLPRT ANOP  ,                                                 86244  01600000
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,&DATA,&LEN,&EXLEN),SL2(&S),&UEX    89095  01610000
         MEXIT ,                                                 86244  01620000
.CSTRPRT ANOP  ,                                                 86244  01630000
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,1,&LEN,&L.ZFD&C.C),SL2(ZFD&C.C),&UEX      01640000
ZFD&C.C  DC    C&S                                               89095  01650000
         MEXIT ,                                                 86244  01660000
.LONG    AIF   ('&S'(1,1) EQ '''').CSTRING                       86244  01670000
         AIF   ('&EXLEN' NE '' AND NOT &SKPLEN).EXLEN                   01680000
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,&O9,&O7,&DATA,&LEN,&L&S),SL2(&S),&UEX     01690000
         MEXIT                                                          01700000
.EXLEN   AIF   (K'&EXLEN LT 2).NORLEN                            81270  01710000
         AIF   ('&EXLEN'(1,1) NE '(' OR '&EXLEN'(2,1) EQ '(').NORLEN    01720000
&T       SETA  &T+2          ILEN IS REGISTER FORM               81270  01730000
.NORLEN  ANOP  ,                                                 81270  01740000
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,&O9,&O7,&DATA,&LEN,&EXLEN),SL2(&S),&UEX   01750000
         MEXIT ,                                                        01760000
.CSTRING ANOP  ,                                                        01770000
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,&O9,&O7,1,&LEN,&L.ZFD&C.C),SL2(ZFD&C.C),&*01780000
               UEX                                               89095  01790000
ZFD&C.C  DC    C&S                                               89095  01800000
.MEND    MEND  ,                                                 84214  01810000
