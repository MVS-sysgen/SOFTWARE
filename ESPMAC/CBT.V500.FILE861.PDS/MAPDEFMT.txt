         MACRO ,                                                        00010000
&NM      MAPDEFMT &DSECT=YES,&PFX=PDD,&RETURN=,&VER=1           GP05180 00020000
.*                                                                      00030000
.*  MAPPING MACRO FOR SUBROUTINE SUBDEFMT (ADAPTED FROM @SERVICE PDSDE) 00040000
.*   1) WHENEVER USER READS A DIRECTORY BLOCK, STORE ADDRESS (OF LENGTH 00050000
.*      FIELD) IN pddNEXT, 2 (LENGTH OF LENGTH) IN pddINCR, AND         00060000
.*      BYTE -1 (BLOCK-1+(length)) IN pddLAST                           00070000
.*   2) COPY THE RECFM TO pddRECFM (NEEDED FOR LKED/BINDER vs OTHER)    00080000
.*   3) CALL SUBDEFMT, USE RESULTS, DO BXLE ON pddNEXT/pddINCR/pddLAST  00090000
.*      READ NEXT BLOCK IF BXLE FALLS THROUGH                           00100000
.*                                                                      00110000
.*   CHANGED FOR Y2K SUPPORT, ETC.                              GP98365 00120000
.*                                                                      00130000
         LCLC  &P                                                       00140000
&P       SETC  'PDD'                                                    00150000
         AIF   ('&PFX' EQ '').HAVEP                                     00160000
&P       SETC  '&PFX'                                                   00170000
.HAVEP   ANOP  ,                                                        00180000
&NM      MACMAPHD MAP,PFX=&PFX,DSECT=&DSECT,DFLT=DEFMT          GP05180 00190000
&P.NEXT  DC    A(0)          ADDRESS OF CURRENT MEMBER                  00200000
&P.INCR  DC    A(0)          BXLE INCREMENT FOR CURRENT MEMBER          00210000
&P.LAST  DC    A(0)          LAST USED BYTE IN BLOCK                    00220000
&P.RECFM DC    AL1(0)        RECFM                                      00230000
&P.TYPE  DC    X'00'         ENTRY TYPE                                 00240000
&P.TLKED EQU   X'80'           LINKAGE EDITOR ENTRY                     00250000
&P.TSPF  EQU   X'40'           SPF ENTRY                                00260000
&P.TWYL  EQU   X'20'           WYLBUR ENTRY (LOCAL)                     00270000
&P.TDTX  EQU   X'10'           IEBUPDTX ENTRY                           00280000
&P.FLAGS DC    X'00'         ENTRY FLAGS (FIELDS PRESENT)               00290000
&P.AOSLE EQU   X'80'           PRODUCED BY VS LINKAGE EDITOR            00300000
&P.FREAL EQU   X'20'           MAIN MEMBER/EPA PRESENT                  00310000
&P.FSSI  EQU   X'10'           SSI FIELD PRESENT                        00320000
&P.FAPF  EQU   X'08'           APF FIELD PRESENT                        00330000
&P.FSCTR EQU   X'04'           SCATTER LOAD MODULE                      00340000
&P.FBIND EQU   X'01'           PRODUCED BY BINDER               GP05180 00350000
&P.FLAG2 DC    X'00'         SECOND FLAG BYTE                   GP98365 00360000
&P.FLAG3 DC    X'00'         THIRD FLAG BYTE                    GP98365 00370000
&P.UDLEN DC    X'00'         NUMBER OF HALFWORDS OF USER DATA           00380000
&P.OSSI  DC    H'0'          OFFSET TO SSI FROM NAME                    00390000
&P.OAPF  DC    H'0'          OFFSET TO APF FROM NAME                    00400000
         DC    4H'0'         EXTRA FOR EXPANSION                GP98365 00410000
&P.CLRLN EQU   *-&P.TYPE       LENGTH TO CLEAR                          00420000
&P.RALIS DC    C' '          BLANK OR * FOR ALIAS OR Â¬ FOR SCLM GP98365 00430000
&P.RNAME DC    CL8' '        EDITED MEMBER NAME                 GP98365 00440000
&P.RTTR  DC    CL6' '        PRIMARY TTR                        GP05180 00450000
&P.RSSI  DC    CL8' '        SSI OR BLANKS                      GP98365 00460000
&P.RYMD  DC    CL10' '       CHANGE DATE YYYYMMDD               GP98365 00470000
&P.RJDAY DC    CL3' '        CHANGE DATE JJJ (JULIAN FORM)      GP98365 00480000
&P.RTIME DC    CL5' '        TIME MODIFED/ADDED   HH:MM         GP05180 00490000
&P.VVMM  DC    0CL5' '       SPF VERSION/MODIFICATION           GP98365 00500000
&P.RAPF  DC    CL4'AC=N',C' '  APF CODE > 0 OR BLANKS           GP98365 00510000
&P.SIZE  DC    0CL8' '       LOAD MODULE SIZE                   GP05180 00520000
&P.RLINE DC    CL8' '        CURRENT LINE COUNT                 GP05180 00530000
&P.RMAIN DC    CL8' '        MAIN MEMBER OR SPF UID OR BLANKS   GP98365 00540000
&P.REPAD DC    CL8' '        ENTRY POINT OFFSET OF MODULE       GP98365 00550000
&P.RMODE DC    CL4' '        AaRa/A3R2/etc. MODE                GP98365 00560000
&P.RATTR DC    6CL4' '       REFR/RENT/REUS/OVLY/NXEC/PAGE      GP98365 00570000
&P.RATTN EQU   (*-&P.RATTR)/L'&P.RATTR  NUMBER OF ENTRIES       GP98365 00580000
&P.RBLNK EQU   *-&P.RALIS      LENGTH TO BLANK                  GP98365 00590000
.MEND    MEND  ,                                                        00600000
