         MACRO ,                                         NEW ON GP12298 00010000
&NM      XXHHEX &LEN=R2,&WK=R3,&W2=R4,&MAXL=,&MAX=,&ERR=                00020000
.*   CHECK FOR A VALID HEX CONSTANT IN AN SCLINE/SCPAGE EXIT ROUTINE.   00030000
.*   CODE IS FUNNY, BUT DOESN'T NEED A TRT TABLE OR LOOP                00040000
.*   TRANSLATES TO HEX AND BACK, THEN COMPARES. CATCHES MOST ERRORS.    00050000
.*                                                                      00060000
         GBLC  &ZZXXHNM                                                 00070000
         LCLC  &LER,&LAB                                                00080000
&LER     SETC  '&ERR'                                                   00090000
         AIF   ('&LER' NE '').USERR                                     00100000
&LER     SETC  '&ZZXXHNM'.'R'     NORMAL ERROR LABEL                    00110000
.USERR   ANOP  ,                                                        00120000
&LAB     SETC  'ZZX'.'&SYSNDX'                                          00130000
&NM      MVC   DBWK,=8X'F0'  CLEAR OUTPUT                               00140000
         AIF   ('&MAXL' EQ '').NOLL                                     00150000
         CH    &LEN,=AL2(&MAXL)   VALID LENGTH ?                        00160000
         MACPARM &LER,OP=BH,OPR=BHR,MODE=ONE                            00170000
.NOLL    LR    &WK,&LEN      COPY LENGTH                                00180000
         BCTR  &WK,0         MAKE EXECUTE LENGTH                        00190000
         LA    &W2,DBWK+8                                               00200000
         SR    &W2,&LEN      LOCATION TO MOVE TO                        00210000
         EX    &WK,&LAB.M    MOVE TEXT                                  00220000
         NC    DBWK,=8X'1F'  KEEP ONLY SIGNIFICANT BITS                 00230000
         TR    DBWK,=X'000A0B0C0D0E0F0000000000000000000001020304050607*00240000
               0809000000000000'                                        00250000
         PACK  DB(5),DBWK(L'DBWK+1)                                     00260000
.*  CONVERTED TEXT IN DB(4); NOW DO BACKWADS TO CHECK VALIDITY          00270000
         UNPK  DB2(L'DB2+1),DB(5)  REVERSE                              00280000
         NC    DB2,=8X'0F'                                              00290000
         TR    DB2,=C'0123456789ABCDEF'                                 00300000
         MVC   DBWK,=8X'F0'  CLEAR OUTPUT                               00310000
         EX    &WK,&LAB.M    MOVE TEXT                                  00320000
         OC    DBWK,=8C' '   UPPER CASE TO ALLOW REVERSE COMPARE        00330000
         CLC   DB2,DBWK      SAME BOTH WAYS?                            00340000
         MACPARM &LER,OP=BNE,OPR=BNER,MODE=ONE                          00350000
         AIF   ('&MAX' EQ '').NOMAX                                     00360000
         CLC   DB(4),=A(&MAX)   VALID ?                                 00370000
         MACPARM &LER,OP=BH,OPR=BHR,MODE=ONE                            00380000
.NOMAX   B     *+4+6         CONTINUE PAST EXECUTIONERS                 00390000
&LAB.M   MVC   0(0,&W2),FIWTEXT                                         00400000
         MEND  ,                                                        00410000
