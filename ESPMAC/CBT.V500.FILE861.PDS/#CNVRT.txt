         MACRO ,                                                        00010000
&NM      #CNVRT &FUN,&ADR,&LEN,&OPF,&OPT=,&BZ=,&BNZ=,&BM=,&BP=  GP03122 00020000
         GBLC  &ZZCFXNM(32)                                             00030000
         GBLC  &ZZCFXF@                                         GP04055 00040000
         GBLC  &ZZCFXFC                                         GP04055 00050000
         GBLC  &MACPLAB                                                 00060000
         GBLB  &ZZCFXFG                                                 00070000
         GBLA  &ZZCFXFM                                                 00080000
.*--------------------------------------------------------------------* 00090000
.*                                                                    * 00100000
.*  MACRO #CNVRT IS USED BY FUNCTIONS @PRINTER, @SCREENS, AND EXHIBIT * 00110000
.*  MODULE EXHASCRN TO INVOKE COMMON DATA CONVERSION ROUTINES IN      * 00120000
.*  MODULE @FORMATS                                                   * 00130000
.*                                                                    * 00140000
.*--------------------------------------------------------------------* 00150000
         LCLA  &I                                                       00160000
&MACPLAB SETC  '&NM'                                                    00170000
         AIF   ('&OPT' EQ 'TABLE' OR '&OPT' EQ 'EXPAND').LOCOPT GP04055 00180000
         AIF   ('&OPT' EQ '').KEEPOPT                           GP04055 00190000
&ZZCFXFC SETC  '&OPT'                                           GP04055 00200000
.KEEPOPT AIF   ('&ZZCFXFC' NE '').LOCOPT                        GP04055 00210000
&ZZCFXFC SETC  'CALL'        DEFAULT INVOKATION MODE            GP04055 00220000
.LOCOPT  AIF   ('&ZZCFXF@' NE '').HAVEPT                        GP04055 00230000
&ZZCFXF@ SETC  '@FORMATS'    DEFAULT ADDRESS OF @FORMATS        GP04055 00240000
     AIF   ('&SYSECT' NE 'EXHASCRN' AND '&SYSECT' NE '@SCREENS').HAVEPT 00250000
&ZZCFXF@ SETC  'ADFORMAT'    ADDRESS OF @FORMATS                GP04055 00260000
.HAVEPT  AIF   (&ZZCFXFG).DEFINED                               GP04055 00270000
&ZZCFXFG SETB  1                                                        00280000
&ZZCFXNM(01) SETC 'DBK'      DEBLANK (STRIP "B")                        00290000
&ZZCFXNM(02) SETC 'DBKL'     DEBLANK (STRIP "H")                        00300000
&ZZCFXNM(03) SETC 'DBKR'     DEBLANK (STRIP "T")                        00310000
&ZZCFXNM(04) SETC 'DBKZ'     REMOVE LEADING HEX AND CHAR ZEROES         00320000
&ZZCFXNM(05) SETC 'CENTER'   DEBLANK, THEN CENTER                       00330000
&ZZCFXNM(06) SETC 'DATE'     8-CHAR DATE MM/DD/YY                       00340000
&ZZCFXNM(07) SETC 'TIME'     8-CHAR TIME; 24-HOUR                       00350000
&ZZCFXNM(08) SETC 'CONVERT'  NUMERIC (FD BASED) CONVERSION (INT->EXT)   00360000
&ZZCFXNM(09) SETC 'TREVNOC'  CONVERSION FROM EXTERNAL TO INTERNAL       00370000
&ZZCFXFM SETA  9                                                        00380000
.DEFINED AIF   ('&OPT' EQ 'TABLE').EXPAND                       GP04055 00390000
         AIF   ('&OPT' EQ 'EXPAND').EXPAND                      GP04055 00400000
.LOOKUP  AIF   (&I GE &ZZCFXFM).NOMATCH                                 00410000
&I       SETA  &I+1                                                     00420000
         AIF   ('&ZZCFXNM(&I)' EQ '&FUN').MATCH                         00430000
         AGO   .LOOKUP                                                  00440000
.NOMATCH MNOTE 8,'#CNVRT: FUNCTION &FUN NOT DEFINED'                    00450000
&I       SETA  8             SET FOR CONVERT (?)                        00460000
.MATCH   MACPARM R0,&LEN,NULL=SKIP   LOAD OPTIONS/LENGTH                00470000
         MACPARM R15,&OPF,NULL=SKIP  LOAD OPTIONS/OUTLEN                00480000
         MACPARM R1,&ADR,NULL=SKIP   LOAD ITEM ADDRESS                  00490000
         AIF   ('&ZZCFXFC' EQ 'CALL').LOAD@                     GP03287 00500000
         AIF   ('&ZZCFXFC' EQ 'CALLA').LOADA                    GP03287 00510000
         AIF   ('&ZZCFXFC' EQ 'CALLV').LOADV                    GP03287 00520000
         MNOTE 4,'#CNVRT: UNRECOGNIZED OPT=&ZZCFXFC '           GP03287 00530000
.LOADV   MACPARM R14,=V(@FORMATS),OP=L                          GP03287 00540000
         AGO   .LOADCOM                                         GP03287 00550000
.LOADA   MACPARM R14,=A(@FORMATS),OP=L                          GP03287 00560000
         AGO   .LOADCOM                                         GP03287 00570000
.LOAD@   MACPARM R14,&ZZCFXF@,OP=L   LOAD CONVERSION ROUTINE ADDRESS    00580000
.LOADCOM ANOP  ,                                                GP03287 00590000
&I       SETA  &I-1                                                     00600000
         AH    R14,32+&I*2(,R14)  GET FUNCTION ENTRY ADDRESS            00610000
         BASR  R14,R14       INVOKE FUNCTION                            00620000
 AIF ('&BZ' EQ '' AND '&BNZ' EQ '' AND '&BM' EQ '' AND '&BP' EQ '').MND 00630000
.*OOPS   LTR   R0,R0         SET CONDITION CODE                 GP05032 00640000
         MACPARM &BZ,MODE=ONE,NULL=SKIP,OP=BZ,OPR=BZR           GP05032 00650000
         MACPARM &BM,MODE=ONE,NULL=SKIP,OP=BM,OPR=BMR           GP05032 00660000
         MACPARM &BP,MODE=ONE,NULL=SKIP,OP=BP,OPR=BPR           GP05032 00670000
         MACPARM &BNZ,MODE=ONE,NULL=SKIP,OP=BNZ,OPR=BNZR        GP05032 00680000
.MND     MEXIT ,                                                        00690000
.EXPAND  AIF   (&I GE &ZZCFXFM).MEND                                    00700000
&I       SETA  &I+1                                                     00710000
&MACPLAB DC    AL2(&ZZCFXNM(&I)-&SYSECT)                                00720000
&MACPLAB SETC  ' '                                                      00730000
         AGO   .EXPAND                                                  00740000
.MEND    MEND  ,                                                        00750000
