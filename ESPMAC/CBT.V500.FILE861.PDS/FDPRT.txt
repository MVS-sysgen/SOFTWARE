         MACRO                                                          00010000
&NM      FDPRT &STR,&EXLEN,&VALUES,&LEN=0,                             *00020000
               &LABEL=,&LABOPT=PAD,&KEEP=0                       85119  00030000
.*                                                                      00040000
.*       PROVIDED FOR COMPATIBILITY WITH GOSSIP FD MACROS, BUT USING    00050000
.*       SHORTER DATA SECTION TO SAVE STORAGE                           00060000
.*         INTENDED FOR PRINT PROCESSING                                00070000
.*                                                                      00080000
         GBLA  &FDCNTR                                                  00090000
         LCLA  &C,&I,&J,&K,&N,&Z                                 85119  00100000
         LCLA  &T,&E,&O7,&O9,&DATA                                      00110000
         LCLC  &L,&FDCHAIN,&W                                           00120000
         LCLB  &NOP,&IN,&SKPLEN,&INDAD,&CNLOCK                   92086  00130000
         LCLB  &NL,&DEBL,&DEBR,&DEBZ,&PADL,&PADR,&RADJ,&UP              00140000
         LCLB  &BLUE,&GREEN,&PINK,&RED,&TURQ,&WHITE,&YELLOW,&C1,&C2,&C3 00150000
         LCLB  &UNDER,&BLINK,&REVERSE,&MONO,&MDT                 87313  00160000
         LCLB  &INTENSE,&DETECT,&NONDISP,&NUMERIC,&SKIP,&PROTECT        00170000
         LCLB  &DEFAULT,&PREV                                           00180000
&FDCNTR  SETA  &FDCNTR+1                                                00190000
&FDCHAIN SETC  'ZFD'.'&FDCNTR'                                          00200000
&C       SETA  &FDCNTR+1                                                00210000
         AIF   ('&NM' EQ '').NONAME                                     00220000
&NM      EQU   *                                                        00230000
.NONAME  AIF   ('&STR' NE 'END' AND '&STR' NE '*END').PROCESS           00240000
&FDCHAIN DC    AL1(0)        END OF FD LIST                             00250000
         MEXIT ,                                                        00260000
.PROCESS AIF   (T'&LABEL EQ 'O').PROCEED                         85118  00270000
         AIF   ('&KEEP' EQ '' OR '&KEEP' EQ '0').DEFKEEP         85119  00280000
&FDCHAIN DC    AL1(ZFD&C-*,30,0,&KEEP)  FDKEEP                   85119  00290000
         AGO   .DEFKCOM                                          85119  00300000
.DEFKEEP ANOP  ,                                                 85119  00310000
&Z       SETA  &C+1                                              85119  00320000
&FDCHAIN DC    AL1(ZFD&C-*,30,0,12+ZFD&Z-*)  FDKEEP              85119  00330000
.DEFKCOM ANOP  ,                                                 85119  00340000
&FDCNTR  SETA  &FDCNTR+1                                         85119  00350000
&FDCHAIN SETC  'ZFD'.'&FDCNTR'                                   85119  00360000
&C       SETA  &FDCNTR+1                                         85119  00370000
&I       SETA  12            DEFAULT PADL+PADR                   85118  00380000
         AIF   ('&LABOPT' EQ 'PAD').PROPAD                       85119  00390000
         AIF   ('&LABOPT' EQ '' OR '&LABOPT' EQ '0').PADNONE     85119  00400000
&I       SETA  8                                                 85118  00410000
         AIF   ('&LABOPT' EQ 'PADL').PROPAD                      85118  00420000
&I       SETA  4                                                 85118  00430000
         AIF   ('&LABOPT' EQ 'PADR').PROPAD                      85118  00440000
         MNOTE 4,'UNSUPPORTED LABOPT=&LABOPT'                    85118  00450000
.PADNONE ANOP  ,                                                 85119  00460000
&I       SETA  0                                                 85119  00470000
.PROPAD  AIF   ('&LABEL'(1,1) NE '''').LABNQ                     85118  00480000
&FDCHAIN DC    AL1(ZFD&C-*,48,&I,129,0,ZFD&C-*-1),C&LABEL        85118  00490000
         AGO   .PROCOM                                           85118  00500000
.LABNQ   ANOP  ,                                                 85118  00510000
&FDCHAIN DC    AL1(ZFD&C-*,48,&I,129,0,ZFD&C-*-1),C'&LABEL'      85118  00520000
.PROCOM  ANOP  ,                                                 85118  00530000
&FDCNTR  SETA  &FDCNTR+1                                         85118  00540000
&FDCHAIN SETC  'ZFD'.'&FDCNTR'                                   85118  00550000
&C       SETA  &FDCNTR+1                                         85118  00560000
.PROCEED ANOP  ,                                                 85118  00570000
&I       SETA  N'&SYSLIST                                               00580000
&J       SETA  1                                                        00590000
         AIF   ('&STR'(1,1) NE '''').NOLIT                              00600000
&SKPLEN  SETB  1                                                        00610000
.NOLIT   AIF   (&J GE &I).CHECK                                         00620000
&J       SETA  &J+1                                                     00630000
&L       SETC  '&SYSLIST(&J)'                                           00640000
         AIF   ('&L' EQ '').NOLIT                                       00650000
&NOP     SETB  (&NOP  OR '&L' EQ 'NOP')                                 00660000
&CNLOCK  SETB  (&CNLOCK OR '&L' EQ 'LOCK' OR '&L' EQ 'LOCKED')   92086  00670000
&NL      SETB  (&NL  OR '&L' EQ 'NL' OR '&L' EQ 'NEWLINE')              00680000
&DEBL SETB (&DEBL OR '&L' EQ 'DEBL' OR '&L' EQ 'DEB' OR '&L' EQ 'DEBZ') 00690000
&DEBR    SETB  (&DEBR OR '&L' EQ 'DEBR' OR '&L' EQ 'DEB')               00700000
&DEBZ    SETB  (&DEBZ OR '&L' EQ 'DEBZ')                                00710000
&PADL    SETB  (&PADL OR '&L' EQ 'PADL' OR '&L' EQ 'PAD')               00720000
&PADR    SETB  (&PADR OR '&L' EQ 'PADR' OR '&L' EQ 'PAD')               00730000
&RADJ    SETB  (&RADJ OR '&L' EQ 'RADJ')                                00740000
&UP    SETB  (&UP OR '&L' EQ 'UP')                                      00750000
&UP      SETB  (&UP OR '&L' EQ 'UPPER')                                 00760000
&BLUE    SETB  (&BLUE OR '&L' EQ 'BLUE')                                00770000
&GREEN   SETB  (&GREEN OR '&L' EQ 'GREEN')                              00780000
&PINK    SETB  (&PINK OR '&L' EQ 'PINK')                                00790000
&RED     SETB  (&RED  OR '&L' EQ 'RED')                                 00800000
&TURQ    SETB  (&TURQ  OR '&L' EQ 'TURQ')                               00810000
&WHITE   SETB  (&WHITE OR '&L' EQ 'WHITE')                              00820000
&YELLOW  SETB  (&YELLOW OR '&L' EQ 'YELLOW')                            00830000
&BLUE    SETB  (&BLUE OR '&L' EQ 'BL')                          GP10240 00840000
&GREEN   SETB  (&GREEN OR '&L' EQ 'GR')                         GP10240 00850000
&PINK    SETB  (&PINK OR '&L' EQ 'PI')                          GP10240 00860000
&RED     SETB  (&RED  OR '&L' EQ 'RE')                          GP10240 00870000
&TURQ    SETB  (&TURQ  OR '&L' EQ 'TU' OR '&L' EQ 'CY')         GP10240 00880000
&WHITE   SETB  (&WHITE OR '&L' EQ 'WH')                         GP10240 00890000
&YELLOW  SETB  (&YELLOW OR '&L' EQ 'YE')                        GP10240 00900000
&MONO    SETB  (&MONO OR '&L' EQ 'MONO')                         87313  00910000
&UNDER   SETB  (&UNDER OR '&L' EQ 'UL' OR '&L' EQ 'UNDER')              00920000
&BLINK   SETB  (&BLINK OR '&L' EQ 'BLINK')                              00930000
&REVERSE SETB  (&REVERSE OR '&L' EQ 'REVERSE')                          00940000
&INTENSE SETB  (&INTENSE OR '&L' EQ 'INTENSE' OR '&L' EQ 'INT')         00950000
&DETECT  SETB  (&DETECT OR '&L' EQ 'DETECT' OR '&L' EQ 'LP')            00960000
&NONDISP SETB  (&NONDISP OR '&L' EQ 'NONDISP' OR '&L' EQ 'NDISP')       00970000
&NUMERIC SETB  (&NUMERIC OR '&L' EQ 'NUMERIC' OR '&L' EQ 'NUM')         00980000
&SKIP    SETB  (&SKIP OR '&L' EQ 'SKIP')                                00990000
&PROTECT SETB  (&PROTECT OR '&L' EQ 'PROTECT')                          01000000
&MDT     SETB  (&MDT OR '&L' EQ 'MDT')                           87313  01010000
&DEFAULT SETB  (&DEFAULT OR '&L' EQ 'DEFAULT' OR '&L' EQ 'DFLT')        01020000
&PREV    SETB  (&PREV OR '&L' EQ 'PREVIOUS' OR '&L' EQ 'PREV')          01030000
&INDAD   SETB  (&INDAD OR '&L' EQ '*')                           81270  01040000
         AIF   (&DATA NE 0).NOLIT                                       01050000
         AIF   (K'&L GT 4).OMLEN                                        01060000
&K       SETA  0                                                        01070000
&L       SETC  '&L'.'    '                                              01080000
&L       SETC  '&L'(1,4)                                                01090000
         AIF   ('&L' NE 'X   ').DT                              GP10240 01100000
&L       SETC  'HEX '                                           GP10240 01110000
.DT      AIF   (&K GE 35).OMLEN                                 GP07004 01120000
&K       SETA  &K+1                                                     01130000
&N       SETA  (&K-1)*4+1                                               01140000
&W      SETC  'CHARCON ASISADDRHEX SHEXBIT I   $I  D   $D  F   TIMETIMD*01150000
               DATEDATJWDAYMTH DAY MD  DMY MDY CHEXICM ICN IZ  IA  DCM *01160000
               DCN DZ  DA  EDATDATDCCHHTTR '(&N,4)              GP07004 01170000
         AIF   ('&L' NE '&W').DT                                        01180000
&DATA    SETA  &K                                                       01190000
         AIF   (&J EQ 2).OMSET                                          01200000
         AGO   .NOLIT                                                   01210000
.OMLEN   AIF   (&J NE 2 OR &SKPLEN).NOLIT                               01220000
 AIF (&NOP OR &IN OR &NL OR &DEBL OR &DEBR OR &DEBZ OR &PADL).OMSET     01230000
 AIF (&UP OR &PADR OR &RADJ OR &BLUE OR &GREEN OR &PINK OR &RED).OMSET  01240000
 AIF (&TURQ OR &WHITE OR &YELLOW OR &UNDER OR &BLINK).OMSET             01250000
 AIF (&REVERSE OR &INTENSE OR &DETECT OR &NONDISP OR &INDAD).OMSET      01260000
 AIF (&NUMERIC OR &SKIP OR &PROTECT OR &DEFAULT OR &PREV).OMSET         01270000
         AIF   (&MDT OR &MONO OR &CNLOCK).OMSET                  92086  01280000
         AGO   .NOLIT                                                   01290000
.OMSET   ANOP  ,             EXPLICIT LENGTH OMITTED                    01300000
&SKPLEN  SETB  1             USE L'                                     01310000
         AGO   .NOLIT                                                   01320000
.CHECK   ANOP  ,                                                        01330000
&T       SETA  64*&NOP+48+&INDAD+8*&CNLOCK                       92086  01340000
&E SETA 128*&NL+64*&DEBL+32*&DEBR+16*&DEBZ+8*&PADL+4*&PADR+2*&RADJ+&UP  01350000
&O7      SETA  128*&DEFAULT+64*&PREV+&MDT                        87313  01360000
&PROTECT SETB  (&PROTECT OR &SKIP)                                      01370000
&NUMERIC SETB  (&NUMERIC OR &SKIP)                                      01380000
&DETECT  SETB  (&DETECT  OR &NONDISP)                                   01390000
&INTENSE SETB  (&INTENSE OR &NONDISP)                                   01400000
&O7      SETA  &O7+32*&PROTECT+16*&NUMERIC+8*&INTENSE+4*&DETECT         01410000
&C1      SETB  (&GREEN OR &TURQ OR &WHITE OR &YELLOW)                   01420000
&C2      SETB  (&RED OR &PINK OR &WHITE OR &YELLOW)                     01430000
&C3      SETB  (&BLUE OR &PINK OR &TURQ OR &WHITE)                      01440000
&O9      SETA  64*&C1+32*&C2+16*&C3+8*&MONO+4*&UNDER+2*&REVERSE+&BLINK  01450000
         AIF   ('&STR'(1,1) EQ '''').CSTRING                            01460000
         AIF   ('&EXLEN' NE '' AND NOT &SKPLEN).EXLEN                   01470000
&L       SETC  'L'''                                                    01480000
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,&DATA,&LEN,&L&STR),SL2(&STR)              01490000
         MEXIT                                                          01500000
.EXLEN   AIF   (K'&EXLEN LT 2).NORLEN                            81270  01510000
         AIF   ('&EXLEN'(1,1) NE '(' OR '&EXLEN'(2,1) EQ '(').NORLEN    01520000
&T       SETA  &T+2          ILEN IS REGISTER FORM               81270  01530000
.NORLEN  ANOP  ,                                                 81270  01540000
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,&DATA,&LEN,&EXLEN),SL2(&STR)              01550000
         MEXIT ,                                                        01560000
.CSTRING ANOP  ,                                                        01570000
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,129,&LEN,ZFD&C-*-1),C&STR                 01580000
         MEND  ,                                                        01590000
