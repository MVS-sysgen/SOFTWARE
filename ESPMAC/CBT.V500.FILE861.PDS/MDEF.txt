         MACRO ,                                                        00010000
&NM      MDEF  &LIST,&TYPE=,&PFX=,&SECT=                                00020000
.*                                                                      00030000
.*   MDEF IS THE MACRO USED TO DEFINE ONE ERROR MESSAGE.                00040000
.*     THIS MACRO IS LIMITED TO MESSAGE TABLE MODULES, STARTED WITH     00050000
.*     MDEFHEAD, AND TERMINATED BY MDEFEND. EACH COMBINATION OF         00060000
.*     LANGUAGE AND CHARACTER SET IS EXPECTED TO HAVE A SEPARATE TABLE. 00070000
.*                                                                      00080000
.*   &LB      THE NAME FIELD SPECIFIES THE MESSAGE IDENTIFIER           00090000
.*   &PFX     SPECIFIES A PREFIX TO THE MESSAGE ID, AND IS A GLOBAL     00100000
.*            (ONCE USED IT CAN BE CHANGED, BUT NOT TURNED OFF)         00110000
.*                                                                      00120000
.*   &LIST    ARE THE WORDS OF THE MESSAGE, SEPARATED BY COMMAS.        00130000
.*      ENTRIES MAY BE:                                                 00140000
.*        1) SIMPLE WORD, E.G.         DSNAME                           00150000
.*        2) QUOTED STRING, E.G.       'IT''S'                          00160000
.*        3) HEXADECIMAL TEXT, E.G.    X'070809'                        00170000
.*        4) REPLACEABLE USER PARAMETER:  *1 - *16                      00180000
.*        5) A FUNCTION INVOCATION (DEFERRED)                           00190000
.*                                                                      00200000
.*   &TYPE    TYPE OF DATA BEING DEFINED. TYPE IS LOCAL, BUT OVERRIDES  00210000
.*        ANY TYPE SPECIFIED IN MDEFHEAD.                               00220000
.*                                                                      00230000
.*      SUPPORTED ARE:      (DEFAULT IS C IF NOT SPECIFIED ANYWHERE)    00240000
.*        C  CHARACTER CONSTANT (USE FOR X', ETC. ANYWAY)               00250000
.*        G  DBDC VALUE (NOT SUPPORTED YET)                             00260000
.*                                                                      00270000
.*   REQUIRES INNER MACROS MDEFDICT, AND MACQOTER.                      00280000
.*     RELATED MACROS ARE MDEFHEAD, MDEFEND, AND MDEFGBL.               00290000
.*   MUST BE INVOKED IN AN RSECT. CREATES AND USES RSECTS NAMED         00300000
.*   MSGDSCT AND MSGSECT.                                               00310000
.*                                                                      00320000
.*   WHEN SECT=DSECT IS SPECIFIED, MAPPINGS FOR THREE CONTROL BLOCKS    00330000
.*   ARE EXPANDED: MTPDSECT - MAP MESSAGE POINTER                       00340000
.*                 MTMDSECT - MAP MESSAGE DEFINITION AND SPECIALS       00350000
.*                 MTDDSECT - MAP DICTIONARY DEFINITION                 00360000
.*                                                                      00370000
.*                                      V1 WRITTEN 3/1999 BY GYP        00380000
         COPY  MDEFGBL                                                  00390000
         GBLC  &MACQSTR                                                 00400000
         LCLA  &I,&J,&K,&N,&STRCNT,&STRDC                               00410000
         LCLC  &CH,&CHIX,&STRING,&LQ,&TCH,&FCODE,&LOTYP,&DCLAB          00420000
         LCLC  &QU,&TP,&LB                                      GP03245 00430000
&ZZSECT  SETC  '&SYSECT'                                                00440000
         AIF   ('&SECT' EQ 'DSECT').MAPPER                              00450000
&LB      SETC  '&NM'         SET LABEL TO SUPPLIED VALUE                00460000
         AIF   ('&ZZMPFX' EQ '').NMDOPF                         GP08078 00470000
&N       SETA  K'&ZZMPFX                                        GP08078 00480000
&DCLAB   SETC  '&LB'(1,&N)                                      GP08078 00490000
         AIF   ('&ZZMPFX' NE '&DCLAB').NMDOPF                   GP08078 00500000
&LB      SETC  '&NM'(&N+1,K'&NM)   REMOVE DUPLICATE PREFIX      GP08078 00510000
.NMDOPF  ANOP  ,                                                GP08078 00520000
&N       SETA  N'&SYSLIST                                               00530000
&DCLAB   SETC  '&ZZMPFX'.'&LB'  LABEL FOR FIRST/ONLY DEF STRING         00540000
.*  STRING ACCUMULATES ENTRIES FOR THE MESSAGE'S DICTIONARY LIST        00550000
.*  STRCNT CONTAINS THE NUMBER OF ENTRIES                               00560000
&STRING  SETC  ''                                                       00570000
&STRCNT  SETA  0                                                        00580000
.*  PFX OVERRIDES GLOBAL PREFIX, IF ANY, UNTIL CHANGED                  00590000
         AIF   ('&PFX' EQ '').NONUPFX                                   00600000
&ZZMPFX  SETC  '&PFX'                                                   00610000
.NONUPFX AIF   (&N GT 0).NUMGOOD                                        00620000
.*  NOTE THIS MAY APPEAR WHEN USER HAS UNBALANCED APOSTROPHES           00630000
         MNOTE 8,'MDEF: NO MESSAGE TEXT PROCESSABLE'                    00640000
         MEXIT ,                                                        00650000
.NUMGOOD AIF   ('&LB' NE '').LABGOOD                                    00660000
         MNOTE 8,'MDEF: MESSAGE ID (NAME FIELD) OMITTED'                00670000
         MEXIT ,                                                        00680000
.*  MAINTAIN GLOBAL TYPE. NOTE THAT X' IS TREATED AS C WITH X IN DATA   00690000
.LABGOOD ANOP  ,                                                        00700000
&LOTYP   SETC  'C'                                                      00710000
         AIF   ('&ZZMDTYP' EQ '').HAVEGBL                               00720000
&LOTYP   SETC  '&ZZMDTYP'                                               00730000
.HAVEGBL AIF   ('&TYPE' EQ '').GETWORD                                  00740000
&LOTYP   SETC  '&TYPE'                                                  00750000
.*                                                                      00760000
.*  LOOP THROUGH EACH WORD IN THE LIST                                  00770000
.*                                                                      00780000
.GETWORD AIF   (&K GE &N).PLANT                                         00790000
&K       SETA  &K+1                                                     00800000
&CH      SETC  '&SYSLIST(&K)'                                           00810000
&FCODE   SETC  ''            NO SPECIAL CODE                            00820000
&TCH     SETC  ''            NO SPECIAL TRAILER                         00830000
&I       SETA  K'&CH         SEE HOW LONG IT IS                         00840000
         AIF   (&I LT 1).COMMA  TREAT AS COMMA                          00850000
         AIF   (&I LT 2).WORD   NOTHING SPECIAL?                        00860000
         AIF   (N'&SYSLIST(&K) LE 1).NOSUB                              00870000
         AIF   ('&SYSLIST(&K,1)'(1,1) EQ '*').PARM                      00880000
.NOSUB   AIF   ('&CH'(1,1) EQ '*').PARM                                 00890000
&TCH     SETC  '&CH'(&I,1)                                              00900000
         AIF   ('&TCH' EQ '|').TRSP    ABUTTAL?                         00910000
         AIF   ('&TCH' EQ 'Â¦').TRSP    ABUTTAL?                         00920000
         AIF   ('&TCH' EQ '-').TRCH                                     00930000
         AIF   ('&TCH' EQ '.').TRCH                                     00940000
         AIF   ('&TCH' EQ ':').TRCH                                     00950000
         AIF   ('&TCH' EQ ';').TRCH                                     00960000
         AIF   ('&TCH' EQ '/').TRCH                                     00970000
         AIF   ('&TCH' EQ '!').TRCH                                     00980000
         AIF   ('&TCH' EQ '?').TRCH                                     00990000
         AIF   ('&TCH' EQ '+').TRCH                                     01000000
         AIF   ('&TCH' EQ '*').TRCH                                     01010000
         AGO   .WORD                                                    01020000
.*                                                                      01030000
.*  PROCESS A PARM REFERENCE                                            01040000
.*    GENERAL FORM IS (*N,TYPE,MOD)                                     01050000
.*    N IS THE PARAMETER NUMBER IN CALL                                 01060000
.*    TYPE IS C (DEFAULT), I, P, ETC.                                   01070000
.*    MOD IS A FORMATTING MODIFIER                                      01080000
.*                                                                      01090000
.PARM    ANOP  ,             PRELIMINARY                                01100000
&J       SETA  1                                                        01110000
&QU      SETC  '0'                                                      01120000
&TP      SETC  'C'                                                      01130000
         AIF   (N'&SYSLIST(&K) LT 3).PARM2                              01140000
&QU      SETC  '&SYSLIST(&K,3)' QUALIFIER, IF ANY                       01150000
.PARM2   AIF   (N'&SYSLIST(&K) LT 2).PARM1                              01160000
&TP      SETC  '&SYSLIST(&K,2)' FORMATTING TYPE (C, O, X, P, I)         01170000
.PARM1   AIF   ('&TP' NE 'UI' AND '&TP' NE 'IU').PARMTP                 01180000
&TP      SETC  'U'                                                      01190000
.PARMTP  AIF   ('&TP' EQ 'CTAPDIIHXBBXXX'(&J,1)).PARMTPF                01200000
&J       SETA  &J+1                                                     01210000
         AIF   (&J LE 16).PARMTP                                        01220000
.PARMTPF ANOP  ,                                                        01230000
&J       SETA  &J-1                                                     01240000
&TP      SETC  '&J'                                                     01250000
&CH      SETC  '&SYSLIST(&K,1)'                                         01260000
&TCH     SETC  '&CH'(2,&I-1)  ISOLATE PARM NUMBER                       01270000
&FCODE   SETC  'X''E000''+('.'&TCH'.'-1)*256+16*'.'&QU'.'+'.'&TP'       01280000
         AGO   .CODESTR                                                 01290000
.*                                                                      01300000
.*  NULL PARAMETER - EXPAND A COMMA AND SPACE                           01310000
.*                                                                      01320000
.COMMA   ANOP  ,                                                        01330000
&CH      SETC  ','                                                      01340000
&FCODE   SETC  'X''FD00''+C'','''                                       01350000
         AGO   .CODESTR      ADD JUST A COMMA                           01360000
.*                                                                      01370000
.*  ABUTTAL - SEE WHETHER PRECEDING CHARACTER IS ALSO SPECIAL           01380000
.*                                                                      01390000
.TRSP    ANOP  ,             NO TRAILING SPACE                          01400000
&TCH     SETC  ' '                                                      01410000
&FCODE   SETC  'X''FE00''+C'''.'&TCH'.''''  ABUTTAL - NO SPACE          01420000
&CH      SETC  '&CH'(1,&I-1)  DROP TRAILER                              01430000
&I       SETA  K'&CH                                                    01440000
&TCH     SETC  '&CH'(&I,1)                                              01450000
         AIF   (&I LT 2).WORD   TOO SHORT -  PROCESS WORD PROPER        01460000
         AIF   ('&TCH' EQ '-').NSCH                                     01470000
         AIF   ('&TCH' EQ '.').NSCH                                     01480000
         AIF   ('&TCH' EQ ':').NSCH                                     01490000
         AIF   ('&TCH' EQ ';').NSCH                                     01500000
         AIF   ('&TCH' EQ '/').NSCH                                     01510000
         AIF   ('&TCH' EQ '!').NSCH                                     01520000
         AIF   ('&TCH' EQ '?').NSCH                                     01530000
         AIF   ('&TCH' EQ '+').NSCH                                     01540000
         AIF   ('&TCH' NE '*').WORD                                     01550000
.NSCH    ANOP  ,             SPECIAL CHARACTER + ABUTTAL                01560000
&CH      SETC  '&CH'(1,&I-1)  DROP TRAILER                              01570000
&FCODE   SETC  'X''FD00''+C'''.'&TCH'.''''                              01580000
         AGO   .WORD         PROCESS REST                               01590000
.*                                                                      01600000
.*  SPECIAL CHARACTER - STRIP FROM WORD, AND ADD LEXICAL SUFFIX ENTRY   01610000
.*                                                                      01620000
.TRCH    ANOP  ,                                                        01630000
&CH      SETC  '&CH'(1,&I-1)  DROP TRAILER                              01640000
&FCODE   SETC  'X''FD00''+C'''.'&TCH'.''''                              01650000
.*                                                                      01660000
.*  EXTRACT WORD; SEE WHAT IT IS; ADJUST ACCORDINGLY                    01670000
.*  REGARDLESS OF CURRENT FORM, PUT IT IN QUOTES                        01680000
.WORD    ANOP  ,                                                        01690000
&I       SETA  K'&CH                                                    01700000
         AIF   (&I LT 3).WORDQ                                          01710000
         AIF   ('&CH'(1,2) EQ 'X''').WORDH                              01720000
         AIF   ('&CH'(1,2) EQ 'B''').WORDH                              01730000
         AIF   ('&CH'(1,2) EQ 'C''').WORDC                              01740000
         AIF   ('&CH'(1,2) NE 'Z''').WORDQ                              01750000
.WORDC   ANOP  ,                                                        01760000
&CH      SETC  '&CH'(2,&I-1)                                            01770000
         AGO   .WORDQ                                                   01780000
.WORDH   ANOP  ,                                                        01790000
&MACQSTR SETC  '&CH'                                                    01800000
         AGO   .WORDD                                                   01810000
.WORDQ   MACQOTER &CH,OPT=QUOTE,NAME=MDEF,TYPE=&LOTYP                   01820000
.*  ADD THIS WORD TO DICTIONARY, OR GET DICTIONARY INDEX IN ZZDCTRT     01830000
.WORDD   MDEFDICT &MACQSTR,TYPE=&LOTYP                                  01840000
&CHIX    SETC  '00000'.'&ZZDCTRT'                                       01850000
&J       SETA  K'&CHIX                                                  01860000
&CHIX    SETC  '&CHIX'(&J-4,5)                                          01870000
.*                                                                      01880000
.*  ADD DICTIONARY OFFSET TO STRING, AND UP COUNTER                     01890000
.STRINGS AIF   ('&STRING' NE '').APPEND                                 01900000
&STRING  SETC  'DWD&CHIX-MSGDSCT'                                       01910000
         AGO   .APPCNT                                                  01920000
.APPEND  ANOP  ,                                                        01930000
&STRING  SETC  '&STRING'.','.'DWD&CHIX-MSGDSCT'                         01940000
.APPCNT  ANOP  ,             COUNT ENTRIES IN STRING                    01950000
&STRCNT  SETA  &STRCNT+1                                                01960000
.CODESTR AIF   ('&FCODE' EQ '').TESTDC                                  01970000
         AIF   (&ZZDOFLN EQ 4).SCALE4                                   01980000
         AIF   (&ZZDOFLN NE 3).SCALED                                   01990000
&FCODE   SETC  '('.'&FCODE'.')*256'                                     02000000
         AGO   .SCALED                                                  02010000
.SCALE4  ANOP  ,                                                        02020000
&FCODE   SETC  '('.'&FCODE'.')*65536'                                   02030000
.SCALED  AIF   ('&STRING' NE '').SUFFIX                                 02040000
&STRING  SETC  '&FCODE'                                                 02050000
         AGO   .SUFFCNT                                                 02060000
.SUFFIX  ANOP  ,                                                        02070000
&STRING  SETC  '&STRING'.','.'&FCODE'                                   02080000
.SUFFCNT ANOP  ,                                                        02090000
&STRCNT  SETA  &STRCNT+1                                                02100000
.*  WHEN THE STRING HAS AN ARBITRARY NUMBER OF ENTRIES, EXPAND IT       02110000
.*    TO AVOID OVERFLOWING LEGAL LENGTHS                                02120000
.TESTDC  AIF   ((&STRCNT-&STRDC) LE 6).GETWORD                  GP08082 02130000
MSGSECT  CSECT ,                                                        02140000
&STRDC   SETA  &STRCNT                                                  02150000
&DCLAB   DC    AL&ZZDOFLN.(&STRING)                                     02160000
&DCLAB   SETC  ''                                                       02170000
&STRING  SETC  ''                                                       02180000
         AGO   .GETWORD                                                 02190000
.*  LAST WORD PROCESSED. EXPAND MESSAGE DEFINITION                      02200000
.PLANT   AIF   ('&STRING' EQ '').PLEITE                                 02210000
MSGSECT  CSECT ,                                                        02220000
&DCLAB   DC    AL&ZZDOFLN.(&STRING)                                     02230000
.PLEITE  AIF   (&ZZMSGIX NE 1).PLINT                                    02240000
&LQ      SETC  'L'''                                                    02250000
LENWDPTR EQU   &LQ&ZZMPFX&LB DEFINE FIELD LENGTH FOR DICT. PTR          02260000
.PLINT   ANOP  ,                                                        02270000
.*  EXPAND POINTER TO MESSAGE STRING JUST CREATED                       02280000
&ZZSECT  CSECT ,                                                        02290000
&ZZMSGIX SETA  &ZZMSGIX+1                                               02300000
&CHIX    SETC  '00000'.'&ZZMSGIX'                                       02310000
&J       SETA  K'&CHIX                                                  02320000
&CHIX    SETC  '&CHIX'(&J-4,5)                                          02330000
MPT&CHIX DC    AL&ZZMOFLN.(&ZZMPFX&LB-MSGSECT),CL&ZZIDLEN'&ZZMPFX&LB',A*02340000
               L2(&STRCNT)                                              02350000
         AIF   (&ZZMSGIX NE 1).NOMPTLN                                  02360000
MPTSIZE  EQU   *-MPT&CHIX    DEFINE ENTRY LENGTH                        02370000
.NOMPTLN ANOP  ,                                                        02380000
&ZZSECT  CSECT ,                                                        02390000
&CH      SETC  '&ZZMPFX'.'&LB'  GET MESSAGE ID                          02400000
         AIF   ('&CH' GE '&ZZMGPRE').SEQOK                              02410000
&ZZMGSRT SETC  'IS NOT'      MESSAGES NOT IN SEQUENCE                   02420000
.SEQOK   AIF   ('&CH' GE '&ZZMGLO').SEQLO                               02430000
&ZZMGLO  SETC  '&CH'         REMEMBER LOWEST ID                         02440000
.SEQLO   AIF   ('&CH' LE '&ZZMGHI').SEQHI                               02450000
&ZZMGHI  SETC  '&CH'         REMEMBER HIGHEST ID                        02460000
.SEQHI   ANOP  ,                                                        02470000
&ZZMGPRE SETC  '&CH'         REMEMBER PREVIOUS ID                       02480000
.MEXIT   MEXIT ,                                                        02490000
.MAPPER  ANOP  ,             ENTRY MAPPING - FIXED NAMES                02500000
MTPDSECT DSECT ,             MESSAGE TABLE POINTER ENTRY                02510000
MTPMSGOF DS    AL&ZZMOFLN    OFFSET TO MESSAGE DEFINITION               02520000
MTPMSGID DS    CL&ZZIDLEN    MESSAGE IDENTIFIER                         02530000
MTPWDCNT DS    AL&ZZDOFLN    NUMBER OF ENTRIES FOLLOWING                02540000
         SPACE 1                                                        02550000
MTMDSECT DSECT ,             MESSAGE DEFINITION ENTRY                   02560000
MTMDICOF DS    AL&ZZDOFLN    ONE OR MORE DICTIONARY OFFSETS OR SPECIALS 02570000
MTSPFUN  EQU   X'D0'           FUNCTION INVOCATION                      02580000
MTSPPRM  EQU   X'E0'           CALL PARAMETER INSERTION                 02590000
MTSPMETA EQU   X'F0'           SYNTACTICAL FUNCTIONS                    02600000
         SPACE 1                                                        02610000
MTDDSECT DSECT ,             DICTIONARY ENTRY DEFINITION                02620000
MTDPHYLN DS    AL1           PHYSICAL ENTRY LENGTH                      02630000
MTDLOGLN DS    AL1           LOGICAL ENTRY LENGTH                       02640000
MTDTYPE  DS    C             ENTRY TYPE - C (OR G, LATER)               02650000
MTDTEXT  DS    0C            VARIABLE LENGTH TEXT                       02660000
         MEND  ,                                                        02670000
