         MACRO ,                                       NEW 2003/01/01   00010000
&NM      BANDAID &ARG,&A=                                               00020000
         GBLC  &LOCAL                                           GP09301 00030000
         GBLB  &ZZSTAE,&ZZSPIE                                          00040000
         LCLA  &OV,&I,&J,&K,&L,&N                                       00050000
         LCLB  &SYSDEB,&SPIE                                            00060000
         LCLC  &C,&OPT,&OPT1,&OPT2,&LBL                                 00070000
&N       SETA  N'&SYSLIST    NUMBER OF ARGUMENTS                        00080000
&LBL     SETC  '&NM'                                                    00090000
         AIF   (&N GT 0).OKARG                                          00100000
.NOARG   MNOTE 8,'BANDAID REQUIRES AN ARGUMENT'                         00110000
         MEXIT ,                                                        00120000
.OKARG   AIF   (&L GE &N).EXPAND                                        00130000
&L       SETA  &L+1                                                     00140000
&OPT     SETC  '&SYSLIST(&L)'                                           00150000
         AIF   ('&OPT' EQ '').OKARG                                     00160000
&C       SETC  '&OPT'(1,3).'   '                                        00170000
&C       SETC  '&C'(1,3)                                                00180000
&I       SETA  -2                                                       00190000
&J       SETA  1+8*3                                                    00200000
.ARGLOOP AIF   (&I GE &J).FAIL                                          00210000
&I       SETA  &I+3                                                     00220000
         AIF   ('&C' NE 'INITERCLOSPITRARESPUSPOP'(&I,3)).ARGLOOP       00230000
         AIF   (&I GE 7).PART2                                          00240000
         AIF   (&SYSDEB).DUPE                                           00250000
&SYSDEB  SETB  1                                                        00260000
&OPT1    SETC  '&OPT'                                                   00270000
         AGO   .OKARG                                                   00280000
.*                                                                      00290000
.PART2   AIF   (&SPIE).DUPE                                             00300000
&OV      SETA  ((&I-6)/3)                                               00310000
&OPT2    SETC  '&OPT'                                                   00320000
&SPIE    SETB  1                                                        00330000
         AGO   .OKARG                                                   00340000
.*                                                                      00350000
.EXPAND  AIF   (NOT &SYSDEB AND NOT &SPIE).NOARG                        00360000
         AIF   (NOT &SYSDEB).DONDEB                                     00370000
.*   When OPT is INIT:        FOR TERM INVOKES WITHDRAWAL               00380000
.*   USE THIS MACRO TO INITIALIZE THE @BANDAID (SYSDEBUG) ESTAE/ESPIE   00390000
.*   SERVICE ROUTINE.  REQUIRES //SYSDEBUG OUTPUT DD                    00400000
&LBL     LOAD  EP=@BANDAID   LOAD @BANDAID, ISSUE ESTAE, ESPIE, ETC.    00410000
&LBL     SETC  ' '                                                      00420000
         LR    R15,R0                                                   00430000
         L     R0,=CL4'&OPT1'  PASS OPTION (FOR LATER ESPIE SUPPORT)    00440000
         BASSM R14,R15       INVOKE WITH AMODE31                        00450000
&ZZSTAE  SETB  1                                                        00460000
.*                                                                      00470000
.DONDEB  AIF  (NOT &SPIE).MEND                                          00480000
.*   When OPT is SPIE:  initialize SPIE services                        00490000
.*   When OPT is TRAP:  specify address of recovery routine             00500000
.*   When OPT is RESET: cancel recovery                                 00510000
.*   When OPT is PUSH:  specify new recovery routine; save old one      00520000
.*   When OPT is POP:   cancel current and reinstate old recovery       00530000
.*   When OPT is CLOSE: close SYSDEBUG DCB, remove SPIE                 00540000
.*   REQUIRES //SYSDEBUG OUTPUT DD                                      00550000
&LBL     LOAD  EP=@SPIEDER   LOAD @SPIEDER, ISSUE ESPIE, ETC.           00560000
&LBL     SETC  ' '                                                      00570000
         LR    R15,R0                                                   00580000
         AIF   ('&LOCAL' EQ '').ALIEN  NOT ESP ENVIRONMENT      GP09301 00590000
         ST    R15,@SPIEDER                                             00600000
.ALIEN   LA    R0,&OV        PASS OPTION TO INIT                        00610000
         MACPARM R1,&A,NULL=SKIP                                        00620000
         BASSM R14,R15       INVOKE WITH AMODE                          00630000
&ZZSPIE  SETB  1                                                        00640000
         MEXIT ,                                                        00650000
.DUPE    MNOTE 4,'BANDAID: OPTION &OPT CONFLICTS'                       00660000
         MEXIT ,                                                        00670000
.FAIL    MNOTE 4,'BANDAID: OPTION &OPT NOT RECOGNIZED'                  00680000
.MEND    MEND  ,                                                        00690000
