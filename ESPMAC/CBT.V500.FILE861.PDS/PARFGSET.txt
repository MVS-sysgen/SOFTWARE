         MACRO ,                                                        00010001
&NM      PARFGSET &BXLE,&TEXT=PRS$TEXT,&ERR=?,&DONE=            GP03229 00020002
.*--------------------------------------------------------------------* 00030001
.*  PARFGSET IS USED AFTER A PARSE REQUEST FINDS A SELF-DEFINING      * 00040001
.*    KEYWORD REPRESENTING A FLAG TO BE TURNED ON OR OFF, AS DEFINED  * 00050001
.*    BY A TABLE BUILD WITH PARKEYBX/PARKEYFG MACROS.                 * 00060001
.*                                                                    * 00070001
.*  USES REGISTERS 14-2, AND WORK AREA DB                             * 00080001
.*--------------------------------------------------------------------* 00090001
         GBLB  &ZZPARFG                                                 00100001
         LCLC  &P                                               GP03229 00110002
&P       SETC  'ZZP'.'&SYSNDX'                                  GP03229 00120002
*---------------------------------------------------------------------* 00130001
*  LOOK UP TEXT &TEXT USING BXLE TABLE &BXLE                            00140001
*---------------------------------------------------------------------* 00150001
&NM      MACPARM R1,&BXLE    GET BXLE POINTER                           00160001
         MACPARM R0,&TEXT    VERB TO BE LOCATED                         00170001
         SUBCALL SUBVERB     (NEEDS EXTRN OR SERVLOAD FOR SUBVERB)      00180001
         LTR   R2,R15        DID IT WORK ?                              00190001
         MACPARM &ERR,OP=BZ,OPR=BZR,MODE=ONE  TAKE ERROR BRANCH         00200001
         SPACE 1                                                        00210001
*---------------------------------------------------------------------* 00220001
*  APPLY FLAG OFF, THEN FLAG ON REQUEST                               * 00230001
*---------------------------------------------------------------------* 00240001
         SR    R15,R15       GET THE LENGTH                             00250001
         IC    R15,OFOFLEN(,R2)  LOAD FLAG LENGTH - 1                   00260001
         MVC   DB,=8X'FF'                                               00270001
         EX    R15,EXFGOFF   SET FLAG(S) OFF                            00280001
         LA    R14,OFOFONN(R15,R2)  POINT TO ON FLAG                    00290001
         EX    R15,EXFGON    SET FLAGS ON                               00300001
         MACPARM &DONE,OP=B,OPR=BR,MODE=ONE,NULL=SKIP  KEYWORD PROC     00310002
         AIF   (&ZZPARFG).MEND                                          00320001
         AIF   ('&DONE' NE '').NORND                            GP03229 00330002
         B     &P.X                                                     00340002
.NORND   ANOP  ,                                                GP03229 00350002
EXFGOFF  NC    0(*-*,R1),OFOFOFF(R2)  SET FLAGS OFF                     00360001
EXFGON   OC    0(*-*,R1),0(R14)       SET FLAGS ON                      00370001
&ZZPARFG SETB  1                                                        00380001
         AIF   ('&DONE' NE '').MEND                             GP03229 00390002
&P.X     DS    0H                                                       00400002
.MEND    MEND  ,                                                        00410001
