         MACRO                                                          00010000
&REF     BUILDCDE &LENGTH=,&ADDR=,&EP=,&SYS=MVS,&LIST=(1)       GP06320 00020000
.********************************************************************** 00030000
.*                                                                    * 00040000
.* MACRO NAME = BUILDCDE                                              * 00050000
.*                                                                    * 00060000
.* DESCRIPTIVE NAME = BUILD A MAJOR CDE TO IDENTIFY A STORAGE AREA    * 00070000
.*                                                                    * 00080000
.* FUNCTION = MAKES A STORAGE AREA EASIER TO LOCATE IN A              * 00090000
.*            DUMP BY MAKING IT APPEAR AS A LOAD_MODULE.              * 00100000
.*                                                                    * 00110000
.* STATUS = R200                                                      * 00120000
.*                                                                    * 00130000
.* AUTHOR = GILBERT SAINT-FLOUR <GSF@POBOX.COM>                       * 00140000
.*                                                                    * 00150000
.* ENVIRONMENT = SEE BELOW                                            * 00160000
.*                                                                    * 00170000
.*     AMODE = ANY                                                    * 00180000
.*     SCP   = MVS/XA OR MVS/ESA   |  MVS 3.8J WITH SYS=MVS           * 00190000
.*     KEY   = USER                                                   * 00200000
.*     MODE  = PROBLEM                                                * 00210000
.*     APF   = OFF                                                    * 00220000
.*                                                                    * 00230000
.* OPERATION = BUILDCDE USES THE "LOADER" FORM OF IDENTIFY TO CREATE  * 00240000
.*             A MAJOR CDE AND CORRESPONDING XL, THEN ISSUES A LOAD   * 00250000
.*             SVC TO CREATE AN LLE AND ASSOCIATE THE CDE WITH THE    * 00260000
.*             CURRENT TCB.                                           * 00270000
.*                                                                    * 00280000
.* INVOCATION = SEE BELOW                                             * 00290000
.*                                                                    * 00300000
.*          (BEFORE)     GETMAIN RU,LV=20000                          * 00310000
.*                                                                    * 00320000
.*          (AFTER)      GETMAIN RU,LV=20000                          * 00330000
.*                       BUILDCDE LENGTH=(0),ADDR=(1),EP=DYNAM        * 00340000
.*                                                                    * 00350000
.* NOTES = SEE BELOW                                                  * 00360000
.*                                                                    * 00370000
.*        BUILDCDE MAY BE ISSUED MORE THAN ONCE, PROVIDED THE EP      * 00380000
.*        KEY-WORD HAS A DIFFERENT VALUE EACH TIME.                   * 00390000
.*                                                                    * 00400000
.*        IF STORAGE WAS ALLOCATED IN SUB-POOL ZERO (SP=0 IS          * 00410000
.*        SPECIFIED OR DEFAULTED TO IN THE GETMAIN MACRO),            * 00420000
.*        THE DELETE MACRO MAY BE ISSUED AGAINST THE SPECIFIED EP     * 00430000
.*        TO DELETE THE CDE AND FREE UP THE STORAGE.                  * 00440000
.*                                                                    * 00450000
.*        IF GETMAIN SPECIFIES A NON-ZERO SUBPOOL, THE DELETE MACRO   * 00460000
.*        DELETES THE CDE BUT DOES NOT FREE UP THE STORAGE:  YOU      * 00470000
.*        MUST ISSUE FREEMAIN YOURSELF.                               * 00480000
.*                                                                    * 00490000
.*        IN A MAJOR CDE CREATED BY THE "LOADER" FORM OF IDENTIFY,    * 00500000
.*        THE "RENT" AND "REUS" ATTRIBUTES ARE OFF.  THIS MEANS       * 00510000
.*        THAT YOU MAY NOT ISSUE LOAD AGAINST THE EP NAME (BUILDCDE   * 00520000
.*        HAS ALREADY ISSUED LOAD), BUT YOU MAY ISSUE LINK, ATTACH    * 00530000
.*        OR XCTL IF YOU WANT TO (ONCE ONLY).  YOU MAY ALSO ISSUE     * 00540000
.*        THE STANDARD FORM OF IDENTIFY TO DEFINE SECONDARY ENTRY     * 00550000
.*        POINTS WHICH HAVE THE "RENT" ATTRIBUTE.                     * 00560000
.*                                                                    * 00570000
.*        IF THIS MACRO IS USED BY AN AUTHORIZED PROGRAM, THEN        * 00580000
.*        A S306 ABEND WILL OCCUR (THE CDE CREATED HAS ITS CDSYSLIB   * 00590000
.*        BIT OFF).  LOOK FOR A SECOND VERSION OF THE MACRO THAT MAY  * 00600000
.*        BE USED IN AN AUTHORIZED PROGRAM AT THE END OF THIS JOB.    * 00610000
.*                                                                    * 00620000
.*        THE IDENTIFY MACRO SUPPLIED IN SYS1.MACLIB SUPPORTS THE     * 00630000
.*        "LOADER" FORM, BUT THE FORMAT OF THE PARAMETER LIST IS      * 00640000
.*        NOT DOCUMENTED, AS FAR AS I KNOW.  THE RETURN CODES X'18'   * 00650000
.*        OR X'1C' MAY BE RETURNED IN R15 WHEN THE PARAMETER LIST     * 00660000
.*        IS INVALID.  THESE RETURN CODES ARE DOCUMENTED ALONG WITH   * 00670000
.*        THE IDENTIFY MACRO.                                         * 00680000
.*                                                                    * 00690000
.*        WHEN THE LOAD-LIST IS EMPTY (I.E. NO LOAD HAS BEEN ISSUED   * 00700000
.*        FOR THIS TCB YET), IDENTIFY FAILS WITH A RETURN CODE 12.    * 00710000
.*        THIS CAN BE CIRCUMVENTED BY ISSUING THE FOLLOWING SEQUENCE: * 00720000
.*                                                                    * 00730000
.*                       LOAD    EP=IEFBR14                           * 00740000
.*                       GETMAIN RU,LV=20000                          * 00750000
.*                       BUILDCDE LENGTH=(0),ADDR=(1),EP=DYNAM        * 00760000
.*                       DELETE  EP=IEFBR14                           * 00770000
.*                                                                    * 00780000
.*        I'VE NOTICED THAT ON MVS SYSTEMS PRIOR TO MVS/ESA 4.3, THE  * 00790000
.*        "LOADER" FORM OF IDENTIFY RETURNS THE ADDRESS OF THE CDE IT * 00800000
.*        CREATED IN REG1.  IN MVS/ESA 4.3 THIS IS NO LONGER TRUE.    * 00810000
.*                                                                    * 00820000
.********************************************************************** 00830000
.*     CORRECTIONS AND CHANGES BY GERHARD POSTPISCHIL  2006-11-15     * 00840000
.*        BY DEFAULT, BUILDCDE USES THE GETMAINED AREA TO BUILD THE   * 00850000
.*          IDENTIFY LIST. IF YOU USE THIS AREA BETWEEN THE GETMAIN   * 00860000
.*          AND BUILDCDE (E.G., SAVE AREA, OR INIT TO ZERO) THEN      * 00870000
.*          USE LIST= FOR AN ALTERNATE LIST ADDRESS. FOR EXAMPLE,     * 00880000
.*          TRY LIST=20(R13) - USE ANY RS FORMAT ADDRESS LENGTH 32    * 00890000
.*                                                                    * 00900000
.*        RETROFIT TO FUNCTION CORRECTLY UNDER MVS 3.8J (SYS=MVS)     * 00910000
.*        CHANGE TO ASSEMBLE UNDER ASM/XF                             * 00920000
.********************************************************************** 00930000
         LCLA  &O16                    OFFSETS IN PARM LIST     GP06320 00940000
&O16     SETA  16                      OFFSET TO EXTENT LIST    GP06320 00950000
         AIF   ('&SYS' NE 'MVS').NEWSYS                         GP06320 00960000
&O16     SETA  12                      OFFSET TO EXTENT LIST    GP06320 00970000
.NEWSYS  ANOP  ,                       XA, ESA, .....           GP06320 00980000
&REF     IHBINNRA &ADDR,&LENGTH        SET R0,R1                        00990000
         LR    R15,R1                  PRESERVE ENTRY ADDRESS   GP06320 01000000
         IHBINNRA &LIST                SET LIST ADDRESS INTO R1 GP06320 01010000
         ST    R15,0(,R1)              STORE ENTRY-POINT ADDR   GP06320 01020000
         ST    R15,&O16+12(,R1)        ALSO AS LOAD ADDRESS     GP06320 01030000
         MVC   4(8,R1),=CL8'&EP '      CDNAME                   GP06320 01040000
         AIF   ('&SYS' EQ 'MVS').OLDSYS   SHORTER LIST IN 3.8   GP06320 01050000
         XC    12(4,R1),12(R1)         CLEAR BYTES 12-15                01060000
.OLDSYS  LA    R14,16                  LENGTH OF PSEUDO XL      GP06320 01070000
         LA    R15,1                   NUMBER OF RELOCATION FACTORS     01080000
         STM   R14,R0,&O16.(R1)        FORMAT BYTES 16-23       GP06320 01090000
         MVI   &O16+8(R1),X'80'        END OF LIST              GP06320 01100000
         MNOTE *,'       IDENTIFY MF=(E,(R1))'                          01110000
         SLR   R0,R0                   LOADER-TYPE IDENTIFY             01120000
         SVC   41                      ISSUE IDENTIFY SVC               01130000
         LR    R14,R1                  PASS CDE ADDRESS                 01140000
         MNOTE *,'       LOAD EP=&EP,DCB=0'                             01150000
         LA    R0,=CL8'&EP '           POINT TO EP NAME         GP06320 01160000
         SLR   R1,R1                   NO DCB FOR LOAD                  01170000
         SVC   8                       ISSUE LOAD SVC                   01180000
         LR    R1,R0                   EP ADDRESS IN R1                 01190000
         SLR   R0,R0                                                    01200000
         ICM   R0,B'0111',&O16+9(R1)  LENGTH IN R0              GP06320 01210000
         MEND                                                           01220000
