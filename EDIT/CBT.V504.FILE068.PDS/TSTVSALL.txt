TSTVSALL TITLE 'DYNAMIC ALLOCATION ROUTINE FOR TSTVS'
*---------------------------------------------------------------------*
*                                                                     *
*        Module:        TSTVSALL.                                     *
*        Author:        Rob Prins, ING-Bank Amsterdam                 *
*                                                                     *
*        Function:      Allocate the library used in TSTVS            *
*                       under ddnames INP01 and OUTP01.               *
*                       if flag "$RENDS' is on: ask for an old DSNAME *
*                       and a new DSNAME and rename the specified     *
*                       dataset                                       *
*                                                                     *
*                                                                     *
*        PARAMETER:     1 BYTE ALLOCATION SWITCH:                     *
*                       AND 2 BYTES ROUTING CODE PASSED BY            *
*                       THE MAIN ROUTINE TSTVS.                       *
*                                                                     *
*                                                                     *
*        Returncodes:   0000 - OK                                     *
*                       0004 - Function canceled                      *
*                       0008 - RENAME errors occurred                 *
*                                                                     *
*        Modifications: July 29,1993 By Rpr: Support added for the    *
*                       RENAME command. Both catalogued and not       *
*                       catalogued datasets can be renamed.           *
*                                                                     *
*                       April 06,1995 By Rpr: DYNAMIC=YES added       *
*                       to UCBLOOK macro                        @950406
*                       Oct 27,1997   By Rpr: RANGE=ALL   added       *
*                       to UCBLOOK macro                        @971027
*                       August 16,2002 By Rpr: Include the MVS38J     *
*                       version of UCBLOOK (running throught the UCB  *
*                       lookup table). Use SYSPARM(MVS) to include    *
*                       This MVS function.                      @020814
*                                                                     *
*-------------------------------------- (C)-2002 SKYBIRD SYSTEMS -----*
         SPACE 2
TSTVSALL CSECT
         SAVE  (14,12),,*              SAVE REGISTERS.
         LR    R10,R15                 BASE REGISTER
         USING TSTVSALL,R10            GET ADDRESSABILITY
         LR    R2,R1
         LA    R1,SAVEAREA
         ST    R1,8(,R13)              FORWARD POINTER
         ST    R13,4(,R1)              BACKWARD POINTER
         LR    R13,R1
         L     R11,0(,R2)              OBTAIN PARM FROM MAIN ROUT
         USING PARMAREA,R11            GET ADDRESSABILITY OF THE PARM
         MVI   SW,0                    Reset switches           @930729
         XR    R7,R7
         ICM   R7,3,ROUTCDE            OBTAIN ROUTING CODE MESSAGES
         STH   R7,DSNWAIT-4            STORE IN WTORS
         STH   R7,VOLWAIT-4
         STH   R7,NEWNWAIT-4                                    @930729
         STH   R7,MSG99E-2
         TM    ALLOCSW,$UNALL          UNALLOCATE ONLY ?
         BNZ   ALLOC00                 YES: DO NOT ASK NEW DSNAME
ASKDSN   EQU   *
         NI    SW,255-$NCTLGD          Reset flag               @930729
         XC    ECB,ECB                 CLEAR ECB AREA
         MVC   DALDSN(44),BLANKS       CLEAR REPLY AREA
         WTOR  'EDT100A Enter dataset name or ''CANCEL''',             *
               DALDSN,44,ECB,ROUTCDE=(2,3)
DSNWAIT  WAIT  ECB=ECB                 WAIT FOR EVENT
         CLI   DALDSN,C' '             NOTHING REPLIED ?
         BE    ASKDSN                  ASK AGAIN
         CLC   =C'CANCEL',DALDSN       Function canceled ?      @930729
         BNE   ASKVOL                  No: continue             @930729
         MVC   MSG99+4(60),MSG05       move msg EDT198I         @930729
         WTO   MF=(E,MSG99)            Function canceled        @930729
         LA    R15,4                   Load return code         @930729
         B     RETURN                  and return               @930729
ASKVOL   EQU   *                       ASK VOLUME
         XC    ECB,ECB                 CLEAR EVENT CONTROL BLOCK
         MVC   DAVOLSER,BLANKS         CLEAR REPLY AREA
         WTOR  'EDT101A Enter volume serial or ''U''',DAVOLSER,6,ECB,  X
               ROUTCDE=(2,3)
VOLWAIT  WAIT  ECB=ECB                 WAIT FOR EVENT
         CLI   DAVOLSER,C' '           NOTHING REPLIED ?
         BE    ASKVOL                  ASK AGAIN.
         CLC   DAVOLSER(2),=C'U '      U MEANS LOOK IN CATALOG
         BE    DSNLOC                  BRANCH IF yes            @930729
         OI    SW,$NCTLGD              Mark dataset not catlgd  @930729
         B     FUNCTION                and branch               @930729
DSNLOC   DS    0H                                               @930729
         LOCATE LOC                    LOOK IN CATALOG
         LTR   R15,R15                 CHECK COMPLETION.
         BZ    VOL1                    Branch if alright
         MVC   MSG99+4(60),MSG04       DATASET NOT IN CATALOG
         WTO   MF=(E,MSG99)            INFORM OPER/PGMR
         B     ASKDSN                  ASK FOR NEW ALLOCATION
VOL1     DS    0H                      LOCATE IS OK
         MVC   DAVOLSER,LOCVOL         MOVE VOLUME SERIAL
FUNCTION DS    0H                      Check if func = ALLOCATE @930729
*                                      or RENAME                @930729
         MVC   CURDSN,DALDSN           Save dataset name        @930729
         TM    ALLOCSW,$RENDS          Rename of a dataset ?    @930729
         BNO   ALLOC00                 NO; Allocate.            @930729
         OBTAIN OBTNLIST               Check if DS is on volume @930729
         LTR   R15,R15                 TEST COMPLETION          @930729
         BZ    ASKNEWN                 OK: branch               @930729
         MVC   MSG99+4(60),MSG03       move msg EDT104E         @930729
         WTO   MF=(E,MSG99)            DS COULD NOT BE OBTAINED @930729
         B     ASKDSN                  Retry ...                @930729
ASKNEWN  DS    0H                      Ask for NEWNAME          @930729
         XC    ECB,ECB                 CLEAR ECB AREA           @930729
         MVC   NEWNAME(44),BLANKS      Clear reply area         @930729
         WTOR  'EDT106A Enter NEW dataset name or ''CANCEL''',         X
               NEWNAME,44,ECB,ROUTCDE=(2,3)                     @930729
NEWNWAIT WAIT  ECB=ECB                 WAIT FOR EVENT           @930729
         CLI   NEWNAME,C' '            NOTHING REPLIED ?        @930729
         BE    ASKNEWN                 ASK AGAIN                @930729
         CLC   =C'CANCEL',NEWNAME      Function canceled ?      @930729
         BNE   NEWNC001                No: continue             @930729
         MVC   MSG99+4(60),MSG05       move msg EDT198I         @930729
         WTO   MF=(E,MSG99)            Function canceled        @930729
         LA    R15,4                   Load return code         @930729
         B     RETURN                  and return               @930729
NEWNC001 DS    0H                                               @930729
         TM    SW,$NCTLGD              Dataset catalogued ?     @930729
         BO    NEWNC005                No: bypass LOCATE        @930729
         LA    R3,VCB                  Volume list for RENAME   @930729
         LA    R4,LOC#DEVT             Volume list from LOCATE  @930729
         LH    R5,LOC#VOLS             # volumes                @930729
         MVC   VOLCNT,LOC#VOLS         Copy # volumes           @930729
NEWNC003 DS    0H                                               @930729
         MVC   0(10,R3),0(R4)          Copy DEVT + volume       @930729
         XC    10(2,R3),10(R3)         Clear rename status code @930729
         LA    R3,12(,R3)              next entry output        @930729
         LA    R4,12(,R4)              next entry input         @930729
         BCT   R5,NEWNC003             loop until all done      @930729
         MVC   DALDSN,NEWNAME          move to CAMLST-NAME      @930729
         LOCATE LOC                    LOOK IN CATALOG          @930729
         LTR   R15,R15                 DS found in catalog ?    @930729
         BNZ   NEWNC005                No: Ok                   @930729
         MVC   MSG99+4(60),MSG06       move msg EDT107E         @930729
         WTO   MF=(E,MSG99)            Newname already catlgd   @930729
         B     ASKNEWN                 and retry ...            @930729
NEWNC005 DS    0H                                               @930729
         MVC   DALDSN,NEWNAME          move to CAMLST-SEARCH    @930729
         OBTAIN OBTNLIST               Check if DS is on volume @930729
         LTR   R15,R15                 TEST COMPLETION          @930729
         BNZ   NEWNC007                OK: already on volume    @930729
         MVC   MSG99+4(60),MSG07       move msg EDT108E         @930729
         WTO   MF=(E,MSG99)            Newname already on volume@930729
         B     ASKNEWN                 and retry ...            @941019
NEWNC007 DS    0H                                               @930729
         BAL   R14,RENAME              Rename the dataset       @930729
         B     RETURN
ALLOC00  EQU   *                       ALLOCATE
         TM    ALLOCSW,ALLOC           ALREADY ALLOCATED PRIOR ?
         BZ    ALLOC01                 GO ON IF NOT
         BAL   R14,UNALLOC             UNALLOCATE FIRST
         LTR   R15,R15                 UNALLOC OK ?
         BNZ   ASKDSN                  TRY AGAIN IF NOT
ALLOC01  EQU   *                       ALLOCATE THE DATASET
         TM    ALLOCSW,$UNALL          UNALLOCATE ONLY ?
         BNZ   ALLOC99                 YES: BYPASS IT
         BAL   R14,ALLOCPRC            ALLOCATE
         LTR   R15,R15                 TEST COMPLETION.
         BNZ   ASKDSN                  TRY AGAIN IF NOT
         MVC   MSG99+4(60),MSG01       GIVE SUCCESSFUL ALLOC MSG
         WTO   MF=(E,MSG99)            INFORM OPER/PROGRAMMER
ALLOC99  DS    0H                                               @930729
         XR    R15,R15                 RC = 0000                @930729
RETURN   L     R13,4(,R13)
         RETURN (14,12),RC=(15)        GO TO MAIN MODULE
         TITLE 'DYNAMIC ALLOCATION ROUTINE'
*--------------------------------------------------------------------*
*                                                                    *
*        THIS ROUTINE DYNAMICALLY ALLOCATES DATASETS THROUGH         *
*        THE DYNALLOC-MACRO <SVC 99>. TO DO THIS IT NEEDS THE DSNAME *
*        AND THE VOLUME-ID. THE DATASET WILL BE ALLOCATED WITH THE   *
*        DDNAME AS SPECIFIED IN 'DALDDN'. A RETURNCODE WILL BE       *
*        AVAILABLE IN REG. 15 AND IN AN ERROR-SITUATION A SUITABLE   *
*        MESSAGE WILL BE PRODUCED (REFER TO MVS JOB-MANAGEMENT).     *
*                                                                    *
*------------------------------------- (C)-1995 Skybird Systems -----*
         SPACE 2
ALLOCPRC DS    0H
         LA    R2,0
         ST    R14,ALLOCSVE            SAVE RETURNREGISTER
ALLOC001 EQU   *
         MVC   DALDDN(8),=C'INP01   '
         LA    R1,DALPTR               REQUEST BLOCK
         SVC   99                      ALLOCATE <SVC99>
         LTR   R15,R15                 TEST RETURNCODE
         BZ    ALLOC999                OK IF ZERO
ALLOC003 EQU   *
         CVD   R15,DWB
         UNPK  R15BC(4),DWB+4(4)       RETURNCODE
         OI    R15BC+3,X'F0'
         SYCONVHX IN=DAERROR,OUT=ERRORBC,L=2
         SYCONVHX IN=DAINFO,OUT=INFOBC,L=2
         MVC   ALLVERB,=C'ALLOCATION'  PREPARE MSG
         MVC   MSG99+4(60),MSG02       ALLOCATION ERR MSG
         WTO   MF=(E,MSG99)            INFORM OPER/PROGRAMMER
         LA    R2,12                   RETURNCODE
         B     ALLOCERR
ALLOC999 EQU   *
         MVC   DALDDN(8),=C'OUTP01  '  FILL IN DDNAME
         LA    R1,DALPTR               REQUEST BLOCK
         SVC   99                      ALLOCATE
         OBTAIN OBTNLIST
         LTR   R15,R15                 TEST COMPLETION
         BZ    ALLOCOK
         MVC   MSG99+4(60),MSG03
         WTO   MF=(E,MSG99)            DS COULD NOT BE OBTAINED
         LA    R2,12                   TRY AGAIN
ALLOCOK  EQU   *
         OI    ALLOCSW,ALLOC           MASK ALLOCATED
ALLOCERR L     R14,ALLOCSVE            LOAD RETURN ADDRESS
         LR    R15,R2                  RETURNCODE
         BR    R14                     RETURN
         TITLE 'DYNAMIC UNALLOCATION ROUTINE'
*--------------------------------------------------------------------*
*                                                                    *
*        THIS ROUTINE DYNAMICALLY UNALLOCATES A DATASET WITH THE     *
*        DDNAME AS SPECIFIED IN 'DALDDNAM'. THIS FUNCTION WILL ALSO  *
*        BE PERFORMED THROUGH THE DYNALLOC-MACRO AND A RETURNCODE    *
*        WILL BE AVAILABLE IN REG. 15.                               *
*                                                                    *
*------------------------------------- (C)-1995 Skybird Systems -----*
         SPACE 2
UNALLOC  EQU   *
         ST    R14,UNALLSVE            SAVE RETURNADDRESS
         LA    R2,0                    INIT RETCD
UNALL001 EQU   *
         MVC   DALDDN(8),=C'INP01   '
         LA    R1,DUNPTR               REQUEST BLOCK
         SVC   99                      UNALLOCATE
         LTR   R15,R15                 OK ?
         BZ    UNALL999                BRANCH IF SO
UNALL003 EQU   *
         CVD   R15,DWB
         UNPK  R15BC(4),DWB+4(4)       RETURNCODE
         OI    R15BC+3,X'F0'
         SYCONVHX IN=DUNERROR,OUT=ERRORBC,L=2
         SYCONVHX IN=DUNINFO,OUT=INFOBC,L=2
         MVC   ALLVERB(10),=C'UNALLOC.  '
         MVC   MSG99+4(60),MSG02
         WTO   MF=(E,MSG02)            GIVE ERROR MSG
         LA    R2,4                   RETURNCODE
         B     UNALLERR
UNALL999 EQU   *
         MVC   DALDDN(8),=C'OUTP01  '  FILL IN DDNAME
         LA    R1,DUNPTR               LOAD REQUEST BLOCK
         SVC   99                      ISSUE DYNALLOC SVC
         NI    ALLOCSW,X'FF'-ALLOC     MASK UNALLOCATED
UNALLERR L     R14,UNALLSVE            RESTORE R14
         LR    R15,R2                  RETURNCODE
         BR    R14                     RETURN
         TITLE 'Rename dataset Routine'
*---------------------------------------------------------------------*
*                                                                     *
*        Routine:    RENAME                                           *
*        Function:   Rename datasets:                                 *
*                    If the dataset is catalogued, the dataset will   *
*                    be renamed, uncatalogued and the newname will    *
*                    be catalogued (msg EDT101A replied with 'U')     *
*                    If the dataset is not catalogued (msg EDT101A    *
*                    has been replied with a volume serial number),   *
*                    only the dataset in the VTOC will be renamed.    *
*                                                                     *
*------------------------------------- (C)-1995 Skybird Systems ------*
         SPACE
RENAME   DS    0H
         USING TSTVSUCB,R4             GET ADDRESSABILITY OF UCB
         ST    R14,SAVE14R             Save reg.14              @930729
         TM    SW,$NCTLGD              Dataset catalogued ?     @930729
         BNO   REN002                  Yes: rename directly     @930729
         AIF   ('&SYSPARM' EQ 'MVS').SKIP1                      @020814
*---------------------------------------------------------------------*
*        Do the lookup of the UCB in the OS/390 way
*---------------------------------------------------------------------*
         MODESET KEY=ZERO              Goto system key          @930729
         UCBLOOK VOLSER=DAVOLSER,DEVCLASS=DASD,UCBPTR=ADDRUCB,         X
               NOPIN,DYNAMIC=YES,RANGE=ALL                      @971027
         LTR   R15,R15                 UCBLOOK ok?              @930729
         BZ    REN001                  YES: branch              @930729
         MODESET KEY=NZERO             Goback to user key       @930729
.SKIP1   ANOP
         AIF   ('&SYSPARM' NE 'MVS').SKIP2                      @020814
*---------------------------------------------------------------------*
*        Do the lookup of the UCB in the MVS way
*---------------------------------------------------------------------*
         L     R3,CVTPTR               GET CVTADRESS            @020814
         USING CVT,R3                  GET ADDRESSABILITY OF CVT@020814
         L     R2,CVTILK2              POINT TO START of lookup @020814
LOOKUP   EQU   *                       FIND UCB                 @020814
         CLC   0(2,R2),=X'FFFF'        END OF LOOKUP TABLE ?    @020814
         BE    LOOKERR                 YES: END OF UCBS REACHED @020814
         CLC   0(2,R2),=X'0000'        NULL ENTRY?              @020814
         BE    LOOKNXT                 GO TO NEXT ONE           @020814
         XR    R4,R4                   CLEAR REGISTER           @020814
         ICM   R4,3,0(R2)              # HALFWORDS IN R4        @020814
*        SLL   R4,1                 *2 BECAUSE LOOKUP=HALFWORDS @020814
         CLC   SRTEVOLI,DAVOLSER     IS THIS THE VOLSER WE NEED?@020814
         BE    LOOKFND                 BRANCH IF YES.           @020814
LOOKNXT  DS    0H                                               @020814
         LA    R2,2(,R2)              Next entry in lookup table@020814
         B     LOOKUP                  Search for next UCB      @020814
LOOKFND  DS    0H                                               @020814
         ST    R4,ADDRUCB              Save UCB address         @020814
         B     REN001                                           @031006
LOOKERR  DS    0H                                               @020814
         LA    R15,4                   RC=0004                  @020814
         DROP  R3                      kill addr of CVT         @020814
.SKIP2   ANOP
         CVD   R15,DWB                 Set                      @930729
         UNPK  MSG11+26(4),DWB            returncode            @930729
         OI    MSG11+29,X'F0'                in msg             @930729
         MVC   MSG11+35(6),DAVOLSER    Set VOLSER in msg        @930729
         MVC   MSG99+4(60),MSG11       move msg EDT111E         @930729
         WTO   MF=(E,MSG99)            UCBLOOK failed           @930729
         B     REN970                  and Branch               @930729
REN001   DS    0H                                               @930729
         AIF   ('&SYSPARM' EQ 'MVS').SKIP3                      @020814
         MODESET KEY=NZERO             Goback to user key       @930729
.SKIP3   ANOP
         MVC   VOLCNT,=H'1'            Volume count is 1        @930729
         MVC   VOLSER,DAVOLSER         Move volume name         @930729
         L     R4,ADDRUCB              Pickup UCB pointer       @930729
         MVC   VOLDEVT,UCBTYP          Move device type         @930729
         DROP  R4                      Kill local addr.         @930729
REN002   DS    0H                                               @930729
         SR    R0,R0                   Clear reg.0              @930729
         RENAME RENAMELS               Rename the dataset       @930729
         LTR   R15,R15                 Rename OK ?              @930729
         BZ    REN003                  Yes: continue            @930729
         CVD   R15,DWB                 Set                      @930729
         UNPK  MSG08+26(4),DWB            Returncode            @930729
         OI    MSG08+29,X'F0'                in message         @930729
         SYCONVHX IN=VOLRNCDE+1,OUT=MSG08+41,L=1 Status code    @930729
         MVC   MSG99+4(60),MSG08       move msg EDT109E         @930729
         WTO   MF=(E,MSG99)            Rename failed            @930729
         B     REN970                  and Branch               @930729
REN003   DS    0H                                               @930729
         TM    SW,$NCTLGD              Data set not catalogued  @930729
         BO    REN900                  YES: finished give msg   @930729
         CATALOG UNCATLG               Uncatalog the dataset    @930729
         LTR   R15,R15                 OK ???                   @930729
         BZ    REN005                  Yeah: branch             @930729
         MVC   MSG09+8(2),=C'UN'       UNcatalog failed         @930729
         CVD   R15,DWB                 Set                      @930729
         UNPK  MSG09+29(4),DWB            Returncode            @930729
         OI    MSG09+32,X'F0'                in message         @930729
         MVC   MSG99+4(60),MSG09       move msg EDT110E         @930729
         WTO   MF=(E,MSG99)            UNCATLG failed           @930729
REN005   DS    0H                                               @930729
         LA    R4,VCB                  Point to volume list     @930729
         LH    R5,VOLCNT               # volumes                @930729
REN007   DS    0H                                               @930729
         XC    10(2,R4),10(R4)         Clear status/dsseqnr     @930729
         LA    R4,12(,R4)              Next entry               @930729
         BCT   R5,REN007               Loop until all done      @930729
         CATALOG CATLG                 Catalog the new dataset  @930729
         LTR   R15,R15                 OK ???                   @930729
         BZ    REN900                  Yeah: branch             @930729
         MVC   MSG09+8(2),=C'  '       CAtalog failed           @930729
         CVD   R15,DWB                 Set                      @930729
         UNPK  MSG09+29(4),DWB            Returncode            @930729
         OI    MSG09+32,X'F0'                in message         @930729
         MVC   MSG99+4(60),MSG09       move msg EDT110E         @930729
         WTO   MF=(E,MSG99)            UNCATLG failed           @930729
         B     REN970                  Give RC=0012             @930729
REN900   DS    0H                                               @930729
         MVC   MSG99+4(60),MSG10       move msg EDT199I         @930729
         WTO   MF=(E,MSG99)            RENAME successful        @930729
         XR    R15,R15                 RC=0000                  @930729
         B     REN999                  return                   @930729
REN970   DS    0H                      errors occurred          @930729
         LA    R15,8                   Load RC,                 @930729
REN999   DS    0H                                               @930729
         L     R14,SAVE14R             Restore reg.14           @930729
         BR    R14                     Return to caller         @930729
         TITLE 'Variables and constants'
DWB      DS    D                       For CVD/CVB instructions @930729
ADDRUCB  DS    F                       A(UCB) if ds not catlgd  @930729
ECB      DS    F
SAVEAREA DC    18F'0'
MSG01    DC    CL60'EDT103I Allocation of dataset successful'
MSG02    DS    0CL60
         DC    CL8'EDT102E '           MSGID 8
ALLVERB  DS    CL10
         DC    CL11' Errorcode='       11
ERRORBC  DS    CL4
         DC    CL10',INFOCODE='        10
INFOBC   DS    CL4
         DC    CL5',R15='              5
R15BC    DS    CL4
         DC    CL4' '                  4
MSG03    DC    CL60'EDT104E Dataset not on volume'
MSG04    DC    CL60'EDT105E Dataset not in catalog'
MSG05    DC    CL60'EDT198I Function canceled by operator'      @930729
MSG06    DC    CL60'EDT107E Newname already catalogued'         @930729
MSG07    DC    CL60'EDT108E Newname already on volume'          @930729
MSG08    DC    CL60'EDT109E RENAME failed, RC=xxxx, Statuscd=xx' @93729
MSG09    DC    CL60'EDT110E xxCATALOG failed, RC=xxxx'          @930729
MSG10    DC    CL60'EDT199I RENAME of dataset successful'       @930729
MSG11    DC    CL60'EDT111E UCBLOOK failed RC=xxxx,VOL=vvvvvv'  @930729
BLANKS   DC    CL60' '
MSG99    WTO   '1234512345123451234512345123451234512345123451234512345X
               12345',ROUTCDE=(2,3),MF=L
MSG99E   EQU   *
*
LOC      CAMLST NAME,DALDSN,,LOCAREA   FOR LOCATE
OBTNLIST CAMLST SEARCH,DALDSN,DAVOLSER,WORKAREA
RENAMELS CAMLST RENAME,CURDSN,NEWNAME,VOLLIST                   @930729
CATLG    CAMLST CATBX,NEWNAME,,VOLLIST                          @930729
UNCATLG  CAMLST UCATDX,CURDSN                                   @930729
*
LOCAREA  DS    0D
LOC#VOLS DS    CL2                     # volume on which ds resides
LOC#DEVT DS    CL4                     devtype                  @930729
LOCVOL   DS    CL6                     FOUND VOLUME
LOCDSEQ  DS    CL2                     Dataset seq. number      @930729
         DS    CL251                   REST OF LOCAREA
*
         DS    0D
WORKAREA DS    CL140
CURDSN   DS    CL44                    Old dataset if RENAME    @930729
NEWNAME  DS    CL44                    New dataset if RENAME    @930729
*
         DS    0H                      volume list on hw-bound. @930729
VOLLIST  DS    0CL256                  Volume list for RENAME   @930729
VOLCNT   DS    CL2                     # volumes                @930729
VCB      DS    0CL12                   1 volume entry           @930729
VOLDEVT  DS    CL4                     Device-type              @930729
VOLSER   DS    CL6                     Volume serial number     @930729
VOLRNCDE DS    CL2                     Rename code              @930729
         DS    CL242                   Rest of volume list      @930729
*                                                               @930729
SW       DC    X'00'                                            @930729
$NCTLGD  EQU   128                                              @930729
         LTORG ,
         TITLE 'ALLOCATION CONTROL-BOCKS'
*--------------------------------------------------------------------*
*                                                                    *
*        THESE ARE THE CONTROL-BLOCKS USED BY THE DYNAMIC ALLOCATION *
*        ROUTINE and the DASDSM Rename routine.                      *
*                                                                    *
*------------------------------------- (C)-1995 Skybird Systems -----*
         SPACE 2
         DS    0F
DALPTR   DC    X'80',AL3(DALBLK)
*
DALBLK   DC    AL1(20)                 LENGTH OF REQ BLK
DALVERB  DC    AL1(1)                  DSNAME ALLOCATION
DALFLG1  DC    X'2000'                 FLAGS1
DAERROR  DC    AL2(0)                  ERROR CODE
DAINFO   DC    AL2(0)                  INFOCODE
DALTU    DC    A(DALTXT)               TEXT UNIT LIST
         DC    A(0)                    RESERVED
DALFLG2  DC    A(0)                    FLAGS2
*
DALTXT   DS    0F
         DC    A(DALDDNAM)             DDNAME TU
         DC    A(DALDSNAM)             DSNAME TU
         DC    A(DALSTATS)             DISPOSITION TU
         DC    A(DALVLSER)             VOLUME SERIAL TU
         DC    X'80',AL3(DALUNIT)      UNIT NAME CONTAINS SYSDA
*
DALDSNAM DS    0H                      DSNAME TU
         DC    X'0002'
         DC    AL2(1)
DALDSNL  DC    AL2(44)                 LENGTH
DALDSN   DS    CL44
*
DALSTATS DS    0H                      DATASET DISP.
         DC    X'0004'
         DC    AL2(1)
         DC    AL2(1)                  LENGTH
         DC    X'08'                   DISP=SHR
*
DALVLSER DS    0H                      VOLUME SERIAL TU
         DC    X'0010'
         DC    AL2(1)
         DC    AL2(6)                  VOLUME LENGTH
DAVOLSER DS    CL6
*
DALUNIT  DS    0H                      UNIT TU
         DC    X'0015'
         DC    AL2(1)
DALUNITL DC    AL2(8)                  LENGTH
DALUNITD DC    C'SYSALLDA'
*
DALDDNAM DS    0H                      DDNAME TU
         DC    X'0001'
         DC    AL2(1)
DALDDNL  DC    AL2(8)
DALDDN   DS    0CL8                    DDNAME
         DC    CL8'INP01'
         TITLE 'UNALLOCATION CONTROL-BLOCKS'
*--------------------------------------------------------------------*
*                                                                    *
*        THESE ARE THE CONTROL-BLOCKS USED BY THE DYNAMIC UNALLO-    *
*        CATION ROUTINE.                                             *
*                                                                    *
*------------------------------------- (C)-1995 Skybird Systems -----*
         SPACE 2
         DS    0F
DUNPTR   DC    X'80',AL3(DUNBLK)       REQUEST BLOCK POINTER
*
DUNBLK   DC    AL1(20)                 LENGTH OF REQ.BLK
DUNVERB  DC    AL1(2)                  UNALLOCATION VERB
DUNFLG1  DC    X'2000'                 FLAGS1
DUNERROR DC    AL2(0)                  ERRORCODE
DUNINFO  DC    AL2(0)                  INFOCODE
         DC    A(DUNTXT)               TEXT UNIT LIST
         DC    A(0)                    RESERVED
DUNFLG2  DC    A(0)                    FLAGS 2
*
DUNTXT   DS    0F
         DC    X'80',AL3(DALDDNAM)
*
SAVE14R  DS    F                       SAVE R14 In Rename rout. @930729
ALLOCSVE DS    F                       SAVE R14
UNALLSVE DS    F
*
*        GENERAL EQUATES.
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10                      First base register
R11      EQU   11                      Address of Parameter from TSTVS
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         TITLE 'Dummy sections'
         PRINT NOGEN
PARMAREA DSECT                         FROM MAIN MODULE
ALLOCSW  DS   C                        ALLOCSW
ALLOC    EQU  X'80'
$UNALL   EQU  X'08'                    DON ONLY AN UNALLOCATE
$RENDS   EQU  X'02'                    Perform a rename         @930729
ROUTCDE  DS   CL2                      ROUTING CODE MESSAGES
*
TSTVSUCB DSECT ,                                                @930729
         IEFUCBOB LIST=NO
         AIF   ('&SYSPARM' NE 'MVS').SKIP4                      @020814
         CVT   DSECT=YES,LIST=NO
.SKIP4   ANOP
         END
