         TITLE 'DALCDS - ISPF Display Allocated Dataset Information   '
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*  ==================================================================
*
*  DDDDD     AAA   LL       CCCCC  DDDDD    SSSSS
*  DD   D  AA   AA LL      CC   CC DD   D  SS    S
*  DD   DD AA   AA LL      CC      DD   DD SS
*  DD   DD AAAAAAA LL      CC      DD   DD  SSSSS
*  DD   DD AA   AA LL      CC      DD   DD      SS
*  DD   D  AA   AA LL      CC   CC DD   D  S    SS
*  DDDDD   AA   AA LLLLLLL  CCCCC  DDDDD    SSSSS
*
*  ==================================================================
*
*
*  Program: DALCDS
*
*  Author:  Larry Belmontes Jr.
*           https://ShareABitofIT.net/DALCDS-in-MVS38J
*           Copyright (C) 2020  Larry Belmontes, Jr.
*
*  Disclaimer:
*  ===================================================================
*
*     No guarantee; No warranty; Install / Use at your own risk.
*
*     This software is provided "AS IS" and without any expressed
*  or implied warranties, including, without limitation, the
*  implied warranties of merchantability and fitness for a
*  particular purpose.
*
*     The author requests keeping authors name intact in any
*  modified versions.
*
*     In addition, the author requests readers to submit any
*  code modifications / enhancements and associated comments
*  for consideration into a subsequent release (giving credit
*  to contributor(s)) thus, improving overall functionality
*  and further benefiting the MVS 3.8J hobbyist public domain
*  community.
*
*
         EJECT
*  Overview:
*  ================================================================
*
*     DALCDS displays allocated dataset information for the current
*  TSO session under the MVS 3.8J environment.
*
*     DALCDS displays date and time on line 1 and User ID, Step name,
*  and Logon Proc name on line 4.  TSO allocations display on line 6.
*
*     DALCDS display snippet follows:
*
*   ---------------------------------------------------------
*   | dd/mm/yy.jjj hh:mm      -----  TSO Allocations   --...|   Line 1
*   | COMMAND ===>                                       ...|        2
*   |                                                    ...|        3
*   | UserID- LARRY01  Step- IKJACCNT Proc- ISPLGN00     ...|        4
*   |                                                    ...|        5
*   | DD# DDNAME   ORG DISP VOLSER DSNAME                ...|        6
*   | 001 STEPLIB  PO  Shr  PUB005 ISP.V2R1M0.LLIB       ...|        7
*   |                  Shr  PUB005 LARRY01.REVIEW.MYLOAD ...|        8
*   | 002 SYS00004 VS  Shr  PUB000 SYS1.UCAT.TSO         ...|        9
*   | 003 SYS00002 VS  Shr  MVSCAT SYS1.UCAT.MVS         ...|        .
*   | 004 ISPPROF  PO  Shr  PUB000 LARRY01.ISP.PROF      ...|        .
*   | 005 SYSOUT   PS  New         Terminal              ...|        .
*   ---------------------------------------------------------
*
*     The method used to acquire TSO allocations is via the TIOT
*  and JFCB.  In essence, for each DD, the JFCB is interrogated for
*  DSN and other file information.
*
*     Date and time values are sourced from ISPF Z variables or
*  subroutine GETDTE, if found in a link library.  If GETDTE is not
*  installed on your system or elect not to do so, no problem!
*  See Prerequisite section below for more information.
*
*     DALCDS uses ISPF generic message ISRZ002 for posting errors.
*  Message set ISRZ00 must be installed to display message content.
*  Otherwise, a 'not found' message is posted.  See Prerequisite
*  section below for more information.
*
*     Some common macros are used in this program and other
*  ShareABitofIT software before assembly-link-edit which requires
*  installation of SHRABIT.MACLIB.  See Prerequisite section below
*  for more information.
*
         EJECT
*  Overview:  (continued)
*  ================================================================
*
*     DALCDS, can be invoked directly from an ISPF panel or menu
*  using 'CMD(DALCDS) NEWAPPL(DALC)' as follows:
*
*     For example -
*
*       Assume an existing ISPF panel has the following )PROC section,
*       the 'NEW ENTRY' line can be added to invoke DALCDS when
*       option UA is entered on the panel (menu) command line.
*
*         )PROC
*           &ZSEL = TRANS( TRUNC (&ZCMD,'.')
*                         1,'CMD(xxxxx) NEWAPPL(ISR)'
*                         6,'PGM(xxxxxx)'
*                        UA,'CMD(DALCDS) NEWAPPL(DALC)'  <-- NEW ENTRY
*                       ' ',' '
*                         *,'?' )
*         )END
*
*     Alternatively, a command entry can be defined in the ISPF
*  command table (ISPCMDS) as follows:
*
*     For example -
*
*               VERB      T  ACTION
*                               DESCRIPTION
*
*          ____ DALCDS    6  SELECT CMD(DALCDS) NEWAPPL(DALC)
*                               DISPLAY TSO ALLOCATIONS
*
*     Then, you can access DALDCS from any screen by typing DALDCS
*  on the command line.
*
*     DALCDS is based on Wally Mclaughlin's ISPF product Ver 2.1 and
*  written in IFOX00 under MVS38J TK3 / Hercules.
*
*     Other ISPF resource required include:
*            - PDALCDS0  TSO Allocation Display Panel
*            - HDALCDS0  TSO Allocation Help    Panel
*            - ISRZ00    Messages, use the generic ISRZ002 message
*              This ISPF message set can be downloaded from
*              https://www.shareabitofit.net/ISRZ00-in-mvs38j/
*
*
         EJECT
*  Prerequisite: User Modifications and Others
*  ===================================================================
*
*     SHRABIT.MACLIB is NECESSARY to assemble this software.  This PDS
*  contains macros and copybooks.  Download from this website:
*       https://ShareABitofIT.net/SHRABIT-MACLIB-in-MVS38J
*
*     **** NOTE: SHRABIT.MACLIB is part of the installation process.
*                You can visit the SHRABIT.MACLIB page and download
*                at a later time!
*
*     ISPF generic message ISRZ002, part of message member ISRZ00, is
*  used for error message processing and posting by this software.
*  If ISRZ00 is NOT INSTALLED on your system, error posting results in
*  a message not found condition with no error context posted.
*  ISRZ00 can be downloaded from this website:
*       https://ShareABitofIT.net/ISRZ00-in-MVS38J
*
*     **** NOTE: ISRZ00 is part of the installation process.  You can
*                visit the ISRZ00 page and download at a later time!
*
*     This software displays date and time on line 1 using ISPF
*  Z-variables ZDATE, ZJDATE and ZTIME or subroutine GETDTE (if
*  installed on your system).
*
*     GETDTE, is OPTIONAL, and can be downloaded from website:
*       https://ShareABitofIT.net/GETDTE-in-MVS38J
*
*     If GETDTE is installed, two user-mods, ZP60014 and ZP60038 are
*  REQUIRED to process CLIST symbolic variables via the IKJCT441 API.
*  Otherwise, GETDTE will function normally, but will NOT create
*  CLIST variable, RDTEANSR.
*
*     More information on the above user-mods can be obtained from
*  the following website:
*       http://www.prycroft6.com.au/vs2mods/
*
*     Check your system to determine if one or both user-mods are
*  required.  ZP60038 requires ZP60014.
*
*
*
         EJECT
*  DALCDS Programs / Services:
*  ==================================================================
*
*    1) GETMAIN/FREEMAIN          Working storage
*    2) ISPLINK                   ISPLINK module for ISPF Services
*    3) SHRABIT.MACLIB            Macros for ShareABitofIT Programs
*    4) GETDTE                    Get Date-Time-Environment Utility
*
*
         EJECT
*  Table 1 - Program Register Usage
*
*  +------+----------------------------------------------------------+
*  | REG  |                                                          |
*  |USAGE |  DESCRIPTION                                             |
*  +------+----------------------------------------------------------+
*  |  R13 |  Calling program registers (R14-R12) upon entry          |
*  |  R14 |  Calling program return address upon entry               |
*  |  R15 |  Called  program entry address  upon entry               |
*  |  R1  |  Called  program parms starting address                  |
*  +------+----------------------------------------------------------+
*  |  R0  |  Working Register                                        |
*  |  R1  |  Working Register                                        |
*  |  R2  |  Working Register                                        |
*  |  R3  |  Working Register                                        |
*  |  R4  |  Working Register                                        |
*  |  R5  |  Working Register                                        |
*  |  R6  |  Working Register                                        |
*  |  R7  |  Working Register                                        |
*  |  R8  |  Working Register                                        |
*  |  R9  |  Address of WORKAREA via GETMAIN/FREEMAIN                |
*  |  R10 |  Base Register 1                                         |
*  |  R11 |  Base Register 2                                         |
*  |  R12 |  Base Register 3                                         |
*  |  R13 |  Address of SAVEAREA                                     |
*  +------+----------------------------------------------------------+
*  |  R15 |  Return Code upon exit                                   |
*  +------+----------------------------------------------------------+
*
         EJECT
*  Table 2 - Return Codes
*
*  +------+----------------------------------------------------------+
*  |RETURN|                                                          |
*  | CODE |  DESCRIPTION                                             |
*  +------+----------------------------------------------------------+
*  |  00  |  Successful                                              |
*  +------+----------------------------------------------------------+
*  | 4096 |  ISPLINK not found, link error                           |
*  +------+----------------------------------------------------------+
*  | 4099 |  Program requires TSO                                    |
*  +------+----------------------------------------------------------+
*
         EJECT
*  Macros and SYSLIB Location:
*  ==================================================================
*
*  Macro     Description                         Library
*  --------  ----------------------------------  ------------------
*  YREGS     Register Equates                    SYS2.MACLIB
*  IHAPSA    Prefixed Save Area                  SYS1.AMODGEN
*  CVT       Communication Vector Table          SYS1.AMODGEN
*  IKJTCB    Task Control Block                  SYS1.AMODGEN
*  IEFTIOT1  Task IO Table                       SYS1.AMODGEN
*  IEFJFCBN  Job File Control Block              SYS1.AMODGEN
*  IHAASCB   Address Space Control Block         SYS1.AMODGEN
*
*  ISPFPL    ISPF Parm List for LBISPL           SHRABIT.MACLIB
*  ISPFSRV   ISPF Services and Keywords          SHRABIT.MACLIB
*  LBISPL    CALL equivalent for ISPLINK         SHRABIT.MACLIB
*  MOVEC     Move quoted string data             SHRABIT.MACLIB
*  MOVEI     Initialize variable                 SHRABIT.MACLIB
*  MOVER     Move string right-trim              SHRABIT.MACLIB
*  MOVEV     Move variable                       SHRABIT.MACLIB
*  RTRIM     Right-trim subroutine               SHRABIT.MACLIB
*  RDTECOMA  GETDTE Commarea                     SHRABIT.MACLIB
*
*  References:
*  ==================================================================
*
*  - GC28-6628-9 OS Rel 21.7 System Control Blocks
*
*
         EJECT
*  Change History:
*  ==================================================================
*
*  MM/DD/CCYY Version  Name / Description
*  ---------- -------  --------------------------------------------
*
*  08/10/2020 1.0.00   Larry Belmontes Jr.
*                      - Initial version released to MVS 3.8J
*                        hobbyist public domain
*
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
DALCDS   CSECT
*****************************************************************/
*                          *    Software Option Flags           */
         LCLB  &USEDTE     * Use GETDTE flag                    */
&USEDTE  SETB  0           *   0=No,   1=Yes                    */
*                          *                                    */
*****************************************************************/
         USING DALCDS,R10,R11,R12  my BASE REGISTER(S)
         PRINT NOGEN
*
*     * /********************************************************/
*     * /* Save regs and declare base registers R10,R11,R12     */
*     * /* including saving R1 (PARMS) in R2                    */
*     * /* - R1  myPARMS address on entry                       */
*     * /* - R15 myENTRY address on entry                       */
*     * /********************************************************/
         STM   R14,R12,12(R13)     Save calling program registers
         LR    R10,R15             Load base1 w R15
         LA    R11,4095(R10)       Load base2 (R11) w/ offset
         LA    R11,1(R11)          Adjust base2
         LA    R12,4095(R11)       Load base3 (R12) w/ offset
         LA    R12,1(R12)          Adjust base3
         LR    R15,R13             Save callers registers
         LR    R2,R1               Save PARM addr
*
*     * /********************************************************/
*     * /* Program Eyecatcher                                   */
*     * /********************************************************/
         B     OVRSTAMP            Branch past eye-catcher
MYSTAMP  EQU   *
PGMID    DC    CL8'DALCDS  '       My Program STAMP
         DC    CL8'MVS3.8J '       OS
PGMVRM   DC    CL8'V1.0.00 '       .Version
         DC    CL8'08102020'       ..date  MMDDCCYY
         DC    CL1' '
PGMDS    DC    CL8'&SYSDATE'       Assemble Date
         DC    CL3' - '            .and
PGMTS    DC    CL8'&SYSTIME'       ..Time
         DC    C'Copyright (C) 2020'
         DC    C'Larry Belmontes, Jr. '
         DC    C'https://ShareABitofIT.net/DALCDS-in-MVS38J'
MYSTAMPL EQU   *-MYSTAMP
OVRSTAMP DS    0H
*
         EJECT
*     * /********************************************************/
*     * /* GETMAIN, working storage, using R9                   */
*     * /* - R0  Working register                               */
*     * /* - R1  Working register (GETMAIN address)             */
*     * /* - R9  Working Storage DSECT address                  */
*     * /********************************************************/
         LA    R0,WSAREAL          R0 = size of Workarea DSECT
         GETMAIN   R,LV=(R0)       Get Storage, R1 = addr of storage
         LTR   R15,R15             Did we get the storage?
         BNZ   NOMAINS             NO, return with rc in R15
         LR    R9,R1               YES. R9 = Workarea DSECT Register
         USING WSDSECT,R9          Tell Assembler
*
*     * /********************************************************/
*     * /* Update Save Areas for caller and calling programs    */
*     * /* - R1  myPARMS address on entry  (restored)           */
*     * /********************************************************/
         LR    R15,R13             Save callers savearea addr
         LA    R13,SAVEAREA        Load Addr of my savearea
         ST    R15,4(,R13)         Chain callers to my savearea
         ST    R13,8(,R15)         Chain my savearea to callers
         LR    R1,R2               Restore R1 as PARM addr
*
         ST    R2,MYCPPLP          Store CPPL Address
*
*     * /********************************************************/
*     * /* ISPLINK Entry Point                                  */
*     * /* - R0 Working Register                                */
*     * /********************************************************/
         XC    ISPLINK,ISPLINK     Clear ISPLINK Address
         LOAD  EP=ISPLINK,ERRET=ERR4096
         ST    R0,ISPLINK          Save ISPLINK Entry Point
*
*     * /********************************************************/
*     * /* GETDTE  Entry Point                                  */
*     * /* - R0 Working Register                                */
*     * /********************************************************/
         XC    GETDTE,GETDTE       Clear GETDTE  Address
         AIF  (NOT &USEDTE).NODTE
         LOAD  EP=GETDTE,ERRET=GETDTENO
         ST    R0,GETDTE           Save GETDTE  Entry Point
.NODTE   ANOP
GETDTENO EQU   *
*
         EJECT
*     * /********************************************************/
*     * /* Check if we are running under TSO FG                 */
*     * /* - R3 Working Register                                */
*     * /********************************************************/
         MVI   AMITSO,C'Y'         Assume TSO FG
         L     R3,PSAAOLD-PSA(0)   R3=ASCB Address
         L     R3,ASCBTSB-ASCB(R3) R3=ASCBTSB Address
         LTR   R3,R3               If 0, program under TSO BG
         BZ    ERR4099             ..error, TSO BG!!
*
*     * /********************************************************/
*     * /* Initialize                                           */
*     * /********************************************************/
         MVC   RCEXIT,=F'0'        Zero Return Code R15
*
         BAL   R14,ISPCNTL         CONTROL ERRORS RETURN
         BAL   R14,ISPVDEF         VDEFINE ISPF Variables
*
         MOVEI DJMSG,ONLY          Blank DJMSG
         MOVEI PMYMSG,ONLY         Blank PMYMSG
         MVC   PMYRC,=C'00000'     Zero  PMYRC
         MOVEI PMYCMD,ONLY         Blank PMYCMD
         ZAP   ROWN,=P'0'          Zero Row Number counter
*
         EJECT
*     * /********************************************************/
*     * /* Get PARAMETER information passed to program          */
*     * /* - R1 myPARMS address on entry                        */
*     * /* - R4 Starting parms address                          */
*     * /* - R6 Parm Length                                     */
*     * /* - R3, R4, R5 Working Register                        */
*     * /********************************************************/
         MOVEI DDSRCH,ONLY         Blank DDSRCH
         L     R1,MYCPPLP          R1=Parm Address at start up
PARMS    EQU   *
         LTR   R1,R1               Do we have a PARM?
         BZ    TBLSTRT             NO, start process
         LR    R3,R1               YES, R3=PARM/CPPL addr
         TM    0(R3),X'80'         Is it PARM or CPPL addr?
         BZ    CPPLIN              YES, CPPL addr
PARMIN   EQU   *                   NO,  must be PARM addr
         L     R4,0(,R3)           R4=Addr of PARM
         LH    R6,0(,R4)           R6=Length of PARM
         LTR   R6,R6               PARM > 0 length?
         BZ    TBLSTRT             NO, start process
         LA    R4,2(,R4)           YES, R4=point to start of PARM data
         B     PARMSXT              and continue...
CPPLIN   EQU   *
         ST    R3,MYCPPLP          Store CPPL Address
         L     R4,0(,R3)           R4=Command Buffer addr
         LH    R5,2(,R4)           R5=Offset to Command Variables
         LTR   R5,R5               Any Variables?
         BZ    TBLSTRT             NO, start process
         LH    R6,0(,R4)           YES, R6=Length of Command Buffer
         SR    R6,R5               Subtract variable offset
         SH    R6,=H'4'            Subtract prefix
         BE    TBLSTRT             NO, start process
         BM    TBLSTRT             NO, start process
         LA    R4,4(R4,R5)         R4=Point to variables start addr
PARMSXT  EQU   *
*
         EJECT
*     * /********************************************************/
*     * /* Uppercase translation of PARMIN                      */
*     * /* - R4 Starting parms address                          */
*     * /* - R6 Parm Length                                     */
*     * /* - R8, R4, R5 Working Register                        */
*     * /********************************************************/
CAP#EM   EQU   *
         LR    R8,R6               R8=Parm Length
         BCTR  R8,0                Adjust length for OC
         EX    R8,UPPERC           Execute Uppercase translation
         B     UPPERCX
UPPERC   OC    0(0,R4),=256X'40'   EBCDIC Lower-Upper Case Translate
UPPERCX  EQU   *
*
*     * /********************************************************/
*     * /* GET DD from parm                                     */
*     * /********************************************************/
PARMSCAN EQU   *
         LR    R8,R6               R8=Parm Length
         C     R8,=F'8'            ParmLength > 8
         BNH   MOVERDO             No, move data
         LA    R8,8                Yes, Force 8 bytes
MOVERDO  EQU   *
         BCTR  R8,0                Adjust length for OC
         EX    R8,MOVERC           Execute Uppercase translation
         B     MOVERCX
MOVERC   MVC   DDSRCH(0),0(R4)     Move parm data to DDSRCH byte1-8
MOVERCX  EQU   *
*
*     * /********************************************************/
*     * /* Create TABLE                                         */
*     * /* - ISPEXEC TBCREATE TBLNAME                           */
*     * /*           NAMES                                      */
*     * /*           NOWRITE REPLACE                            */
*     * /********************************************************/
TBLSTRT  EQU   *
*        ISPLINK TBCREATE,tblname,keylist,namelist,nowrite,replace
        LBISPL ISPLINK,(TBCREATE,TBLNAME,B,TBLCOLS1,NOWRITE,REPLACE),VL
         ST    R15,RCEXIT          Store R15
         BAL   R14,ISPERR          Err Display, return here or MYEXIT
*
         EJECT
*     * /********************************************************/
*     * /* Scan TIOT for allocated DDNAMEs                      */
*     * /* - For each DDNAME, get DSN from JFCB                 */
*     * /* - R1, R2, R3, R4, R5, R6, R7, R8 Working Register    */
*     * /********************************************************/
         L     R2,CVTPTR           Address of CVT
         USING CVT,R2              Tell Assembler, CVT
         L     R2,CVTTCBP          Addrs (2 FW) pointing to TCB
         L     R2,4(R2)            Take 2nd FW as my TCB Addr
         USING TCB,R2              Tell Assembler, TCB
         L     R2,TCBTIO           Load TIOT Address
         USING TIOT,R2             Tell Assembler, TIOT
         MVC   PJOBNM,0(R2)        UserID
         MVC   PSTEPNM,8(R2)       Job Step
         MVC   PPROCNM,16(R2)      Proc Name
         LA    R1,TIOENTRY-TIOT1   Position past TIOT header
LOOPDDN  EQU   *
         AR    R2,R1               Point to TIOENTRY #1
         USING TIOENTRY,R2         Tell Assembler, TIOENTRY
         CLI   TIOELNGH,0          End of TIOT?
         BE    TIOT#EOD            Yes, done...
         TM    TIOESTTA,TIOSLTYP   Entry in use?
         BO    TIONEXT             Yes, get another
*
         CLI   DDSRCH,C' '         Specific DD request?
         BE    DDSTART             No, display DD info
         CLC   TIOEDDNM,DDSRCH     Yes. Is this the DD?
         BE    DDSTART               Yes, display DD info
         CP    ROWN,=P'0'            No. Already displayed DD info?
         BE    TIONEXT                 No, get next TIOT entry
         CLI   TIOEDDNM,C' '           Yes. Concatenation for DD?
         BNE   TIOT#EOD                  No, done...
*
*     * /********************************************************/
*     * /* Dataset Name              (from TIOT)                */
*     * /********************************************************/
DDSTART  EQU   *
         MVC   PDDNAME,TIOEDDNM    DDNAME
*
*     * /********************************************************/
*     * /* Row Number represents DD item count                  */
*     * /********************************************************/
         MOVEI PROWNUM,ONLY        Blank ROWNUM
         CLI   PDDNAME,C' '        DD Blank?
         BE    BYPSCNT             Yes, bypass count DD
CNTIT$   EQU   *                   No, count DD
         AP    ROWN,=P'1'          Add 1 to row counter
         UNPK  PROWNUM,ROWN        Unpack row counter
         OI    PROWNUM+2,X'F0'     Force F-zone
BYPSCNT  EQU   *
*
         EJECT
*     * /********************************************************/
*     * /* Point to JFCB for DD     (TIOT -> JFCB)              */
*     * /********************************************************/
         SR    R5,R5               Clear R8
         ICM   R5,B'0111',TIOEJFCB Load JFCB Addr
         LA    R5,16(,R5)          Bump past header prefix
         USING JFCB,R5             Tell Assembler, JFCB
*
*     * /********************************************************/
*     * /* Volume Serial            (from JFCB)                 */
*     * /********************************************************/
         MVC   PVOLSER,JFCBVOLS    VOLSER
*
*     * /********************************************************/
*     * /* DSN, Member name         (from JFCB)                 */
*     * /********************************************************/
         TM    TIOELINK,TIOTTERM   TERMINAL allocation?
         BO    TERM#               Yes, branch to TERM
DSN#     EQU   *                   No, move DSN(mem)
         MOVEI PDSNAME             Move DSN
         MOVER JFCBDSNM,44         Set length due to DSECT label
         CLI   JFCBELNM,C' '       Member Name?
         BE    DSN#X               No, done...
         MOVEC '('                 Yes, move member name
         MOVER JFCBELNM,8          set length due to DSECT label
         MOVEC '('
         B     DSN#X
TERM#    EQU   *
         MOVEI PDSNAME             Move 'Terminal'
         MOVEC 'Terminal'
         B     DSN#X
DSN#X    EQU   *
*
         EJECT
*     * /********************************************************/
*     * /* Dataset Organization Type (from JFCB)                */
*     * /********************************************************/
         MVC   PDSORG,=C'   '      Blank DSO
         CLI   PDDNAME,C' '        DDNAME Blank?
         BE    DORG1X              Yes, display DSORG as blank
*                                  No, determine DSORG
         TM    JFCDSRG2,JFCORGAM         08  .... 1...
         BO    DORG15
         TM    JFCDSRG1,JFCORGIS         80  1... ....
         BO    DORG12
         TM    JFCDSRG1,JFCORGPS         40  .1.. ....
         BO    DORG14
         TM    JFCDSRG1,JFCORGDA         20  ..1. ....
         BO    DORG13
         TM    JFCDSRG1,JFCORGPO         02  .... ..1.
         BO    DORG11
         MVC   PDSORG,=C'???'            Unknown DSO
         B     DORG1X
*
DORG11   EQU   *
         MVC   PDSORG,=C'PO '
         B     DORG1X
DORG12   EQU   *
         MVC   PDSORG,=C'IS '
         B     DORG1X
DORG13   EQU   *
         MVC   PDSORG,=C'DA '
         B     DORG1X
DORG14   EQU   *
         MVC   PDSORG,=C'PS '
         B     DORG1X
DORG15   EQU   *
         MVC   PDSORG,=C'VS '
         B     DORG1X
DORG1X   EQU   *
*
         EJECT
*     * /********************************************************/
*     * /* Disposition Type (from JFCB)                         */
*     * /********************************************************/
         TM    JFCBIND2,JFCNEW           c0   11.. ....
         BO    DISP11
         TM    JFCBIND2,JFCMOD           80   1... ....
         BO    DISP12
         TM    JFCBIND2,JFCOLD+JFCSHARE  48   .1.. 1...
         BO    DISP14
         TM    JFCBIND2,JFCOLD           40   .1.. ....
         BO    DISP13
         TM    JFCBIND2,JFCSHARE         08   .... 1...
         BO    DISP14
         TM    JFCBIND2,JFCTEMP          01   .... ...1
         BO    DISP15
DISP1#   EQU   *
         MVC   PDISP1,=C'???'
         B     DISP1X
DISP11   EQU   *
         MVC   PDISP1,=C'New'
         B     DISP1X
DISP12   EQU   *
         MVC   PDISP1,=C'Mod'
         B     DISP1X
DISP13   EQU   *
         MVC   PDISP1,=C'Old'
         B     DISP1X
DISP14   EQU   *
         MVC   PDISP1,=C'Shr'
         B     DISP1X
DISP15   EQU   *
         MVC   PDISP1,=C'Tmp'
         B     DISP1X
DISP1X   EQU   *
*
         EJECT
ADDEM    EQU   *
*     * /********************************************************/
*     * /* Add TABLE row                                        */
*     * /* - ISPEXEC TBADD    TBLNAME                           */
*     * /* - R1, R14, R15 Working Register                      */
*     * /********************************************************/
         LBISPL ISPLINK,(TBADD,TBLNAME),VL
         ST    R15,RCEXIT          Store R15
         BAL   R14,ISPERR          Err Display, return here or MYEXIT
*
*     * /********************************************************/
*     * /* Point to next TIO Entry                              */
*     * /********************************************************/
TIONEXT  EQU   *
         XR    R1,R1               Clear R1
         IC    R1,TIOELNGH         Load TIO Entry Length
         B     LOOPDDN             Try next
*
         EJECT
*     * /********************************************************/
*     * /* End of TIOT                                          */
*     * /********************************************************/
TIOT#EOD EQU   *                   TIOT End of Data
         MVC   PANTBL,PANALCDS     Panel name
         MVC   PANCSR,ZCMD         Panel Cursor
         MOVEI PANMSG,ONLY         Blank message number
*
         CLI   DDSRCH,C' '         Specific DD request?
         BE    SET@TOP             No, continue
         CP    ROWN,=P'0'          Yes. Found specific DD?
         BNE   SET@TOP               Yes, continue
*                                    No, display no DD found
         ZAP   ROWN,=P'0'          Zero Row Number counter
         MOVEI PROWNUM,ONLY        Blank ROWNUM
         MOVEI PDDNAME             Move DDSRCH value
         MOVEV DDSRCH
         MOVEI PDSORG,ONLY         Blank DSORG
         MOVEI PVOLSER,ONLY        Blank VOLSER
         MOVEI PDSNAME             Move DD not found message
         MOVEC '** DD not found'
         MOVEI PDISP1,ONLY         Blank DISP1
         MOVEI DDSRCH,ONLY         Blank DDSRCH
         LBISPL ISPLINK,(TBADD,TBLNAME),VL
         ST    R15,RCEXIT          Store R15
         BAL   R14,ISPERR          Err Display, return here or MYEXIT
*
*     * /********************************************************/
*     * /* Position to TOP of TABLE                             */
*     * /* - ISPEXEC TBTOP    TBLNAME                           */
*     * /********************************************************/
SET@TOP  EQU   *
         LBISPL ISPLINK,(TBTOP,TBLNAME),VL
         ST    R15,RCEXIT          Store R15
         BAL   R14,ISPERR          Err Display, return here or MYEXIT
*
         EJECT
*     * /********************************************************/
*     * /* Display TABLE                                        */
*     * /* - ISPEXEC TBDISPL  TBLNAME  PANEL(PANTBL)            */
*     * /* -         AUTOSEL(NO) CURSOR(ZCMD) CSRROW(CSSR)      */
*     * /********************************************************/
*     * /* - ISPLINK TBDISPL,tblname,panel,msgid,csrfldnm,tblrow#,
*     * /* -         csrpos,YES/NO,crpname,rowidname,msgfldname)
*     * /********************************************************/
TBDSP    EQU   *
*     * /********************************************************/
*     * /* Contruct date and time for SCRNCLK variable          */
*     * /* ---------------------------------------------------- */
*     * /* If GETDTE subroutine is available,                   */
*     * /*     source date and time from GETDTE                 */
*     * /* Else                                                 */
*     * /*     source date and time from ISPF Z variables       */
*     * /********************************************************/
         L     R15,GETDTE          R15=GETDTE Entry
         LTR   R15,R15             Do we have an address?
         BZ    DT@ZVARS            No, use Z-vars
         LA    R1,$DTECOMS         R1 - Addr of Results
         ST    R1,DTPARM1               ...as PARM1
         OI    DTPARM1,X'80'       VL Mark
         LA    R1,DTPARML          R1 - Addr of DT PARM LIST
         BALR  R14,R15             Get date/time frm GETDTE

         LR    R3,R15              Load R3 w R15
         CVD   R3,DW               Convert to decimal
         UNPK  DJMSG+08(5),DW+5(3) Unpack
         OI    DJMSG+12,X'F0'      Force an F Zone

         STM   R0,R1,DW            SAVE    R0,R1  to   DW before TPUT   LB1010e
         TPUT  DJMSG,15                                                 LB1010e
         LM    R0,R1,DW            RESTORE R0,R1  from DW after  TPUT   LB1010e

*        LTR   R15,R15             GETDTE RC=0 ?
*        BNZ   DT@ZVARS            No, use Z vars
         MVC   PSCRNCLK,RDATE      dd/mm/ccyy.jjj hh:mm:ss
         B     TBDSP1
DT@ZVARS EQU   *
         MOVEI PSCRNCLK
         MOVER PZDATE              dd/mm/yy
         MOVEC '.'
         MOVEV PZJDATE+3,3         20.jjj
         MOVEC ' '
         MOVER PZTIME              hh:mm
TBDSP1   EQU   *
        LBISPL ISPLINK,(TBDISPL,TBLNAME,PANTBL,PANMSG,PANCSR),VL
**      LBISPL ISPLINK,(TBDISPL,TBLNAME,PANTBL,PANMSG,PANCSR,B,B,NO),VL
***     LBISPL ISPLINK,(TBDISPL,TBLNAME,PANTBL,B,B,B,B,NO),VL
         ST    R15,RCEXIT          Store R15
         C     R15,=F'8'           RC=8 (PF3/PF4, END/RETURN)
         BE    TBDSPX              Yes, Exit
         BAL   R14,ISPERR          Err Display, return here or MYEXIT
*
         EJECT
*     * /********************************************************/
*     * /* Process COMMAND from TBDISPL                         */
*     * /********************************************************/
*     * /* ZCMD          Action                                 */
*     * /* ------------  ---------------------------------      */
*     * /* Blanks        Do nothing                             */
*     * /* /xxxxxxxx     Search for DD xxxxxxxx                 */
*     * /* Other         Invalid Command                        */
*     * /********************************************************/
DO#CMD   EQU   *
         CLI   PZCMD,C' '          Blank ZCMD?
         BE    TBDSP               Yes, redisplay table
         CLI   PZCMD,C'/'          DD search?
         BE    DDCMD               Yes, search for DD
         MVC   PANMSG,=C'ISRZ002 ' Use generic ISPF msg ISRZ002
         MOVEI PZERRSM,ONLY        Blank ZERRSM
         MOVEI PZERRLM             Blank ZERRLM
         MOVEC 'Invalid Command '
         MOVER PZCMD
         MVC   PANCSR,=C'ZCMD    ' Panel Cursor
         B     TBDSP               Redisplay
*
*     * /********************************************************/
*     * /* Close (TBEND) current TABLE                          */
*     * /* - ISPEXEC TBEND    TBLNAME                           */
*     * /********************************************************/
DDCMD    EQU   *
         ZAP   ROWN,=P'0'          Zero Row Number counter
         MVC DDSRCH,PZCMD+1        Move 8 bytes after / as DD name
         LBISPL ISPLINK,(TBEND,TBLNAME),VL
         ST    R15,RCEXIT          Store R15
         BAL   R14,ISPERR          Err Display, return here or MYEXIT
         B     TBLSTRT             Look for requested DD
*
TBDSPX   EQU   *
*
         EJECT
*     * /********************************************************/
*     * /* End TABLE Processing                                 */
*     * /* - ISPEXEC TBEND    TBLNAME                           */
*     * /********************************************************/
*     * /* - ISPLINK TBEND,tblname
*     * /* -         ?,crpname,rowidname,msgfldname)
*     * /********************************************************/
         LBISPL ISPLINK,(TBEND,TBLNAME),VL
         ST    R15,RCEXIT          Store R15
         BAL   R14,ISPERR          Err Display, return here or MYEXIT
*
*     * /********************************************************/
*     * /* Delete Functional Variables                          */
*     * /* - ISPEXEC VDELETE *                                  */
*     * /********************************************************/
*     * /* - ISPLINK VDELETE,namelist
*     * /********************************************************/
MYEXIT   EQU   *
         LBISPL ISPLINK,(VDELETE,ALL),VL
         DELETE EP=ISPLINK
         DELETE EP=GETDTE
*
         EJECT
*     * /********************************************************/
*     * /* - Restore savearea, FREEMAIN, and                    */
*     * /*   return to caller                                   */
*     * /********************************************************/
*
*     * /--------------------------------------------------------/
*     * /* R1 = my Working Storage Address from GETMAIN         */
*     * /********************************************************/
         LR    R1,R13              Working Storage Addr
*
*     * /--------------------------------------------------------/
*     * /* Get callers savearea into R13 and overlay my R15     */
*     * /* to be returned to caller                             */
*     * /--------------------------------------------------------/
         L     R13,SAVEAREA+4      Restore R13
         L     R5,RCEXIT           Set R15 to my exit value
*
*     * /--------------------------------------------------------/
*     * /* Working Storage FREEMAIN                             */
*     * /--------------------------------------------------------/
         LA    R0,WSAREAL          Size of WSAREA DSECT
         FREEMAIN  R,LV=(R0),A=(R1)    Free Storage
*
*     * /--------------------------------------------------------/
*     * /* Restore caller registers and return to caller        */
*     * /--------------------------------------------------------/
         LR    R15,R5              R15 = RC for exit
NOMAINS  EQU   *
         L     R14,12(R13)         Restore R14
         LM    R0,R12,20(R13)      Restore R0 thru R12
         BR    R14                 Return to caller
*
         TITLE 'DALCDS - Error Entry Points                           '
*     * /********************************************************/
*     * /* Error Setting                                        */
*     * /* - R7  Working Register                               */
*     * /* - R15 Working Register and RC exit code register     */
*     * /********************************************************/
ERR4096  EQU   *
         MVC   RCEXIT,=F'4096'     ISPLINK  not found, link error
         MVC   DJMSG+15(MSG4096L),MSG4096
         B     ERR#GO
ERR4099  EQU   *
         MVC   RCEXIT,=F'4099'     Program requires TSO
         MVC   DJMSG+15(MSG4099L),MSG4099
         B     ERR#GO
ERR#GO   EQU   *
         BAL   R7,PREFXMSG         Prefix Message
         TPUT  DJMSG,L'DJMSG
         B     MYEXIT
*
         EJECT
*     * /********************************************************/
*     * /* Subroutine -                                  (R14)  */
*     * /*  ISPEXEC CONTROL ERRORS RETURN                       */
*     * /* - R15, R1    Working Register                        */
*     * /********************************************************/
ISPCNTL  EQU   *
         ST    R14,SAVER14S
*        CALL  ISPLINK,(CONTROL,ERRORS,RETURN),VL
         LBISPL ISPLINK,(CONTROL,ERRORS,RETURN),VL
*
         L     R14,SAVER14S
         BR    R14
*
         EJECT
*     * /********************************************************/
*     * /* Subroutine -                                  (R14)  */
*     * /*  ISPEXEC VDEFINE function variables                  */
*     * /* - R15, R1    Working Register                        */
*     * /********************************************************/
ISPVDEF  EQU   *
         ST    R14,SAVER14S
*        CALL  ISPLINK,(VDEFINE,KEYPRESS,PKYPRS,CHAR,L4),VL
         LBISPL ISPLINK,(VDEFINE,DPGMVRM,PGMVRM,CHAR,LPGMVRM),VL
         LBISPL ISPLINK,(VDEFINE,MYMSG,PMYMSG,CHAR,LMYMSG),VL
         LBISPL ISPLINK,(VDEFINE,MYRC,PMYRC,CHAR,LMYRC),VL
         LBISPL ISPLINK,(VDEFINE,MYCMD,PMYCMD,CHAR,LMYCMD),VL
*
         LBISPL ISPLINK,(VDEFINE,KEYPRESS,PKYPRS,CHAR,LKYPRS),VL
         LBISPL ISPLINK,(VDEFINE,RESP0,PRESP0,CHAR,LRESP0),VL
         LBISPL ISPLINK,(VDEFINE,ZCMD,PZCMD,CHAR,LZCMD),VL
*
         LBISPL ISPLINK,(VDEFINE,SCRNCLK,PSCRNCLK,CHAR,LSCRNCLK),VL
         LBISPL ISPLINK,(VDEFINE,ZDATE,PZDATE,CHAR,LZDATE),VL
         LBISPL ISPLINK,(VDEFINE,ZJDATE,PZJDATE,CHAR,LZJDATE),VL
         LBISPL ISPLINK,(VDEFINE,ZTIME,PZTIME,CHAR,LZTIME),VL
*
         LBISPL ISPLINK,(VDEFINE,SEL,TSEL,CHAR,LSEL),VL
*
         LBISPL ISPLINK,(VDEFINE,ZTDSELS,PZTDSELS,CHAR,LZTDSELS),VL
         LBISPL ISPLINK,(VDEFINE,ZTDTOP,PZTDTOP,CHAR,LZTDTOP),VL
         LBISPL ISPLINK,(VDEFINE,ZTDROWS,PZTDROWS,CHAR,LZTDROWS),VL
*
         LBISPL ISPLINK,(VDEFINE,ZERRSM,PZERRSM,CHAR,LZERRSM),VL
         LBISPL ISPLINK,(VDEFINE,ZERRLM,PZERRLM,CHAR,LZERRLM),VL
*
         LBISPL ISPLINK,(VDEFINE,JOBNM,PJOBNM,CHAR,LJOBNM),VL
         LBISPL ISPLINK,(VDEFINE,STEPNM,PSTEPNM,CHAR,LSTEPNM),VL
         LBISPL ISPLINK,(VDEFINE,PROCNM,PPROCNM,CHAR,LPROCNM),VL
*
         LBISPL ISPLINK,(VDEFINE,ROWNUM,PROWNUM,CHAR,LPROWNUM),VL
         LBISPL ISPLINK,(VDEFINE,DDNAME,PDDNAME,CHAR,LDDNAME),VL
         LBISPL ISPLINK,(VDEFINE,DSORG,PDSORG,CHAR,LDSORG),VL
         LBISPL ISPLINK,(VDEFINE,VOLSER,PVOLSER,CHAR,LVOLSER),VL
         LBISPL ISPLINK,(VDEFINE,DSNAME,PDSNAME,CHAR,LPDSNAME),VL
         LBISPL ISPLINK,(VDEFINE,DISP1,PDISP1,CHAR,LPDISP1),VL
*
         L     R14,SAVER14S
         BR    R14
*
         EJECT
*     * /********************************************************/
*     * /* Subroutine -                                  (R14)  */
*     * /*  ISPF Error Display and return                       */
*     * /*   to caller or MYEXIT                                */
*     * /* - RCEXIT     on entry                                */
*     * /* - R15, R1    Working Register                        */
*     * /********************************************************/
ISPERR   EQU   *
         ST    R14,SAVER14S
         L     R15,RCEXIT
         LTR   R15,R15             RC=0 (Process)
         BZ    ISPERRX1            Yes, return to caller
*                                  No, display error and exit
         MVC   DJMSG(L'DSPLERR0),DSPLERR0
         L     R1,ISPFP1           ISPF Service Requested
         MVC   DJMSG+03(08),0(R1)
         MVC   DJMSG+11(06),=C'      '
         CVD   R15,DW              Convert to decimal
         UNPK  DJMSG+20(05),DW+5(03) Unpack and
         OI    DJMSG+24,X'F0'      ... force and F zone
         MVI   DJMSG+20,C'='       Restore equal sign
         STM   R0,R1,DW            SAVE    R0,R1  to   DW before TPUT
         TPUT  DJMSG,L'DSPLERR0
         LM    R0,R1,DW            RESTORE R0,R1  from DW after  TPUT
*
         LA    R14,MYEXIT          R14=Addr MYEXIT
         B     ISPERRX2            Return to MYEXIT
*
ISPERRX1 EQU   *
         L     R14,SAVER14S
ISPERRX2 EQU   *
         BR    R14
*
         EJECT
         RTRIM                     RTRIM Subroutine
*
         EJECT
*     * /********************************************************/
*     * /* Subroutine - Prefix message                   (R7)   */
*     * /* w/ 15-byte header 'pgmid    nnnn -'                  */
*     * /* - R3, R7 Working Register                            */
*     * /********************************************************/
PREFXMSG EQU   *
         ST    R7,SAVER7S          Save R7 Subroutine
         MVC   DJMSG+00(8),PGMID
         L     R3,RCEXIT           Load RCEXIT code value
         CVD   R3,DW               Convert to decimal
         UNPK  DJMSG+08(5),DW+5(3) Unpack
         OI    DJMSG+12,X'F0'      Force an F Zone
         MVI   DJMSG+08,C' '       Blank out digit
         MVI   DJMSG+14,C'-'       Blank out digit
*
         L     R7,SAVER7S          Restore R7 Subroutine
         BR    R7                  Return to caller
*
         TITLE 'DALCDS - Literal Pool                                 '
         LTORG
*
         TITLE 'DALCDS - Constants                                    '
         ISPFSRV                   ISPF Service Constants
         EJECT
*     * /********************************************************/
*     * /* ISPF Constants for services                          */
*     * /********************************************************/
PANALCDS DC    CL8'PDALCDS0'              Panel ID  TSO Allocations
*
         EJECT
*     * /********************************************************/
*     * /* ISPF Variable Names                                  */
*     * /********************************************************/
DPGMVRM  DC    CL8'DPGMVRM '       VarName for PGMVRM
MYMSG    DC    CL8'MYMSG '         VarName for PMYMSG
MYRC     DC    CL8'MYRC    '       VarName for PMYRC
MYCMD    DC    CL8'MYCMD '         VarName for PMYCMD
*
KEYPRESS DC    CL8'KEYPRESS'       Varname for PKYRS
RESP0    DC    CL8'RESP0   '       Varname for PRESP0
ZCMD     DC    CL8'ZCMD    '       VarName for PZCMD
SCRNCLK  DC    CL8'SCRNCLK '       VarName for PSCRNCLK
ZDATE    DC    CL8'ZDATE   '       VarName for PZDATE
ZJDATE   DC    CL8'ZJDATE  '       VarName for PZJDATE
ZTIME    DC    CL8'ZTIME   '       VarName for PZTIME
*
SEL      DC    CL8'SEL     '       VarName for TSEL
*
ZTDSELS  DC    CL8'ZTDSELS '       VarName for PZTDSELS
ZTDTOP   DC    CL8'ZTDTOP  '       VarName for PZTDTOP
ZTDROWS  DC    CL8'ZTDROWS '       VarName for PTDROWS
*
ZERRSM   DC    CL8'ZERRSM  '       VarName for ZERRSM
ZERRLM   DC    CL8'ZERRLM  '       VarName for ZERRLM
*
JOBNM    DC    CL8'JOBNM   '       VarName for PJOBNM
STEPNM   DC    CL8'STEPNM  '       VarName for PSTEPNM
PROCNM   DC    CL8'PROCNM  '       VarName for PROCNM
*
ROWNUM   DC    CL8'ROWNUM  '       VarName for PROWNUM
DDNAME   DC    CL8'DDNAME  '       VarName for PDDNAME
DSORG    DC    CL8'DSORG   '       VarName for PDSORG
VOLSER   DC    CL8'VOLSER  '       VarName for PVOLSER
DSNAME   DC    CL8'DSNAME  '       VarName for PDSNAME
DISP1    DC    CL8'DISP1   '       VarName for PDISP1
*
         EJECT
*     * /********************************************************/
*     * /* ISPF Variable Names Lengths                          */
*     * /********************************************************/
         DS    0F
LPGMVRM  DC    A(L'PGMVRM)         PGMVRM
LMYMSG   DC    A(L'PMYMSG)         PMYMSG
LMYRC    DC    A(L'PMYRC)          PMYRC
LMYCMD   DC    A(L'PMYCMD)         PMYCMD
*
LKYPRS   DC    A(L'PKYPRS)         PKYRS
LRESP0   DC    A(L'PRESP0)         PRESP0
LZCMD    DC    A(L'PZCMD)          PZCMD
LSCRNCLK DC    A(L'PSCRNCLK)       PSCRNCLK
LZDATE   DC    A(L'PZDATE)         PZDATE
LZJDATE  DC    A(L'PZJDATE)        PZJDATE
LZTIME   DC    A(L'PZTIME)         PZTIME
*
LSEL     DC    A(L'TSEL)           SEL    Selection
*
LZTDSELS DC    A(L'PZTDSELS)       PZTDSELS
LZTDTOP  DC    A(L'PZTDTOP)        PZTDTOP
LZTDROWS DC    A(L'PZTDROWS)       PZTDROWS
*
LZERRSM  DC    A(L'PZERRSM)        PZERRSM
LZERRLM  DC    A(L'PZERRLM)        PZERRLM
*
LJOBNM   DC    A(L'PJOBNM)         PJOBNM
LSTEPNM  DC    A(L'PSTEPNM)        PSTEPNM
LPROCNM  DC    A(L'PPROCNM)        PROCNM
*
LPROWNUM DC    A(L'PROWNUM)        PROWNUM
LDDNAME  DC    A(L'PDDNAME)        PDDNAME
LDSORG   DC    A(L'PDSORG)         PDSORG
LVOLSER  DC    A(L'PVOLSER)        VOLSER VOLUME
LPDSNAME DC    A(L'PDSNAME)        PDSNAME
LPDISP1  DC    A(L'PDISP1)         PDISP1
*
         EJECT
*     * /********************************************************/
*     * /* ISPF Tables Constants                                */
*     * /********************************************************/
TBLNAME  DC    CL8'ALCDS0'         Table name
*                                  Table column names
TBLCOLS1 DC    C'(ROWNUM DDNAME DSORG VOLSER DSNAME'
         DC    C' DISP1)'
*
         EJECT
*     * /********************************************************/
*     * /* Local Messages                                       */
*     * /********************************************************/
MSG4096  DC    C'ISPLINK  not found, link error'
MSG4096L EQU   *-MSG4096
MSG4099  DC    C'Program requires TSO'
MSG4099L EQU   *-MSG4099
*
DSPLERR0 DC    C'** xxxxxxxxxxxxxx RC=xxxx, Process Ended!'
*
         TITLE 'DALCDS - Equates                                      '
*     * /********************************************************/
*     * /* E Q U A T E S                                        */
*     * /********************************************************/
*     *
*     *
*     * /********************************************************/
*     * /* Register Equates                                     */
*     * /********************************************************/
         YREGS                     Register EQU as Rn or Rnn
*
*R0       EQU   0
*R1       EQU   0
*R2       EQU   0
*R3       EQU   0
*R4       EQU   0
*R5       EQU   0
*R6       EQU   0
*R7       EQU   0
*R8       EQU   0
*R9       EQU   0
*R10      EQU   0
*R11      EQU   0
*R12      EQU   0
*R13      EQU   0
*R14      EQU   0
*R15      EQU   0
*
         TITLE 'DALCDS - System DSECTs                                '
*     *
*     * /********************************************************/
*     * /* System DSECTs                                        */
*     * /********************************************************/
         IHAPSA                    Prefixed Save Area
PSALEN   EQU   *-PSA               Length of PSA
         EJECT
         CVT   DSECT=YES,LIST=YES  Communication Vector Table
         EJECT
         IKJTCB DSECT=YES,LIST=YES Task Control Block
         EJECT
TIOT     DSECT
         IEFTIOT1                  Task IO Table
TIOTLEN  EQU   *-TIOT              Length of TIOT
         EJECT
JFCB     DSECT
         IEFJFCBN  LIST=YES        Job File Control BLock
JFCBLEN  EQU   *-JFCB              Length of JFCB
         EJECT
         IHAASCB                   Address Space Control Block
ASCBLEN  EQU   *-ASCB              Length of ASCB
*
         TITLE 'DALCDS - Working Storage Variables                    '
*     * /********************************************************/
*     * /* Working Storage  DSECT                               */
*     * /********************************************************/
WSDSECT  DSECT
WSAREA   EQU   *
*     *
*     * /********************************************************/
*     * /* mySAVEARA                                            */
*     * /********************************************************/
SAVEAREA DS    18F                 my Savearea
*                      +00  A(savearea of current CSECT)
*                      +04  A(savearea of calling CSECT)
*                      +08  A(savearea of called  CSECT)
*                      +12  R14
*                      +16  R15
*                      +20  R0
*                      +24  R1
*                      +28  R2
*                      +32  R3
*                      +36  R4
*                      +40  R5
*                      +44  R6
*                      +48  R7
*                      +52  R8
*                      +56  R9
*                      +60  R10
*                      +64  R11
*                      +68  R12
*
         EJECT
         ISPFPL                    ISPF Parm List for LBISPL macro
         EJECT
*     * /********************************************************/
*     * /* ISPF Variables                                       */
*     * /********************************************************/
PMYMSG   DS    CL50                MYMSG
PMYRC    DS    CL5                 MYRC
PMYCMD   DS    CL100               MYCMD
*
PKYPRS   DS    CL4                 KEYPRESS
PRESP0   DS    CL8                 RESP0
PZCMD    DS    CL50                ZCMD
PSCRNCLK DS    CL23                PSCRNCLK
PZDATE   DS    CL8                 ZDATE   DD/MM/YY
PZJDATE  DS    CL6                 ZJDATE  YY.JJJ
PZTIME   DS    CL5                 ZTIME   HH:MM
*
TSEL     DS    CL3                 SEL    Selection
*
PZTDSELS DS    CL4                 ZTDSELS
PZTDTOP  DS    CL6                 ZTDTOP
PZTDROWS DS    CL6                 ZTDROWS
*
PZERRSM  DS    CL24                ZERRSM
PZERRLM  DS    CL78                ZERRLM
*
PJOBNM   DS    CL8                 JOBNM
PSTEPNM  DS    CL8                 STEPNM
PPROCNM  DS    CL8                 ROCNM
*
PROWNUM  DS    CL3                 ROWNUM
PDDNAME  DS    CL8                 DDNAME
PDSORG   DS    CL3                 DSORG
PVOLSER  DS    CL6                 VOLSER VOLUME
PDSNAME  DS    CL54                DSNAME
PDISP1   DS    CL3                 DISP1
*
         EJECT
*     * /********************************************************/
*     * /* Program Variables                                    */
*     * /********************************************************/
SAVER3S  DS    F                   R3  Save in Subroutines
SAVER7S  DS    F                   R7  Save in Subroutines
SAVER14S DS    F                   R14 Save in Subroutines
DW       DS    D                   Double Word area
FW       DS    F                   Full Word   area
RCEXIT   DS    F                   Exit RC
AMITSO   DS    C                   TSO Flag
MYCPPLP  DS    F                   CPPL Address
DJMSG    DS    CL100               MY MESSAGE VARIABLE
PANTBL   DS    CL8                 Table Panel Name
PANMSG   DS    CL8                 Panel Message Number
PANCSR   DS    CL8                 Panel Cursor Field
GETDTE   DS    F                   Entry point for GETDTE
DTPARML  DS    0F                  GETDTE Parameter List
DTPARM1  DS    A                   Address of Results Area
ROWN     DS    PL2                 DD Row Counter
DDSRCH   DS    CL8                 Search DD
*
         EJECT
         RDTECOMA                  GETDTE Commarea
         EJECT
*
         DS    0F,(40)X
WSAREAE  EQU   *                   Working Storage END
WSAREAL  EQU   WSAREAE-WSAREA      Working Storage Area Length
         EJECT
         END   DALCDS
