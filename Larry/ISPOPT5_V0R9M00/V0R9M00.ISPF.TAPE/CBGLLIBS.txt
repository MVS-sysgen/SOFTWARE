PROC 0 PANID(NOPANIDX)

/********************************************************************/
/*                                                                  */
/* CLIST: CBGLLIBS                                                  */
/*                                                                  */
/* Author: Larry Belmontes Jr.                                      */
/*         https://ShareABitofIT.net/ISPOPT5-in-MVS38J              */
/*         Copyright (C) 2021  Larry Belmontes, Jr.                 */
/*                                                                  */
/*                                                                  */
/* Description:                                                     */
/* ---------------------------------------------------------------  */
/*                                                                  */
/* This CLIST will list DSNs searched for SOURCE as JCL comment     */
/* statements for background processing option 5.                   */
/*                                                                  */
/*                                                                  */
/* Command:                                                         */
/* CBGLLIBS PANID(PANID)                                            */
/*                                                                  */
/* Parameters:                                                      */
/* PANID    panel name for selected background process              */
/*                                                                  */
/*                                                                  */
/* Disclaimer:                                                      */
/* ================================================================ */
/*                                                                  */
/*    No guarantee; No warranty; Install / Use at your own risk.    */
/*                                                                  */
/*    This software is provided "AS IS" and without any expressed   */
/* or implied warranties, including, without limitation, the        */
/* implied warranties of merchantability and fitness for a          */
/* particular purpose.                                              */
/*                                                                  */
/*    The author requests keeping authors name intact in any        */
/* modified versions.                                               */
/*                                                                  */
/*    In addition, the author requests readers to submit any        */
/* code modifications / enhancements and associated comments        */
/* for consideration into a subsequent release (giving credit       */
/* to contributor(s)) thus, improving overall functionality         */
/* and further benefiting the MVS 3.8J hobbyist public domain       */
/* community.                                                       */
/*                                                                  */
/*                                                                  */
/*                                                                  */
/* Change History:                                                  */
/* ================================================================ */
/* MM/DD/CCYY Version  Name / Description                           */
/* ---------- -------  -------------------------------------------- */
/* 10/31/2021 0.9.00   Larry Belmontes Jr.                          */
/*                     - Initial version released to MVS 3.8J       */
/*                       hobbyist public domain                     */
/*                                                                  */
/* 05/13/2018 0.1.00   Larry Belmontes Jr.                          */
/*                     - Initial protyping with initial version     */
/*                       of ISPF from Wally Mclaughlin              */
/*                                                                  */
/********************************************************************/

/*---START------------------- $$cbg-globals -------------------------*/
  /****************************************************************
  /* Global Variables to SHARE with subordinate CLISTS
  /****************************************************************
  GLOBAL JCLGEN TDD TDSN TDSA LIBMEM LINMEM +
         ODSNTMP ODSNMEM SRCFND CSRF CMNT P +
         LMDMEM                             +
         LBBGSO  LBBGPO                     +
         LBXODSN LBXPROJ LBXTYPE LBXMBR     +
         LBXLIB1 LBXLIB2 LBXLIB3 LBXLIB4    +
         LBXSTEP1                           +
         LBXTST  LBXOPT1                    +
         LBXGO   LBXCMPL                    +
         LBXSOUT LBXLSTID                   +
         LBXSL1  LBXSL2  LBXSL3  LBXSL4     +
         LBXJCL1 LBXJCL2 LBXJCL3 LBXJCL4
/*---END--------------------- $$cbg-globals -------------------------*/

  SET EXITCD = 0

  /****************************************************************
  /* COMMENT: LIST LIBRARIES ELIGIBLE FOR SOURCE CONTENT SEARCH
  /*          WHEN SOURCE ONLINE SET TO YES
  /****************************************************************
  IF &LBBGSO = Y THEN +
    DO
      PUTCARD '//* SOURCE LIBRARIES SEARCHED:-----------'
      IF &LBXODSN > &STR() THEN +
        DO
          /********************************************************
          /* LIST OTHER DSN
          /********************************************************
          PUTCARD '&CMNT   OTHER: &ODSNTMP'
          PUTCARD '//*   --- SEARCH STOPPED AT OTHER DSN ---'
        END
      ELSE +
        DO
          /********************************************************
          /* LIST LIBRARIES LBXLIB1-4 , IF NOT BLANK
          /********************************************************
          SET I = 1
          SET VARNAM = &STR(&&LBXLIB)
          DO WHILE &I <= 4
            SET VARVAL = &STR(&VARNAM&I)
            IF &LENGTH(&VARVAL) > 0 THEN +
              DO
                SET XX = &STR(//*      )&I:  &LBXPROJ
                SET XX = &STR(&XX).&VARVAL
                SET XX = &STR(&XX).&LBXTYPE
                SET XX = &STR(&XX)(&LBXMBR)
                PUTCARD '&XX.'
              END
            SET I = &I + 1
          END
        END
    END


  /****************************************************************
  /* COMMENT: LIST SOURCE FOUND LIBRARY TEXT, IF PRESENT
  /****************************************************************
  IF &STR(&SRCFND) > &STR() THEN +
    DO
      PUTCARD '&SRCFND'
    END


  /****************************************************************
  /* COMMENT: LIST SOURCE FILE AND OBJ FILE, UNCONDITIONALLY
  /****************************************************************
  PUTCARD '&CMNT'
  PUTCARD '&CMNT   FROM --- &LIBMEM'
  IF &P = 6 THEN -
    PUTCARD '&CMNT     TO --- &LMDMEM'
  ELSE -
    PUTCARD '&CMNT     TO --- &LINMEM'
  PUTCARD '//*---------------------------------------'




DONE: +
EXIT CODE(&EXITCD)
