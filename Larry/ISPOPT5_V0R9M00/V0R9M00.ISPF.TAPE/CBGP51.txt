PROC 0                                /* BG ASSEMBLER               */

/********************************************************************/
/*                                                                  */
/* CLIST: CBGP51                                                    */
/*                                                                  */
/* Author: Larry Belmontes Jr.                                      */
/*         https://ShareABitofIT.net/ISPOPT5-in-MVS38J              */
/*         Copyright (C) 2021  Larry Belmontes, Jr.                 */
/*                                                                  */
/*                                                                  */
/* Description:                                                     */
/* ---------------------------------------------------------------  */
/*                                                                  */
/* Background Processing Option 5                                  '*/
/*                                                                  */
/* This CLIST creates BG Assembler JCL.                             */
/*                                                                  */
/*                                                                  */
/* Command:                                                         */
/* CBGP51                                                           */
/*                                                                  */
/* Parameters:                                                      */
/* None                                                             */
/*                                                                  */
/*                                                                  */
/* Disclaimer:                                                      */
/* ================================================================ */
/*                                                                  */
/*    No guarantee; No warranty; Install / Use at your own risk.    */
/*                                                                  */
/*    This software is provided "AS IS" and without any expressed   */
/* or implied warranties, including, without limitation, the        */
/* implied warranties of merchantability and fitness for a          */
/* particular purpose.                                              */
/*                                                                  */
/*    The author requests keeping authors name intact in any        */
/* modified versions.                                               */
/*                                                                  */
/*    In addition, the author requests readers to submit any        */
/* code modifications / enhancements and associated comments        */
/* for consideration into a subsequent release (giving credit       */
/* to contributor(s)) thus, improving overall functionality         */
/* and further benefiting the MVS 3.8J hobbyist public domain       */
/* community.                                                       */
/*                                                                  */
/*                                                                  */
/*                                                                  */
/* Change History:                                                  */
/* ================================================================ */
/* MM/DD/CCYY Version  Name / Description                           */
/* ---------- -------  -------------------------------------------- */
/* 10/31/2021 0.9.00   Larry Belmontes Jr.                          */
/*                     - Initial version released to MVS 3.8J       */
/*                       hobbyist public domain                     */
/*                                                                  */
/* 05/13/2018 0.1.00   Larry Belmontes Jr.                          */
/*                     - Initial protyping with initial version     */
/*                       of ISPF from Wally Mclaughlin              */
/*                                                                  */
/********************************************************************/

/*---START------------------- $$cbg-globals -------------------------*/
  /****************************************************************
  /* Global Variables to SHARE with subordinate CLISTS
  /****************************************************************
  GLOBAL JCLGEN TDD TDSN TDSA LIBMEM LINMEM +
         ODSNTMP ODSNMEM SRCFND CSRF CMNT P +
         LMDMEM                             +
         LBBGSO  LBBGPO                     +
         LBXODSN LBXPROJ LBXTYPE LBXMBR     +
         LBXLIB1 LBXLIB2 LBXLIB3 LBXLIB4    +
         LBXSTEP1                           +
         LBXTST  LBXOPT1                    +
         LBXGO   LBXCMPL                    +
         LBXSOUT LBXLSTID                   +
         LBXSL1  LBXSL2  LBXSL3  LBXSL4     +
         LBXJCL1 LBXJCL2 LBXJCL3 LBXJCL4
/*---END--------------------- $$cbg-globals -------------------------*/

  SET PANL0 = &STR(ISPOPT51)           /* Process Panel             */

/*---START------------------- $$cbg-pnl-val -------------------------*/
  /****************************************************************
  /* Set Panel Number, initial CURSOR field, and JCL Message
  /****************************************************************
  SET P = &SUBSTR(8:8,&PANL0)          /* Position 8 of panel name  */
  SET CSRF  = LB&P.MBR                 /* Panel Cursor Field        */
  SET EMSG  = BGOPT55&P                /* JCL Generated message     */

  /****************************************************************
  /* Display Panel and handle response
  /****************************************************************
  GETDATA:  +
  IF &CSRF > &STR() THEN +
    ISPEXEC DISPLAY PANEL(&PANL0) CURSOR(&CSRF)
  ELSE +
    ISPEXEC DISPLAY PANEL(&PANL0)
  SET RC = &LASTCC
  /* END/RETURN KEY DEPRESSED
  IF &RC = 8 THEN +
    DO
      IF &KEYPRESS = PF03 THEN +
        DO
          /*ERROR: END KEY PRESSED, PROCESS TERMINATED! NO JCL GENED
          ISPEXEC SETMSG MSG(BGOPT09)
          GOTO DONE
        END
      /************************************************************
      /* Check for JUMP (e.g. =3.4)
      /************************************************************
      IF &STR(&BGPO) > &STR( ) THEN +
        IF (&SUBSTR(1:1,&STR(&BGPO)) = &STR(=) THEN +
          DO
          /*SET LBBGPO = &STR(=)   /* Indicate JUMP */
            SET LBBGPO = &STR(&BGPO)
          /*ISPEXEC SETMSG MSG(BGOPT08) */
            GOTO DONE
          END
    END
  ELSE +
    DO
      IF &RC = 0 THEN
      /* ENTER KEY DEPRESSED, PROCESS.
      ELSE +
        DO
          /*ERROR: SEVERE ERROR WITH DISPLAY SERVICE
          WRITE SEVERE ERROR IN BG PROCESS
          WRITE DISPLAY PANEL=&PANL0  RC=&RC CSRF=&CSRF
          WRITE PROCESS TERMINATED...
          GOTO DONE
        END
    END

  /****************************************************************
  /* Timing
  /****************************************************************
  SET CPU = &SYSCPU
  SET SRV = &SYSSRV

  /****************************************************************
  /* Validate SOURCE INPUT, Set SYSIN DSN, OBJ DSN
  /****************************************************************
  %CBGVALIN PANID(&PANL0)

  SET RC=&LASTCC
  IF &RC = 0 THEN
  ELSE +
    IF &RC = 4 THEN +
      GOTO GETDATA
    ELSE +
      DO
        /*ERROR FROM CLIST
        WRITE *** ERROR IN CBGVALIN RC=&RC ***
        GOTO GETDATA
      END

  /****************************************************************
  /* COMMENT:
  /****************************************************************
  SET VARNAM = &STR(&&LB&P.CMPL)
  SET VARVAL = &STR(&VARNAM)
  PUTCARD '&CMNT.----------------------------------------'
  PUTCARD '&CMNT &P..&VARVAL'
/*---END--------------------- $$cbg-pnl-val -------------------------*/

  /****************************************************************
  /* COMMENT: SELECTED LANGUAGE PROCESSOR
  /****************************************************************
  IF &LB1CMPL = 1 THEN -
    PUTCARD '//* A S S E M B L E R   -  XF'
  ELSE -
  IF &LB1CMPL = 2 THEN -
    PUTCARD '//* A S S E M B L E R   -  XF MACRO XREF'
  ELSE -
  IF &LB1CMPL = 3 THEN -
    PUTCARD '//* A S S E M B L E R   -  G  WATERLOO'
  PUTCARD '&CMNT'

/*---START------------------- $$cbg-lib-info ------------------------*/
  /****************************************************************
  /* COMMENT: LIBRARY INFORMATION
  /****************************************************************
  %CBGLLIBS PANID(&PANL0)
  SET RC=&LASTCC
  IF &RC = 0 THEN
  ELSE +
    DO
      /*ERROR FROM CLIST
      WRITE *** ERROR IN CBGLLIBS RC=&RC ***
      WRITE *** Should not display this message !!
    END
/*---END--------------------- $$cbg-lib-info ------------------------*/

  /****************************************************************
  /* EXEC CARD
  /****************************************************************
  IF &LB1CMPL = 1 THEN -
    PUTCARD '//ASM      EXEC PGM=IFOX00,REGION=128K,'
  ELSE -
  IF &LB1CMPL = 2 THEN -
    PUTCARD '//ASM      EXEC PGM=AFOX00,REGION=512K,'
  ELSE -
  IF &LB1CMPL = 3 THEN -
    PUTCARD '//ASM      EXEC PGM=ASMGASM,REGION=1024K,'
  PUTCARD '//         COND=(12,LE),'
/*---START------------------- $$cbg-compileparm ---------------------*/
  /****************************************************************
  /* Construct PARM1 string for Compiler/Assembler Options
  /****************************************************************
  IF &STR(&LBXTST) > &STR( ) THEN -
    IF &STR(&LBXOPT1) > &STR( ) THEN -
      PUTCARD '//   PARM='&LBXTST,&LBXOPT1''
    ELSE -
      PUTCARD '//   PARM='&LBXTST''
  ELSE -
    IF &STR(&LBXOPT1) > &STR( ) THEN -
      PUTCARD '//   PARM='&LBXOPT1''
    ELSE -
      PUTCARD '//   PARM='''
/*---END--------------------- $$cbg-compileparm ---------------------*/


  /****************************************************************
  /* INSERT STEPLIB, SYSTERM
  /*        SYSPRINT, SYSPUNCH, SYSIN
  /*        SYSUT  WORK FILES
  /*        SYSLIB
  /*        SYSLIN, SYSGO
  /****************************************************************
  IF &LB1CMPL = 3 THEN +
    SET LDD = LIN
  ELSE +
    SET LDD = GO

  %CBGIJCL0 PANID(&PANL0) STEPL(Y) TERM(Y) UTX(4) SLIB(Y) SLIN(&LDD) -
      SPRT(Y) SPUN(Y) SIN(Y)
  SET RC=&LASTCC

  /****************************************************************
  /* MACREF FOR IFOX00 MACRO XREF
  /****************************************************************
  IF &LB1CMPL = 2 THEN -
    DO
      PUTCARD '//MACREF   DD DUMMY'
    END

  /****************************************************************
  /* Quick GO Step, if selected
  /****************************************************************
  IF &LB1GO = Y THEN +
    DO
      %CBGIQGO
    END

/*---START------------------- $$cbg-morejcl -------------------------*/
  /****************************************************************
  /* Append JCL statement from panel to last step generated
  /****************************************************************
  SET I = 1
  SET &VARNAM = &STR(&&LBXJCL)
  DO WHILE &I <= 4
    SET OTHJCL  = &STR(&VARNAM&I)
    IF &STR(&OTHJCL) > &STR( ) THEN +
      DO
        PUTCARD '&OTHJCL'
      END
    SET I = &I + 1
  END
/*---END--------------------- $$cbg-morejcl -------------------------*/

/*---START------------------- $$cbg-endstep -------------------------*/
  /****************************************************************
  /* End of Step
  /****************************************************************
  PUTCARD '//*--END----------------------------------'
  PUTCARD '&CMNT'

  SET JCLGEN = Y

  /****************************************************************
  /* JCL GENERATED Message from CBGP5x panel processor
  /****************************************************************
  ISPEXEC SETMSG MSG(&EMSG)

  /****************************************************************
  /* Performance Marks
  /****************************************************************
  SET CPU = &STR(&SYSCPU-&CPU)
  SET SRV = &STR(&SYSSRV-&SRV)
/*---END--------------------- $$cbg-endstep -------------------------*/

  /****************************************************************
  /* RETURN TO CALLING CLIST
  /****************************************************************

DONE: +
EXIT CODE(0)
